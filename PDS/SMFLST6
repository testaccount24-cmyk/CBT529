000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. SMFLIST.
000300 AUTHOR. MATTHEW V. STITT.
000400 DATE-WRITTEN. 06/15/91.
000500 DATE-COMPILED.
000600 REMARKS. PRINT LISTING OF REPORTS PRINTED FROM SMF RECORDS.
000700 ENVIRONMENT DIVISION.
000800 CONFIGURATION SECTION.
000900 SOURCE-COMPUTER. IBM-3081.
001000 OBJECT-COMPUTER. IBM-3081.
001100 SPECIAL-NAMES.
001200     C01 IS TO-CHAN-1.
001300 INPUT-OUTPUT SECTION.
001400 FILE-CONTROL.
001500     SELECT SMF-DATA ASSIGN TO UT-S-SYS004.
001600     SELECT REPORT-OUT ASSIGN TO UT-S-SYS019.
001700     SELECT SMF-REC-TYPE6 ASSIGN TO UT-S-SORTWK01.
001800 DATA DIVISION.
001900 FILE SECTION.
002000*FD  SMF-DATA
002020*    LABEL RECORDS ARE STANDARD
002030*    BLOCK CONTAINS 0 RECORDS
002040*    RECORD CONTAINS 20 TO 32752 CHARACTERS
002050*    RECORDING MODE IS V.
002060*01  SMF-RECORD-A            PIC X(32752).
002070*01  SMF-RECORD-B            PIC X(20).
002080 FD  SMF-DATA
002090     RECORDING MODE IS S
002100     BLOCK CONTAINS 4096 CHARACTERS
002200     RECORD CONTAINS 0 CHARACTERS
002300     LABEL RECORDS ARE STANDARD.
002400 01  SMF-FILE                    PIC X(32756).
002600 FD  REPORT-OUT
002610     RECORDING MODE IS F
002620     BLOCK CONTAINS 0 RECORDS
002630     LABEL RECORDS ARE STANDARD.
002640 01  PRINT-LINE.
002650     02  P-LINE                  PIC X(133).
002660 SD  SMF-REC-TYPE6.
002670 01  SORT-RECORD.
002680     02  SMF-TIME                PIC S9(8)  COMP.
002690     02  SMF-DATE                PIC 9(7)   COMP-3.
002700     02  SMF-TYPE6.
002800         03  SMF6JBN             PIC X(8).
002900         03  SMF6RST             PIC S9(8)  COMP.
003000         03  SMF6RSD             PIC 9(7)   COMP-3.
003100         03  SMF6UIF             PIC X(8).
003200         03  SMF6OWC             PIC X.
003300         03  SMF6WST             PIC S9(8)  COMP.
003400         03  SMF6WSD             PIC 9(7)   COMP-3.
003500         03  SMF6NLR             PIC S9(8)  COMP.
003600         03  SMF6IOE             PIC X.
003700         03  SMF6NDS             PIC X.
003800         03  SMF6FMN             PIC X(4).
003900         03  SMF6PAD1            PIC X.
004000         03  SMF6SBS             PIC S9(4)  COMP.
004100         03  SMF6LN1             PIC S9(4)  COMP.
004200         03  SMF6DCI             PIC X.
004300         03  SMF6INDC            PIC X.
004400         03  SMF6JNM             PIC X(4).
004500         03  SMF6OUT             PIC X(8).
004600         03  SMF6FCB             PIC X(4).
004700         03  SMF6UCS             PIC X(4).
004800         03  SMF6PGE             PIC S9(8)  COMP.
004900         03  SMF6RTE             PIC S9(4)  COMP.
005000     02  SMF6-PSS.
005100         03  SMF6LN2             PIC S9(4)  COMP.
005200         03  SMF6CPS.
005300             04  SMF6CPS1        PIC X.
005400             04  SMF6CPS2        PIC X.
005500             04  SMF6CPS3        PIC X.
005600             04  SMF6CPS4        PIC X.
005700             04  SMF6CPS5        PIC X.
005800             04  SMF6CPS6        PIC X.
005900             04  SMF6CPS7        PIC X.
006000             04  SMF6CPS8        PIC X.
006100         03  SMF6CHR.
006200             04  SMF6CHR1        PIC X(4).
006300             04  SMF6CHR2        PIC X(4).
006400             04  SMF6CHR3        PIC X(4).
006500             04  SMF6CHR4        PIC X(4).
006600         03  SMF6MID             PIC X(4).
006700         03  SMF6FLI             PIC X(4).
006800         03  SMF6FLC             PIC X.
006900         03  SMF6BID             PIC X.
007000     02  SMF6-RTG.
007100         03  SMF6LN3             PIC S9(4)  COMP.
007200         03  SMF6ROUT            PIC S9(8)  COMP.
007300         03  SMF6EFMN            PIC X(8).
007400 WORKING-STORAGE SECTION.
007500 77  WS-START                    PIC X(36)  VALUE
007600     'SMFLIST  WORKING STORAGE STARTS HERE'.
007610 77  PAGE-CNT                    PIC 9(5).
007620 77  LINE-COUNT                  PIC 9(5).
007700 77  WORK-CHAR                   PIC X.
007800 77  WORK-BIT-EXPAND             PIC S9(5)  COMP-3  VALUE ZERO.
007900 77  WORK-JUL                    PIC 9(5).
007910 77  PREV-PRTR-TYPE              PIC S9(4)  COMP.
008000 77  G207-FUNC10                 PIC XX     VALUE '10'.
008010 77  G207-FUNC14                 PIC XX     VALUE '14'.
008020 77  G207-FUNC16                 PIC XX     VALUE '16'.
008030 77  G207-FUNC34                 PIC XX     VALUE '34'.
008100 77  X                           PIC S9(8)  COMP VALUE ZERO.
008200 77  Y                           PIC S9(8)  COMP VALUE ZERO.
008300 77  Z                           PIC S9(8)  COMP VALUE ZERO.
008400 77  SMF4-RELOC-DISP             PIC S9(8)  COMP VALUE ZERO.
008500 01  SMF-RECORD.
008600*    02  REC-LEN                 PIC XX.
008700*    02  SEG-DESC                PIC XX.
008800     02  SYS-IND                 PIC X.
008900     02  REC-TYPE                PIC X.
009000     02  REC-TIME                PIC S9(8)  COMP.
009100     02  REC-DATE                PIC 9(7)   COMP-3.
009200     02  SYS-IDENT               PIC X(4).
009300     02  SMF-DATA-REC            PIC X(32760).
009400 01  D-TIME.
009500     02  D-HH                    PIC 99.
009600     02  FILLER                  PIC X      VALUE '.'.
009700     02  D-MM                    PIC 99.
009800     02  FILLER                  PIC X      VALUE '.'.
009900     02  D-SS                    PIC 99.
011910 01  HEAD-1.
011920     02  FILLER                  PIC X     VALUE SPACE.
011930     02  HEAD-PROGRAM            PIC X(8)  VALUE SPACE.
011940     02  FILLER                  PIC X(47) VALUE SPACE.
011950     02  FILLER                  PIC X(22) VALUE
011960         'HUNTSVILLE HOSPITAL   '.
011970     02  FILLER                  PIC X(45) VALUE SPACE.
011980     02  FILLER                  PIC X(5)  VALUE 'PAGE '.
011990     02  HEAD-PAGE               PIC ZZZZ9 VALUE ZERO.
011991 01  HEAD-2.
011992     02  FILLER                  PIC X     VALUE SPACE.
011993     02  FILLER                  PIC X(5)  VALUE 'DATE '.
011994     02  HEAD-DATE               PIC X(10) VALUE SPACE.
011995     02  FILLER                  PIC X(11) VALUE SPACE.
011996     02  HEAD-SYS-DESC           PIC X(80) VALUE SPACE.
011997 01  HEAD-3.
011998     02  FILLER                  PIC X     VALUE SPACE.
011999     02  FILLER                  PIC X(51) VALUE SPACE.
012000     02  HEAD-SEQ                PIC X(30) VALUE SPACE.
012100 01  HEAD-4.
012200     02  FILLER                  PIC X(2)  VALUE SPACE.
012300     02  FILLER                  PIC X(8)  VALUE 'JOB     '.
012400     02  FILLER                  PIC X(1)  VALUE SPACE.
012500     02  FILLER                  PIC X(4)  VALUE 'FORM'.
012600     02  FILLER                  PIC X(1)  VALUE SPACE.
012601     02  FILLER                  PIC X(3)  VALUE 'PRT'.
012610     02  FILLER                  PIC X(1)  VALUE SPACE.
012700     02  FILLER                  PIC X(4)  VALUE 'PAGE'.
012800     02  FILLER                  PIC X(1)  VALUE SPACE.
012900     02  FILLER                  PIC X(8)  VALUE ' RECORDS'.
013000     02  FILLER                  PIC X(1)  VALUE SPACE.
013100     02  FILLER                  PIC X(10) VALUE '   START  '.
013200     02  FILLER                  PIC X(1)  VALUE SPACE.
013300     02  FILLER                  PIC X(8)  VALUE '  START '.
013400     02  FILLER                  PIC X(1)  VALUE SPACE.
013500     02  FILLER                  PIC X(10) VALUE '    END   '.
013600     02  FILLER                  PIC X(1)  VALUE SPACE.
013700     02  FILLER                  PIC X(8)  VALUE '   END  '.
013800     02  FILLER                  PIC X(1)  VALUE SPACE.
013900     02  FILLER                  PIC X(8)  VALUE 'DURATION'.
014000 01  HEAD-5.
014100     02  FILLER                  PIC X(2)  VALUE SPACE.
014200     02  FILLER                  PIC X(8)  VALUE 'NAME    '.
014300     02  FILLER                  PIC X(1)  VALUE SPACE.
014400     02  FILLER                  PIC X(4)  VALUE 'NAME'.
014510     02  FILLER                  PIC X(1)  VALUE SPACE.
014520     02  FILLER                  PIC X(3)  VALUE 'CLS'.
014530     02  FILLER                  PIC X(1)  VALUE SPACE.
014600     02  FILLER                  PIC X(4)  VALUE 'NAME'.
014700     02  FILLER                  PIC X(1)  VALUE SPACE.
014800     02  FILLER                  PIC X(8)  VALUE '  OUTPUT'.
014900     02  FILLER                  PIC X(1)  VALUE SPACE.
015000     02  FILLER                  PIC X(10) VALUE '   DATE   '.
015100     02  FILLER                  PIC X(1)  VALUE SPACE.
015200     02  FILLER                  PIC X(8)  VALUE '  TIME  '.
015300     02  FILLER                  PIC X(1)  VALUE SPACE.
015310     02  FILLER                  PIC X(10) VALUE '   DATE   '.
015500     02  FILLER                  PIC X(1)  VALUE SPACE.
015510     02  FILLER                  PIC X(8)  VALUE '  TIME  '.
015700     02  FILLER                  PIC X(1)  VALUE SPACE.
015710     02  FILLER                  PIC X(8)  VALUE '  TIME  '.
015900 01  DETAIL-LINE.
016000     02  FILLER                  PIC X(2).
016100     02  D-JOB-NAME              PIC X(8).
016200     02  FILLER                  PIC X(1).
016300     02  D-FORM-NAME             PIC X(4).
016310     02  FILLER                  PIC X(2).
016320     02  D-PRT-CLASS             PIC X(1).
016400     02  FILLER                  PIC X(2).
016500     02  D-FCB-NAME              PIC X(4).
016600     02  FILLER                  PIC X(1).
016700     02  D-RECS-OUT              PIC Z(8).
016800     02  FILLER                  PIC X(1).
016900     02  D-START-DATE            PIC X(10).
017000     02  FILLER                  PIC X(1).
017100     02  D-START-TIME            PIC X(8).
017200     02  FILLER                  PIC X(1).
017300     02  D-END-DATE              PIC X(10).
017400     02  FILLER                  PIC X(1).
017500     02  D-END-TIME              PIC X(8).
017600     02  FILLER                  PIC X(1).
017700     02  D-DUR-TIME              PIC X(8).
017800 01  WORK-BINARY                 PIC 9(8)  COMP.
017900 01  BINARY-WORK             REDEFINES WORK-BINARY
018000                                 PIC XXXX.
018100 01  BINARY-WORK-1           REDEFINES WORK-BINARY.
018200     02  FILLER                  PIC XXX.
018300     02  BINARY-ONE              PIC X.
018400 01  BINARY-WORK-2           REDEFINES WORK-BINARY.
018500     02  FILLER                  PIC XX.
018600     02  BINARY-TWO              PIC XX.
018700 01  BINARY-WORK-3           REDEFINES WORK-BINARY.
018800     02  FILLER                  PIC X.
018900     02  BINARY-THREE            PIC XXX.
019000 01  BINARY-WORK-4           REDEFINES WORK-BINARY.
019100     02  BINARY-FOUR             PIC X(4).
019200 SKIP1
019300 01  EXPAND-BYTE.
019400     02  BIT-ZERO                PIC X.
019500     02  BIT-ONE                 PIC X.
019600     02  BIT-TWO                 PIC X.
019700     02  BIT-THREE               PIC X.
019800     02  BIT-FOUR                PIC X.
019900     02  BIT-FIVE                PIC X.
020000     02  BIT-SIX                 PIC X.
020100     02  BIT-SEVEN               PIC X.
020200 01  WORK-ARRAY-ONE.
020300     02  ARRAY-ONE           OCCURS 32760 TIMES  INDEXED BY A1.
020400         03  CHAR-ONE            PIC X.
020500 01  WORK-ARRAY-TWO.
020600     02  ARRAY-TWO           OCCURS 32760 TIMES  INDEXED BY A2.
020700         03  CHAR-TWO            PIC X.
020800 PROCEDURE DIVISION.
020810 BEGIN-PROGRAM SECTION.
020900     SORT SMF-REC-TYPE6 ASCENDING SMF6SBS, SMF-DATE, SMF-TIME
021000         INPUT PROCEDURE IS SELECT-RECORDS
021100         OUTPUT PROCEDURE IS PRINT-REPORT.
021200 FINISH-JOB.
021300     GOBACK.
021400*****************************************************************
021500*  THIS ROUTINE WILL FORMAT A BINARY BYTE (EIGHT BITS) INTO     *
021600*  EIGHT BYTES WITH THE BIT ON AND OFF REPRESENTATION IN EACH   *
021700*  BYTE.  THE BINARY BYTE IS PUT INTO FIELD WORK-CHAR AND THE   *
021800*  EIGHT BYTES ARE RETURNED IN FIELD EXPAND-BYTE.  THE FOUR     *
021900*  BYTE FIELDS WORK-BINARY ARE USED HEAVILY IN THIS ROUTINE     *
022000*****************************************************************
022100 EXPAND-BITS SECTION.
022200     MOVE LOW-VALUES TO BINARY-WORK.
022300     MOVE WORK-CHAR TO BINARY-ONE.
022400     COMPUTE WORK-BIT-EXPAND = WORK-BINARY.
022500     MOVE ALL ZERO TO EXPAND-BYTE.
022600     SUBTRACT 128 FROM WORK-BIT-EXPAND.
022700     IF WORK-BIT-EXPAND LESS THAN ZERO
022800         MOVE '0' TO BIT-ZERO
022900         ADD 128 TO WORK-BIT-EXPAND
023000       ELSE MOVE '1' TO BIT-ZERO.
023100     SUBTRACT 064 FROM WORK-BIT-EXPAND.
023200     IF WORK-BIT-EXPAND LESS THAN ZERO
023300         MOVE '0' TO BIT-ONE
023400         ADD 064 TO WORK-BIT-EXPAND
023500       ELSE MOVE '1' TO BIT-ONE.
023600     SUBTRACT 032 FROM WORK-BIT-EXPAND.
023700     IF WORK-BIT-EXPAND LESS THAN ZERO
023800         MOVE '0' TO BIT-TWO
023900         ADD 032 TO WORK-BIT-EXPAND
024000       ELSE MOVE '1' TO BIT-TWO.
024100     SUBTRACT 016 FROM WORK-BIT-EXPAND.
024200     IF WORK-BIT-EXPAND LESS THAN ZERO
024300         MOVE '0' TO BIT-THREE
024400         ADD 016 TO WORK-BIT-EXPAND
024500       ELSE MOVE '1' TO BIT-THREE.
024600     SUBTRACT 008 FROM WORK-BIT-EXPAND.
024700     IF WORK-BIT-EXPAND LESS THAN ZERO
024800         MOVE '0' TO BIT-FOUR
024900         ADD 008 TO WORK-BIT-EXPAND
025000       ELSE MOVE '1' TO BIT-FOUR.
025100     SUBTRACT 004 FROM WORK-BIT-EXPAND.
025200     IF WORK-BIT-EXPAND LESS THAN ZERO
025300         MOVE '0' TO BIT-FIVE
025400         ADD 004 TO WORK-BIT-EXPAND
025500       ELSE MOVE '1' TO BIT-FIVE.
025600     SUBTRACT 002 FROM WORK-BIT-EXPAND.
025700     IF WORK-BIT-EXPAND LESS THAN ZERO
025800         MOVE '0' TO BIT-SIX
025900         ADD 002 TO WORK-BIT-EXPAND
026000       ELSE MOVE '1' TO BIT-SIX.
026100     SUBTRACT 001 FROM WORK-BIT-EXPAND.
026200     IF WORK-BIT-EXPAND LESS THAN ZERO
026300         MOVE '0' TO BIT-SEVEN
026400         ADD 001 TO WORK-BIT-EXPAND
026500       ELSE MOVE '1' TO BIT-SEVEN.
026600 SELECT-RECORDS SECTION.
026700     OPEN INPUT SMF-DATA.
026800 READ-FILE.
026900     MOVE LOW-VALUES TO SMF-RECORD.
027000     READ SMF-DATA INTO SMF-RECORD AT END
027100         GO TO END-SELECT.
027200     MOVE REC-TYPE TO WORK-CHAR.
027300     MOVE LOW-VALUES TO BINARY-WORK.
027400     MOVE WORK-CHAR TO BINARY-ONE.
027500     COMPUTE WORK-BIT-EXPAND = WORK-BINARY.
027600     IF WORK-BIT-EXPAND NOT = 6
027700         GO TO READ-FILE.
027800     MOVE SMF-DATA-REC TO SMF-TYPE6.
027900     MOVE SMF6PAD1 TO WORK-CHAR.
028000     PERFORM EXPAND-BITS.
028100     MOVE LOW-VALUES TO WORK-ARRAY-ONE.
028200     MOVE SMF-RECORD TO WORK-ARRAY-ONE.
028210*    DIVIDE X BY Y GIVING Z.
028300     MOVE 94 TO X.
028400     SET A1 TO X.
028500     IF BIT-ZERO NOT = '1'
028600         GO TO ROUTING-PRESENT.
028700 PSS-PRESENT.
028800     MOVE ZERO TO X.
028900     SET A2 TO X.
029000     MOVE LOW-VALUES TO WORK-ARRAY-TWO.
029100 PSS-LOOP.
029200     SET A2 UP BY 1.
029300     IF A2 > 36
029400         GO TO END-PSS-LOOP.
029500     SET A1 UP BY 1.
029600     MOVE CHAR-ONE (A1) TO CHAR-TWO (A2).
029700     GO TO PSS-LOOP.
029800 END-PSS-LOOP.
029900     MOVE WORK-ARRAY-TWO TO SMF6-PSS.
030000 ROUTING-PRESENT.
030100     IF BIT-ONE NOT = '1'
030200         GO TO OUTPUT-RECORD.
030300     MOVE ZERO TO X.
030400     SET A2 TO X.
030500     MOVE LOW-VALUES TO WORK-ARRAY-TWO.
030600 RTG-LOOP.
030700     SET A2 UP BY 1.
030800     IF A2 > 14
030900         GO TO END-RTG-LOOP.
031000     SET A1 UP BY 1.
031100     MOVE CHAR-ONE (A1) TO CHAR-TWO (A2).
031200     GO TO RTG-LOOP.
031300 END-RTG-LOOP.
031400     MOVE WORK-ARRAY-TWO TO SMF6-RTG.
031500 OUTPUT-RECORD.
031600     MOVE REC-DATE TO SMF-DATE.
031700     MOVE REC-TIME TO SMF-TIME.
031800     RELEASE SORT-RECORD.
031900     GO TO READ-FILE.
032000 END-SELECT.
032100     CLOSE SMF-DATA.
032200 END-SELECT-EXIT.
032300     EXIT.
032400 PRINT-REPORT SECTION.
032500     OPEN OUTPUT REPORT-OUT.
032600     MOVE 'SMFLIST ' TO HEAD-PROGRAM.
032700     MOVE 'SYSTEM MEASUREMENT FACILITY -- JOB OUTPUTS'
032800         TO HEAD-SYS-DESC.
033000     CALL 'G207DCAN' USING G207-FUNC14, HEAD-DATE.
033001     RETURN SMF-REC-TYPE6 AT END
033002         GO TO FINISH-REPORT.
033010     MOVE SMF6SBS TO PREV-PRTR-TYPE.
033100     PERFORM PRINT-HEADINGS-ROUTINE THROUGH PRINT-EXIT.
033200     GO TO GET-SMF-DATA-A.
033300 PRINT-HEADINGS-ROUTINE.
033301     IF SMF6SBS = 0
033302         MOVE 'EXTERNAL WRITER TASKS' TO HEAD-SEQ.
033303     IF SMF6SBS = 2
033304         MOVE 'JES2 PRINTERS' TO HEAD-SEQ.
033305     IF SMF6SBS = 5
033306         MOVE 'JES3 PRINTERS' TO HEAD-SEQ.
033307     IF SMF6SBS = 7
033308         MOVE 'PSF PRINTERS' TO HEAD-SEQ.
033309     IF SMF6SBS = 9
033310         MOVE 'PRINTWAY PRINTERS' TO HEAD-SEQ.
033311     ADD 1 TO PAGE-CNT
033320     MOVE PAGE-CNT TO HEAD-PAGE.
033330     MOVE HEAD-1 TO PRINT-LINE.
033340     WRITE PRINT-LINE AFTER ADVANCING TO-CHAN-1.
033350     MOVE HEAD-2 TO PRINT-LINE.
033360     WRITE PRINT-LINE AFTER ADVANCING 1 LINES.
033370     MOVE HEAD-3 TO PRINT-LINE.
033380     WRITE PRINT-LINE AFTER ADVANCING 1 LINES.
033390     MOVE HEAD-4 TO PRINT-LINE.
033391     WRITE PRINT-LINE AFTER ADVANCING 2 LINES.
033392     MOVE HEAD-5 TO PRINT-LINE.
033393     WRITE PRINT-LINE AFTER ADVANCING 1 LINES.
033394     MOVE SPACE TO PRINT-LINE.
033395     MOVE ZERO TO LINE-COUNT.
033396 PRINT-1.
033397     WRITE PRINT-LINE AFTER ADVANCING 1 LINES.
033398     ADD 1 TO LINE-COUNT.
033399     IF LINE-COUNT > 56
033400         PERFORM PRINT-HEADINGS-ROUTINE.
033401 PRINT-EXIT.
033402     EXIT.
033410 GET-SMF-DATA.
033500     RETURN SMF-REC-TYPE6 AT END
033600         GO TO FINISH-REPORT.
033610     IF SMF6SBS NOT = PREV-PRTR-TYPE
033620         PERFORM PRINT-HEADINGS-ROUTINE.
033621 GET-SMF-DATA-A.
033630     MOVE SMF6SBS TO PREV-PRTR-TYPE.
033700     MOVE SPACE TO DETAIL-LINE.
033800     MOVE SMF6JBN TO D-JOB-NAME.
033900     MOVE SMF6FMN TO D-FORM-NAME.
033910     MOVE SMF6OWC TO D-PRT-CLASS.
034000*    MOVE SMF6FCB TO D-FCB-NAME.
034010     MOVE SMF6FLI TO D-FCB-NAME.
034100     MOVE SMF6NLR TO D-RECS-OUT.
034200     DIVIDE SMF-TIME BY 100 GIVING X.
034300     DIVIDE X BY 3600 GIVING D-HH
034400         REMAINDER Y.
034500     DIVIDE Y BY 60 GIVING D-MM
034600         REMAINDER D-SS.
034700     MOVE D-TIME TO D-END-TIME.
034800     MOVE SMF-DATE TO WORK-JUL.
034900     CALL 'G207DCAN' USING G207-FUNC34, WORK-JUL D-END-DATE.
035000     DIVIDE SMF6WST BY 100 GIVING X.
035100     DIVIDE X BY 3600 GIVING D-HH
035200         REMAINDER Y.
035300     DIVIDE Y BY 60 GIVING D-MM
035400         REMAINDER D-SS.
035500     MOVE D-TIME TO D-START-TIME.
035600     MOVE SMF6WSD TO WORK-JUL.
035700     CALL 'G207DCAN' USING G207-FUNC34, WORK-JUL D-START-DATE.
035800     IF SMF6WST > SMF-TIME
035900         ADD 8640000 TO SMF-TIME.
036000     SUBTRACT SMF-TIME FROM SMF6WST GIVING Z.
036100     DIVIDE Z BY 100 GIVING X.
036200     DIVIDE X BY 3600 GIVING D-HH
036300         REMAINDER Y.
036400     DIVIDE Y BY 60 GIVING D-MM
036500         REMAINDER D-SS.
036600     MOVE D-TIME TO D-DUR-TIME.
036700     MOVE DETAIL-LINE TO P-LINE.
036800     PERFORM PRINT-1 THROUGH PRINT-EXIT.
036900     GO TO GET-SMF-DATA.
037000 FINISH-REPORT.
037100     CLOSE REPORT-OUT.
037200 PRINT-REPORT-EXIT.
037300     EXIT.
