000100 ID DIVISION.
000200 PROGRAM-ID. SMFLISTB.
000300 AUTHOR. MATTHEW STITT.
000400 DATE-WRITTEN. 06/28/91.
000500 DATE-COMPILED.
000600 REMARKS.  PRINTS LIST OF SMF TYPE 14 AND 15 RECORDS.
000700 ENVIRONMENT DIVISION.
000800 CONFIGURATION SECTION.
000900 SOURCE-COMPUTER.  IBM-3081.
001000 OBJECT-COMPUTER.  IBM-3081.
001100 SPECIAL-NAMES.
001200     C01 IS TO-CHAN-1.
001300 INPUT-OUTPUT SECTION.
001400 FILE-CONTROL.
001500     SELECT PARMIN   ASSIGN TO UT-S-SYS017.
001600     SELECT SMFIN    ASSIGN TO UT-S-SYS004.
001700     SELECT REPORT-OUT ASSIGN TO UT-S-SYS019.
001800     SELECT DATA-OUT ASSIGN TO UT-S-SYS020.
001810     SELECT SMF-REC-TYPE14 ASSIGN TO UT-S-SORTWK01.
001900 DATA DIVISION.
002000 FILE SECTION.
002100 FD  PARMIN
002200     RECORDING MODE IS F
002210     LABEL RECORDS ARE STANDARD
002300     BLOCK CONTAINS 0 RECORDS.
002400 01  PARM-RECORD.
002500     03  PARM-TYPE           PIC X(02).
002600     03  PARM-IO-TYPE        PIC X(01).
002700     03  PARM-DSN            PIC X(44).
002800     03  PARM-JOBX       REDEFINES PARM-DSN.
002900         05  PARM-JOB        PIC X(08).
003000         05  FILLER          PIC X(36).
003100     03  PARM-ARRAY      REDEFINES PARM-DSN.
003200         05  PARM-CHAR   OCCURS 44 TIMES
003300                             PIC X.
003400     03  FILLER              PIC X(33).
003500 FD  SMFIN
003600     LABEL RECORDS ARE STANDARD
003700     BLOCK CONTAINS 32760 CHARACTERS
003800     RECORD CONTAINS 0 CHARACTERS
003900     RECORDING MODE IS S.
004000 01  DUMMY-RECORD            PIC X(32760).
004010 SD  SMF-REC-TYPE14.
004100 01  SMFTYPE14.
004200     03  SMF14FLG            PIC X(01).
004300     03  SMF14RTY            PIC X(01).
004400     03  SMF14TME            PIC 9(08) COMP.
004500     03  SMF14DTE            PIC 9(07) COMP-3.
004600     03  SMF14SID            PIC X(04).
004700     03  SMF14JBN            PIC X(08).
004800     03  SMF14RST            PIC 9(08)  COMP.
004900     03  SMF14RSD            PIC 9(07)  COMP-3.
005000     03  SMF14UID            PIC X(08).
005100     03  SMF14RIN            PIC X(02).
005200     03  SMF14DSC            PIC X(01).
005300     03  SMF14NUC            PIC X(01).
005400     03  SMF14SUC            PIC X(01).
005500     03  SMF14SET            PIC X(01).
005600     03  SMF14OPN            PIC 9(08)  COMP.
005700     03  SMFTIOE1            PIC X(01).
005800     03  SMFTIOE2            PIC X(01).
005900     03  SMFTIOE3            PIC X(01).
006000     03  SMFTIOE4            PIC X(01).
006100     03  SMFTIOE5            PIC X(08).
006200     03  SMFTIOE6            PIC X(03).
006300     03  SMFTIOE7            PIC X(01).
006400     03  SMFJFCB.
006500         05  SMFDSN.
006600             07 PART1.
006700                09  DSN1     PIC X(01).
006800                09  FILLER   PIC X(05).
006900             07 FILLER       PIC X(38).
007000         05  FILLER          PIC X(132).
007100     03  SMFDCBOR            PIC X(02).
007200     03  SMFDCBRF            PIC X(01).
007300     03  SMFDCBMF            PIC X(02).
007400     03  SMFDCBFL            PIC X(01).
007500     03  SMFDCBOP            PIC X(01).
007600     03  SMF14RV2            PIC X(01).
007700     03  SMFDEBFL            PIC X(01).
007800     03  SMFDEBOP            PIC X(01).
007900     03  SMFDEBVL            PIC 9(04)  COMP.
008000     03  DASD-DCB.
008100         05  SMF14NTU        PIC 9(08)  COMP.
008200         05  SMF14NTR        PIC 9(08)  COMP.
008300         05  SMF14NER        PIC X(01).
008400         05  SMF14RV4        PIC X(03).
008410         05  SMF14OPD        PIC 9(07)  COMP-3.
008500     03  TAPE-DCB        REDEFINES DASD-DCB.
008600         05  SMFDCBBL        PIC 9(08)  COMP.
008700         05  SMFDSSNO        PIC X(06).
008800         05  SMF14RV3        PIC X(02).
008810         05  SMF14OPE        PIC 9(07)  COMP-3.
008900     03  SMFUCBDV            PIC X(02).
009000     03  SMFSRTEV.
009100         05  VOLTYPE         PIC X(04).
009200         05  FILLER          PIC X(02).
009300     03  SMFUCBTY            PIC X(04).
009400     03  SMFSRTES            PIC X(01).
009500     03  SMF14NEX            PIC X(01).
009600     03  SMF14RV5            PIC X(02).
009700     03  SMFEXCP             PIC S9(09) COMP.
009800     03  SMF14NTA            PIC S9(09) COMP.
009900 FD  REPORT-OUT
009910     RECORDING MODE IS F
010000     LABEL RECORDS ARE OMITTED.
010100 01  PRINT-LINE.
010200     02  P-LINE              PIC X(133).
010300 FD  DATA-OUT
010310     RECORDING MODE IS F
010400     BLOCK CONTAINS 0 RECORDS
010500     LABEL RECORDS ARE STANDARD.
010600 01  RECORD-OUT.
010700     02  R-JOB-NAME              PIC X(8).
010800     02  R-FILE-NAME             PIC X(8).
010900     02  R-DATASET-NAME          PIC X(44).
011000     02  R-UNIT-ADDR.
011100         03  R-ADDR-1            PIC X.
011200         03  R-ADDR-2            PIC X.
011300         03  R-ADDR-3            PIC X.
011400         03  R-ADDR-4            PIC X.
011500     02  R-VOL-SER               PIC X(6).
011600     02  R-END-DATE              PIC X(8).
011700     02  R-END-TIME              PIC X(8).
011800     02  R-BLK-CNT               PIC 9(15).
011900 WORKING-STORAGE SECTION.
012000 77  NBR-RECORDS-READ        PIC 9(18) VALUE ZERO.
012100 77  NBR-RECORDS-USED        PIC 9(18) VALUE ZERO.
012200 77  ED-SUB                  PIC 9(03) COMP VALUE ZERO.
012300 77  ID-SUB                  PIC 9(03) COMP VALUE ZERO.
012400 77  EJ-SUB                  PIC 9(03) COMP VALUE ZERO.
012500 77  IJ-SUB                  PIC 9(03) COMP VALUE ZERO.
012510 77  EU-SUB                  PIC 9(03) COMP VALUE ZERO.
012520 77  IU-SUB                  PIC 9(03) COMP VALUE ZERO.
012600 77  ED-SUB2                 PIC 9(03) COMP VALUE ZERO.
012700 77  ID-SUB2                 PIC 9(03) COMP VALUE ZERO.
012800 77  EJ-SUB2                 PIC 9(03) COMP VALUE ZERO.
012900 77  IJ-SUB2                 PIC 9(03) COMP VALUE ZERO.
012910 77  EU-SUB2                 PIC 9(03) COMP VALUE ZERO.
012920 77  IU-SUB2                 PIC 9(03) COMP VALUE ZERO.
012930 77  EV-SUB                  PIC 9(03) COMP VALUE ZERO.
012940 77  IV-SUB                  PIC 9(03) COMP VALUE ZERO.
012950 77  EV-SUB2                 PIC 9(03) COMP VALUE ZERO.
012960 77  IV-SUB2                 PIC 9(03) COMP VALUE ZERO.
013070 77  PAGE-CNT                PIC 9(07) COMP VALUE ZERO.
013100 77  LINE-COUNT              PIC 9(03) COMP VALUE ZERO.
013200 77  PROCESS-ALL             PIC X(01) VALUE SPACE.
013300 77  CONT                    PIC X(01) VALUE SPACE.
013400 77  IO                      PIC X(01) VALUE SPACE.
013500 77  DASD-FLAG               PIC X(1).
013600 77  VIO-FLAG                PIC X(1).
013700 77  CHK-MASK                PIC X(1).
013800 77  CHK-LN                  PIC 9(02).
013900 77  W                       PIC 9.
014000 77  X                       PIC 9(8)  COMP.
014100 77  Y                       PIC 9(8)  COMP.
014200 77  WORK-CHAR                   PIC X.
014300 77  AB-CODE                     PIC S9(3)  COMP    VALUE ZERO.
014400 77  WORK-BIT-EXPAND             PIC S9(5)  COMP-3  VALUE ZERO.
014500 77  WORK-JUL                    PIC 9(5).
014600 77  WORK-JUL-1                  PIC 9(5).
014700 77  G207-FUNC01                 PIC XX     VALUE '01'.
014710 77  G207-FUNC10                 PIC XX     VALUE '10'.
014800 01  D-TIME.
014900     02  D-HH                    PIC 99.
015000     02  FILLER                  PIC X      VALUE '.'.
015100     02  D-MM                    PIC 99.
015200     02  FILLER                  PIC X      VALUE '.'.
015300     02  D-SS                    PIC 99.
015400 01  WORK-BINARY                 PIC 9(8)  COMP.
015500 01  BINARY-WORK             REDEFINES WORK-BINARY
015600                                 PIC XXXX.
015700 01  BINARY-WORK-1           REDEFINES WORK-BINARY.
015800     02  FILLER                  PIC XXX.
015900     02  BINARY-ONE              PIC X.
016000 01  BINARY-WORK-2           REDEFINES WORK-BINARY.
016100     02  FILLER                  PIC XX.
016200     02  BINARY-TWO              PIC XX.
016300 01  BINARY-WORK-3           REDEFINES WORK-BINARY.
016400     02  FILLER                  PIC X.
016500     02  BINARY-THREE            PIC XXX.
016600 01  BINARY-WORK-4           REDEFINES WORK-BINARY.
016700     02  BINARY-FOUR             PIC X(4).
016800 01  EXPAND-BYTE.
016900     02  BIT-ZERO                PIC X.
017000     02  BIT-ONE                 PIC X.
017100     02  BIT-TWO                 PIC X.
017200     02  BIT-THREE               PIC X.
017300     02  BIT-FOUR                PIC X.
017400     02  BIT-FIVE                PIC X.
017500     02  BIT-SIX                 PIC X.
017600     02  BIT-SEVEN               PIC X.
017700 01  HEAD-1.
017800     02  FILLER                  PIC X     VALUE SPACE.
017900     02  HEAD-PROGRAM            PIC X(8)  VALUE SPACE.
018000     02  FILLER                  PIC X(47) VALUE SPACE.
018100     02  FILLER                  PIC X(22) VALUE
018200         'ABC DISTRIBUTING, LLC '.
018300     02  FILLER                  PIC X(45) VALUE SPACE.
018400     02  FILLER                  PIC X(5)  VALUE 'PAGE '.
018500     02  HEAD-PAGE               PIC ZZZZ9 VALUE ZERO.
018600 01  HEAD-2.
018700     02  FILLER                  PIC X     VALUE SPACE.
018800     02  FILLER                  PIC X(5)  VALUE 'DATE '.
018900     02  HEAD-DATE               PIC X(10) VALUE SPACE.
019000     02  FILLER                  PIC X(11) VALUE SPACE.
019100     02  HEAD-SYS-DESC           PIC X(80) VALUE SPACE.
019200 01  HEAD-3.
019300     02  FILLER                  PIC X     VALUE SPACE.
019400     02  FILLER                  PIC X(51) VALUE SPACE.
019500     02  HEAD-SEQ                PIC X(30) VALUE SPACE.
019600 01  HEAD-4.
019700     02  FILLER                  PIC X(2)  VALUE SPACE.
019800     02  FILLER                  PIC X(8)  VALUE 'JOB'.
019900     02  FILLER                  PIC X(2)  VALUE SPACE.
019910     02  FILLER                  PIC X(8)  VALUE 'USER'.
019920     02  FILLER                  PIC X(2)  VALUE SPACE.
020000     02  FILLER                  PIC X(8)  VALUE 'FILE'.
020100     02  FILLER                  PIC X(2)  VALUE SPACE.
020200     02  FILLER                  PIC X(44) VALUE 'DATASET'.
020300     02  FILLER                  PIC X(2)  VALUE SPACE.
020400     02  FILLER                  PIC X(4)  VALUE 'UNIT'.
020500     02  FILLER                  PIC X(2)  VALUE SPACE.
020600     02  FILLER                  PIC X(6)  VALUE 'VOLUME'.
020700     02  FILLER                  PIC X(2)  VALUE SPACE.
020800     02  FILLER                  PIC X(3)  VALUE 'END'.
020900     02  FILLER                  PIC X(7)  VALUE SPACE.
021000     02  FILLER                  PIC X(3)  VALUE 'END'.
021100     02  FILLER                  PIC X(7)  VALUE SPACE.
021200     02  FILLER                  PIC X(5)  VALUE 'BLOCK'.
021300 01  HEAD-5.
021400     02  FILLER                  PIC X(2)  VALUE SPACE.
021500     02  FILLER                  PIC X(8)  VALUE 'NAME'.
021600     02  FILLER                  PIC X(2)  VALUE SPACE.
021610     02  FILLER                  PIC X(8)  VALUE ' ID '.
021620     02  FILLER                  PIC X(2)  VALUE SPACE.
021700     02  FILLER                  PIC X(8)  VALUE 'NAME'.
021800     02  FILLER                  PIC X(2)  VALUE SPACE.
021900     02  FILLER                  PIC X(44) VALUE 'NAME'.
022000     02  FILLER                  PIC X(2)  VALUE SPACE.
022100     02  FILLER                  PIC X(4)  VALUE 'ADDR'.
022200     02  FILLER                  PIC X(3)  VALUE SPACE.
022300     02  FILLER                  PIC X(4)  VALUE 'NAME'.
022400     02  FILLER                  PIC X(3)  VALUE SPACE.
022500     02  FILLER                  PIC X(4)  VALUE 'DATE'.
022600     02  FILLER                  PIC X(6)  VALUE SPACE.
022700     02  FILLER                  PIC X(4)  VALUE 'TIME'.
022800     02  FILLER                  PIC X(6)  VALUE SPACE.
022900     02  FILLER                  PIC X(5)  VALUE 'COUNT'.
023000 01  DETAIL-LINE.
023100     02  FILLER                  PIC X(2).
023200     02  D-JOB-NAME              PIC X(8).
023300     02  FILLER                  PIC X(2).
023310     02  D-USER-ID               PIC X(8).
023320     02  FILLER                  PIC X(2).
023400     02  D-FILE-NAME             PIC X(8).
023500     02  FILLER                  PIC X(2).
023600     02  D-DATASET-NAME          PIC X(44).
023700     02  FILLER                  PIC X(2).
023800     02  D-UNIT-ADDR.
023900         03  D-ADDR-1            PIC X.
024000         03  D-ADDR-2            PIC X.
024100         03  D-ADDR-3            PIC X.
024200         03  D-ADDR-4            PIC X.
024300     02  FILLER                  PIC X(2).
024400     02  D-VOL-SER               PIC X(6).
024500     02  FILLER                  PIC X(2).
024600     02  D-END-DATE              PIC X(8).
024700     02  FILLER                  PIC X(2).
024800     02  D-END-TIME              PIC X(8).
024900     02  FILLER                  PIC X(2).
025000     02  D-BLK-CNT               PIC Z(15).
025100 01  WORK-ARRAY-ONE.
025200     02  ARRAY-ONE           OCCURS 32760 TIMES  INDEXED BY A1.
025300         03  CHAR-ONE            PIC X.
025400 01  WORK-ARRAY-TWO.
025500     02  ARRAY-TWO           OCCURS 32760 TIMES  INDEXED BY A2.
025600         03  CHAR-TWO            PIC X.
025610*------------------------------------------------------------*
025620*  EXCLUDE DATA SET NAME TABLE                               *
025630*------------------------------------------------------------*
025640 01  ED-TABLE.
025650     05  ED-ENTRY OCCURS 100 TIMES.
025660         10  ED-MASK               PIC X(1) VALUE SPACE.
025670         10  ED-IO                 PIC X(1) VALUE SPACE.
025680         10  ED-LN                 PIC 9(02) VALUE ZERO.
025690         10  ED-DSN                PIC X(44) VALUE LOW-VALUES.
025691*------------------------------------------------------------*
025692*  INCLUDE DATA SET NAME TABLE                               *
025693*------------------------------------------------------------*
025694 01  ID-TABLE.
025695     05  ID-ENTRY OCCURS 100 TIMES.
025696         10  ID-MASK               PIC X(1) VALUE SPACE.
025697         10  ID-IO                 PIC X(1) VALUE SPACE.
025698         10  ID-LN                 PIC 9(02) VALUE ZERO.
025699         10  ID-DSN                PIC X(44) VALUE LOW-VALUES.
025700*------------------------------------------------------------*
025701*  EXCLUDE JOB NAME TABLE                                    *
025702*------------------------------------------------------------*
025703 01  EJ-TABLE.
025704     05  EJ-ENTRY OCCURS 100 TIMES.
025705         10  EJ-MASK               PIC X(1) VALUE SPACE.
025706         10  EJ-IO                 PIC X(1) VALUE SPACE.
025707         10  EJ-LN                 PIC 9(02) VALUE ZERO.
025708         10  EJ-JOB                PIC X(08) VALUE LOW-VALUES.
025709*------------------------------------------------------------*
025710*  INCLUDE JOB NAME TABLE                                    *
025711*------------------------------------------------------------*
025712 01  IJ-TABLE.
025713     05  IJ-ENTRY OCCURS 100 TIMES.
025714         10  IJ-MASK               PIC X(1) VALUE SPACE.
025715         10  IJ-IO                 PIC X(1) VALUE SPACE.
025716         10  IJ-LN                 PIC 9(02) VALUE ZERO.
025717         10  IJ-JOB                PIC X(08) VALUE LOW-VALUES.
025718*------------------------------------------------------------*
025719*  EXCLUDE USERID TABLE                                      *
025720*------------------------------------------------------------*
025721 01  EU-TABLE.
025722     05  EU-ENTRY OCCURS 100 TIMES.
025723         10  EU-MASK               PIC X(1) VALUE SPACE.
025724         10  EU-IO                 PIC X(1) VALUE SPACE.
025725         10  EU-LN                 PIC 9(02) VALUE ZERO.
025726         10  EU-USR                PIC X(08) VALUE LOW-VALUES.
025727*------------------------------------------------------------*
025728*  INCLUDE USERID TABLE                                      *
025729*------------------------------------------------------------*
025730 01  IU-TABLE.
025731     05  IU-ENTRY OCCURS 100 TIMES.
025732         10  IU-MASK               PIC X(1) VALUE SPACE.
025733         10  IU-IO                 PIC X(1) VALUE SPACE.
025734         10  IU-LN                 PIC 9(02) VALUE ZERO.
025735         10  IU-USR                PIC X(08) VALUE LOW-VALUES.
025736*------------------------------------------------------------*
025737*  EXCLUDE VOLUME NAME TABLE                                 *
025738*------------------------------------------------------------*
025739 01  EV-TABLE.
025740     05  EV-ENTRY OCCURS 100 TIMES.
025741         10  EV-MASK               PIC X(1) VALUE SPACE.
025742         10  EV-IO                 PIC X(1) VALUE SPACE.
025743         10  EV-LN                 PIC 9(02) VALUE ZERO.
025744         10  EV-VOL                PIC X(06) VALUE LOW-VALUES.
025745*------------------------------------------------------------*
025746*  INCLUDE VOLUME NAME TABLE                                 *
025747*------------------------------------------------------------*
025748 01  IV-TABLE.
025749     05  IV-ENTRY OCCURS 100 TIMES.
025750         10  IV-MASK               PIC X(1) VALUE SPACE.
025751         10  IV-IO                 PIC X(1) VALUE SPACE.
025752         10  IV-LN                 PIC 9(02) VALUE ZERO.
025753         10  IV-VOL                PIC X(06) VALUE LOW-VALUES.
029320*------------------------------------------------------------*
029400*  DSN OR JOB REDEFINES AREAS                                *
029500*------------------------------------------------------------*
029600 01  WORK-VALUE.
029700     05  WK-CHAR               OCCURS 44 TIMES
029800                               PIC X(1).
029900 01  CHK-FLD.
030000     05  CHK-CHAR              OCCURS 44 TIMES
030100                               PIC X(1).
030200 PROCEDURE DIVISION.
030300     OPEN INPUT  PARMIN.
030810     MOVE  0  TO NBR-RECORDS-READ.
030900     MOVE 'SMFLIST ' TO HEAD-PROGRAM.
031000     MOVE 'SYSTEM MEASUREMENT FACILITY -- SEQUENTIAL FILES'
031100         TO HEAD-SYS-DESC.
031200     MOVE 'AS GIVEN ON DATA FILE' TO HEAD-SEQ.
031300     CALL 'G207DCAN' USING G207-FUNC01, HEAD-DATE.
031310     MOVE 'Y' TO PROCESS-ALL.
031400 PROCESS-PARMS.
031500*************************************************************
031600*  READ A PARAMETER CARD (IF THERE ARE ANY).                *
031700*************************************************************
031800     READ PARMIN AT END
031900         GO TO CHK-PARM-COUNT.
032000     ADD 1 TO NBR-RECORDS-READ.
032010     MOVE 'N' TO PROCESS-ALL.
032100     IF PARM-TYPE = 'ED'
032200         GO TO LOAD-ED.
032300     IF PARM-TYPE = 'ID'
032400         GO TO LOAD-ID.
032500     IF PARM-TYPE = 'EJ'
032600         GO TO LOAD-EJ.
032700     IF PARM-TYPE = 'IJ'
032800         GO TO LOAD-IJ.
032900     IF PARM-TYPE = 'EU'
033000         GO TO LOAD-EU.
033100     IF PARM-TYPE = 'IU'
033200         GO TO LOAD-IU.
033300     IF PARM-TYPE = 'EV'
033400         GO TO LOAD-EV.
033500     IF PARM-TYPE = 'IV'
033600         GO TO LOAD-IV.
033700     GO TO CHECK-PARM-NV.
033800*-----------------------------------------------------------*
034700 LOAD-ED.
034800*************************************************************
034900*  LOAD A EXCLUDE DATASET RECORD                            *
035000*************************************************************
035100     ADD 1 TO ED-SUB.
035200     IF ED-SUB > 100
035300         DISPLAY 'MORE THAN 100 TYPE ED PARAMETER CARDS'
035400         DISPLAY 'PARAMETER CARD NOT PROCESSED         '
035500         DISPLAY PARM-RECORD
035600         MOVE 999 TO AB-CODE
035700         CALL 'ILBOABN0' USING AB-CODE.
035800     MOVE ZERO TO X.
035900     MOVE PARM-DSN TO ED-DSN (ED-SUB).
036000 FIND-ED-MASK-LOOP.
036100     ADD 1 TO X.
036200     IF X > 44
036300         GO TO END-ED-MASK-LOOP.
036400     IF PARM-CHAR (X) = SPACE
036500         GO TO END-ED-MASK-LOOP.
036600     IF PARM-CHAR (X) NOT = '*'
036700         GO TO FIND-ED-MASK-LOOP.
036800     MOVE 'Y' TO ED-MASK (ED-SUB).
037000     MOVE X TO ED-LN (ED-SUB).
037100     DISPLAY 'DSN MASK : ' ED-DSN (ED-SUB) ' TO BE BYPASSED'.
037200     GO TO PROCESS-PARMS.
037300 END-ED-MASK-LOOP.
037400     SUBTRACT 1 FROM X.
037500     MOVE X TO ED-LN (ED-SUB).
037600     DISPLAY 'DSN      : ' ED-DSN (ED-SUB) ' TO BE BYPASSED'.
037700     GO TO PROCESS-PARMS.
037800*-----------------------------------------------------------*
038700 LOAD-ID.
038800*************************************************************
038900*  LOAD A INCLUDE DATASET RECORD                            *
039000*************************************************************
039100     ADD 1 TO ID-SUB.
039200     IF ID-SUB > 100
039300         DISPLAY 'MORE THAN 100 TYPE ID PARAMETER CARDS'
039400         DISPLAY 'PARAMETER CARD NOT PROCESSED         '
039500         DISPLAY PARM-RECORD
039600         MOVE 999 TO AB-CODE
039700         CALL 'ILBOABN0' USING AB-CODE.
039800     MOVE ZERO TO X.
039900     MOVE PARM-DSN TO ID-DSN (ID-SUB).
040000 FIND-ID-MASK-LOOP.
040100     ADD 1 TO X.
040200     IF X > 44
040300         GO TO END-ID-MASK-LOOP.
040400     IF PARM-CHAR (X) = SPACE
040500         GO TO END-ID-MASK-LOOP.
040600     IF PARM-CHAR (X) NOT = '*'
040700         GO TO FIND-ID-MASK-LOOP.
040800     MOVE 'Y' TO ID-MASK (ID-SUB).
041000     MOVE X TO ID-LN (ID-SUB).
041100     DISPLAY 'DSN MASK : ' ID-DSN (ID-SUB) ' TO BE SELECTED'.
041200     GO TO PROCESS-PARMS.
041300 END-ID-MASK-LOOP.
041400     SUBTRACT 1 FROM X.
041500     MOVE X TO ID-LN (ID-SUB).
041600     DISPLAY 'DSN      : ' ID-DSN (ID-SUB) ' TO BE SELECTED'.
041700     GO TO PROCESS-PARMS.
041800*-----------------------------------------------------------*
042700 LOAD-EJ.
042800*************************************************************
042900*  LOAD A EXCLUDE JOB RECORD                                *
043000*************************************************************
043100     ADD 1 TO EJ-SUB.
043200     IF EJ-SUB > 100
043300         DISPLAY 'MORE THAN 100 TYPE EJ PARAMETER CARDS'
043400         DISPLAY 'PARAMETER CARD NOT PROCESSED         '
043500         DISPLAY PARM-RECORD
043600         MOVE 999 TO AB-CODE
043700         CALL 'ILBOABN0' USING AB-CODE.
043800     MOVE ZERO TO X.
043900     MOVE PARM-JOB TO EJ-JOB (EJ-SUB).
044000 FIND-EJ-MASK-LOOP.
044100     ADD 1 TO X.
044200     IF X > 8
044300         GO TO END-EJ-MASK-LOOP.
044400     IF PARM-CHAR (X) = SPACE
044500         GO TO END-EJ-MASK-LOOP.
044600     IF PARM-CHAR (X) NOT = '*'
044700         GO TO FIND-EJ-MASK-LOOP.
044800     MOVE 'Y' TO EJ-MASK (EJ-SUB).
044820     MOVE X TO EJ-LN (EJ-SUB).
044830     DISPLAY 'JOB MASK : ' EJ-JOB (EJ-SUB) ' TO BE SELECTED'.
044840     GO TO PROCESS-PARMS.
044850 END-EJ-MASK-LOOP.
044860     SUBTRACT 1 FROM X.
044870     MOVE X TO EJ-LN (EJ-SUB).
044880     DISPLAY 'JOB      : ' EJ-JOB (EJ-SUB) ' TO BE SELECTED'.
044890     GO TO PROCESS-PARMS.
044891*-----------------------------------------------------------*
044900 LOAD-IJ.
044901*************************************************************
044902*  LOAD A INCLUDE JOB RECORD                                *
044903*************************************************************
044904     ADD 1 TO IJ-SUB.
044905     IF IJ-SUB > 100
044906         DISPLAY 'MORE THAN 100 TYPE IJ PARAMETER CARDS'
044907         DISPLAY 'PARAMETER CARD NOT PROCESSED         '
044908         DISPLAY PARM-RECORD
044909         MOVE 999 TO AB-CODE
044910         CALL 'ILBOABN0' USING AB-CODE.
044911     MOVE ZERO TO X.
044912     MOVE PARM-JOB TO IJ-JOB (IJ-SUB).
044913 FIND-IJ-MASK-LOOP.
044914     ADD 1 TO X.
044915     IF X > 8
044916         GO TO END-IJ-MASK-LOOP.
044917     IF PARM-CHAR (X) = SPACE
044918         GO TO END-IJ-MASK-LOOP.
044919     IF PARM-CHAR (X) NOT = '*'
044920         GO TO FIND-IJ-MASK-LOOP.
044921     MOVE 'Y' TO IJ-MASK (IJ-SUB).
044923     MOVE X TO IJ-LN (IJ-SUB).
044924     DISPLAY 'JOB MASK : ' IJ-JOB (IJ-SUB) ' TO BE SELECTED'.
044925     GO TO PROCESS-PARMS.
044926 END-IJ-MASK-LOOP.
044927     SUBTRACT 1 FROM X.
044928     MOVE X TO IJ-LN (IJ-SUB).
044929     DISPLAY 'JOB      : ' IJ-JOB (IJ-SUB) ' TO BE SELECTED'.
044930     GO TO PROCESS-PARMS.
044931*-----------------------------------------------------------*
044940*************************************************************
044941*  LOAD A EXCLUDE USER ID RECORD                            *
044942*************************************************************
044943 LOAD-EU.
044944     ADD 1 TO EU-SUB.
044945     IF EU-SUB > 100
044946         DISPLAY 'MORE THAN 100 TYPE EU PARAMETER CARDS'
044947         DISPLAY 'PARAMETER CARD NOT PROCESSED         '
044948         DISPLAY PARM-RECORD
044949         MOVE 999 TO AB-CODE
044950         CALL 'ILBOABN0' USING AB-CODE.
044951     MOVE ZERO TO X.
044952     MOVE PARM-JOB TO EU-USR (EU-SUB).
044953 FIND-EU-MASK-LOOP.
044954     ADD 1 TO X.
044955     IF X > 8
044956         GO TO END-EU-MASK-LOOP.
044957     IF PARM-CHAR (X) NOT = '*'
044958         GO TO FIND-EU-MASK-LOOP.
044959     IF PARM-CHAR (X) = SPACE
044960         GO TO END-EU-MASK-LOOP.
044961     MOVE 'Y' TO EU-MASK (EU-SUB).
044963     MOVE X TO EU-LN (EU-SUB).
044964     DISPLAY 'USER MASK: ' EU-USR (EU-SUB) ' TO BE SELECTED'.
044965     GO TO PROCESS-PARMS.
044966 END-EU-MASK-LOOP.
044967     SUBTRACT 1 FROM X.
044968     MOVE X TO EU-LN (EU-SUB).
044969     DISPLAY 'USER ID  : ' EU-USR (EU-SUB) ' TO BE SELECTED'.
044970     GO TO PROCESS-PARMS.
044971*-----------------------------------------------------------*
044980 LOAD-IU.
044981*************************************************************
044982*  LOAD A INCLUDE USER ID RECORD                            *
044983*************************************************************
044984     ADD 1 TO IU-SUB.
044985     IF IU-SUB > 100
044986         DISPLAY 'MORE THAN 100 TYPE IU PARAMETER CARDS'
044987         DISPLAY 'PARAMETER CARD NOT PROCESSED         '
044988         DISPLAY PARM-RECORD
044989         MOVE 999 TO AB-CODE
044990         CALL 'ILBOABN0' USING AB-CODE.
044991     MOVE ZERO TO X.
044992     MOVE PARM-JOB TO IU-USR (IU-SUB).
044993 FIND-IU-MASK-LOOP.
044994     ADD 1 TO X.
044995     IF X > 8
044996         GO TO END-IU-MASK-LOOP.
044997     IF PARM-CHAR (X) =  SPACE
044998         GO TO END-IU-MASK-LOOP.
044999     IF PARM-CHAR (X) NOT = '*'
045000         GO TO FIND-IU-MASK-LOOP.
045001     MOVE 'Y' TO IU-MASK (IU-SUB).
045003     MOVE X TO IU-LN (IU-SUB).
045004     DISPLAY 'USER MASK: ' IU-USR (IU-SUB) ' TO BE SELECTED'.
045005     GO TO PROCESS-PARMS.
045006 END-IU-MASK-LOOP.
045007     SUBTRACT 1 FROM X.
045008     MOVE X TO IU-LN (IU-SUB).
045009     DISPLAY 'USER ID  : ' IU-USR (IU-SUB) ' TO BE SELECTED'.
045010     GO TO PROCESS-PARMS.
045011*-----------------------------------------------------------*
045020 LOAD-EV.
045021*************************************************************
045022*  LOAD A EXCLUDE VOLUME RECORD                             *
045023*************************************************************
045024     ADD 1 TO EV-SUB.
045025     IF EV-SUB > 100
045026         DISPLAY 'MORE THAN 100 TYPE EV PARAMETER CARDS'
045027         DISPLAY 'PARAMETER CARD NOT PROCESSED         '
045028         DISPLAY PARM-RECORD
045029         MOVE 999 TO AB-CODE
045030         CALL 'ILBOABN0' USING AB-CODE.
045031     MOVE ZERO TO X.
045032     MOVE PARM-DSN TO EV-VOL (EV-SUB).
045033 FIND-EV-MASK-LOOP.
045034     ADD 1 TO X.
045035     IF X > 06
045036         GO TO END-EV-MASK-LOOP.
045037     IF PARM-CHAR (X) = SPACE
045038         GO TO END-EV-MASK-LOOP.
045039     IF PARM-CHAR (X) NOT = '*'
045040         GO TO FIND-EV-MASK-LOOP.
045041     MOVE 'Y' TO EV-MASK (EV-SUB).
045043     MOVE X TO EV-LN (EV-SUB).
045044     DISPLAY 'VOL MASK : ' EV-VOL (EV-SUB) ' TO BE BYPASSED'.
045045     GO TO PROCESS-PARMS.
045046 END-EV-MASK-LOOP.
045047     SUBTRACT 1 FROM X.
045048     MOVE X TO EV-LN (EV-SUB).
045049     DISPLAY 'VOL      : ' EV-VOL (EV-SUB) ' TO BE BYPASSED'.
045050     GO TO PROCESS-PARMS.
045051*-----------------------------------------------------------*
045060 LOAD-IV.
045061*************************************************************
045062*  LOAD A INCLUDE VOLUME RECORD                             *
045063*************************************************************
045064     ADD 1 TO IV-SUB.
045065     IF IV-SUB > 100
045066         DISPLAY 'MORE THAN 100 TYPE IV PARAMETER CARDS'
045067         DISPLAY 'PARAMETER CARD NOT PROCESSED         '
045068         DISPLAY PARM-RECORD
045069         MOVE 999 TO AB-CODE
045070         CALL 'ILBOABN0' USING AB-CODE.
045071     MOVE ZERO TO X.
045072     MOVE PARM-DSN TO IV-VOL (IV-SUB).
045073 FIND-IV-MASK-LOOP.
045074     ADD 1 TO X.
045075     IF X > 06
045076         GO TO END-IV-MASK-LOOP.
045077     IF PARM-CHAR (X) = SPACE
045078         GO TO END-IV-MASK-LOOP.
045079     IF PARM-CHAR (X) NOT = '*'
045080         GO TO FIND-IV-MASK-LOOP.
045081     MOVE 'Y' TO IV-MASK (IV-SUB).
045083     MOVE X TO IV-LN (IV-SUB).
045084     DISPLAY 'VOL MASK : ' IV-VOL (IV-SUB) ' TO BE SELECTED'.
045085     GO TO PROCESS-PARMS.
045086 END-IV-MASK-LOOP.
045087     SUBTRACT 1 FROM X.
045088     MOVE X TO IV-LN (IV-SUB).
045089     DISPLAY 'VOL      : ' IV-VOL (IV-SUB) ' TO BE SELECTED'.
045090     GO TO PROCESS-PARMS.
045091 CHECK-PARM-NV.
045092     DISPLAY 'INVALID PARAMETER TYPE, MUST BE E/ID E/IJ E/IU E/IV'
045093     DISPLAY 'CARD PROCESSED: ' PARM-RECORD.
045094     MOVE 888 TO AB-CODE.
045095     CALL 'ILBOABN0' USING AB-CODE.
045096 CHK-PARM-COUNT.
045100     CLOSE PARMIN.
045101     IF RETURN-CODE > 0
045102       GOBACK.
045109 SORT-RECORDS.
045113     SORT SMF-REC-TYPE14 ASCENDING SMF14DTE, SMF14TME
045114         INPUT PROCEDURE IS SELECT-RECORDS
045115         OUTPUT PROCEDURE IS PRINT-REPORT.
045116 FINISH-JOB.
045117     DISPLAY 'NUMBER RECORDS READ = ' NBR-RECORDS-READ.
045118     DISPLAY 'NUMBER RECORDS USED = ' NBR-RECORDS-USED.
045119     MOVE ZERO TO RETURN-CODE.
045121     GOBACK.
045130 PRINT-HEADINGS-ROUTINE.
045200     ADD 1 TO PAGE-CNT
045300     MOVE PAGE-CNT TO HEAD-PAGE.
045400     MOVE HEAD-1 TO PRINT-LINE.
045500     WRITE PRINT-LINE AFTER ADVANCING TO-CHAN-1.
045600     MOVE HEAD-2 TO PRINT-LINE.
045700     WRITE PRINT-LINE AFTER ADVANCING 1 LINES.
045800     MOVE HEAD-3 TO PRINT-LINE.
045900     WRITE PRINT-LINE AFTER ADVANCING 1 LINES.
046000     MOVE HEAD-4 TO PRINT-LINE.
046100     WRITE PRINT-LINE AFTER ADVANCING 2 LINES.
046200     MOVE HEAD-5 TO PRINT-LINE.
046300     WRITE PRINT-LINE AFTER ADVANCING 1 LINES.
046400     MOVE SPACE TO PRINT-LINE.
046500     MOVE ZERO TO LINE-COUNT.
046600 PRINT-1.
046700     WRITE PRINT-LINE AFTER ADVANCING 1 LINES.
046800     ADD 1 TO LINE-COUNT.
046900     IF LINE-COUNT > 54
047000         PERFORM PRINT-HEADINGS-ROUTINE.
047100 PRINT-EXIT.
047200     EXIT.
047210*************************************************************
047220*  CHK FOR EXCLUDE OR INCLUDE                               *
047230*************************************************************
047240 CHECK-FIELD.
047250     MOVE 'N' TO CONT.
047260     IF CHK-MASK NOT = 'Y'
047270         AND CHK-FLD = WORK-VALUE
047280         MOVE 'Y' TO CONT
047290         GO TO CHK-FLD-EXIT.
047291     MOVE ZERO TO X.
047292 CHK-CHAR-LOOP.
047293     ADD 1 TO X.
047294     IF X > CHK-LN
047295         GO TO CHK-FLD-EXIT.
047296     IF X > 44
047297         GO TO CHK-FLD-EXIT.
047298     IF WK-CHAR (X) NOT = CHK-CHAR (X)
047299       AND CHK-CHAR(X) NOT = '*'
047300         GO TO CHK-FLD-EXIT.
047301     IF CHK-CHAR (X) = '*'
047302         MOVE 'Y' TO CONT
047303         GO TO CHK-FLD-EXIT.
047304     GO TO CHK-CHAR-LOOP.
047305 CHK-FLD-EXIT.
047306     EXIT.
047307*****************************************************************
047308*  THIS ROUTINE WILL FORMAT A BINARY BYTE (EIGHT BITS) INTO     *
047309*  EIGHT BYTES WITH THE BIT ON AND OFF REPRESENTATION IN EACH   *
047310*  BYTE.  THE BINARY BYTE IS PUT INTO FIELD WORK-CHAR AND THE   *
047311*  EIGHT BYTES ARE RETURNED IN FIELD EXPAND-BYTE.  THE FOUR     *
047312*  BYTE FIELDS WORK-BINARY ARE USED HEAVILY IN THIS ROUTINE     *
047313*****************************************************************
047314 EXPAND-BITS.
047315     MOVE LOW-VALUES TO BINARY-WORK.
047316     MOVE WORK-CHAR TO BINARY-ONE.
047317     COMPUTE WORK-BIT-EXPAND = WORK-BINARY.
047318     MOVE ALL ZERO TO EXPAND-BYTE.
047319     SUBTRACT 128 FROM WORK-BIT-EXPAND.
047320     IF WORK-BIT-EXPAND LESS THAN ZERO
047321         MOVE '0' TO BIT-ZERO
047322         ADD 128 TO WORK-BIT-EXPAND
047323       ELSE MOVE '1' TO BIT-ZERO.
047324     SUBTRACT 064 FROM WORK-BIT-EXPAND.
047325     IF WORK-BIT-EXPAND LESS THAN ZERO
047326         MOVE '0' TO BIT-ONE
047327         ADD 064 TO WORK-BIT-EXPAND
047328       ELSE MOVE '1' TO BIT-ONE.
047329     SUBTRACT 032 FROM WORK-BIT-EXPAND.
047330     IF WORK-BIT-EXPAND LESS THAN ZERO
047331         MOVE '0' TO BIT-TWO
047332         ADD 032 TO WORK-BIT-EXPAND
047333       ELSE MOVE '1' TO BIT-TWO.
047334     SUBTRACT 016 FROM WORK-BIT-EXPAND.
047335     IF WORK-BIT-EXPAND LESS THAN ZERO
047336         MOVE '0' TO BIT-THREE
047337         ADD 016 TO WORK-BIT-EXPAND
047338       ELSE MOVE '1' TO BIT-THREE.
047339     SUBTRACT 008 FROM WORK-BIT-EXPAND.
047340     IF WORK-BIT-EXPAND LESS THAN ZERO
047341         MOVE '0' TO BIT-FOUR
047342         ADD 008 TO WORK-BIT-EXPAND
047343       ELSE MOVE '1' TO BIT-FOUR.
047344     SUBTRACT 004 FROM WORK-BIT-EXPAND.
047345     IF WORK-BIT-EXPAND LESS THAN ZERO
047346         MOVE '0' TO BIT-FIVE
047347         ADD 004 TO WORK-BIT-EXPAND
047348       ELSE MOVE '1' TO BIT-FIVE.
047349     SUBTRACT 002 FROM WORK-BIT-EXPAND.
047350     IF WORK-BIT-EXPAND LESS THAN ZERO
047351         MOVE '0' TO BIT-SIX
047352         ADD 002 TO WORK-BIT-EXPAND
047353       ELSE MOVE '1' TO BIT-SIX.
047354     SUBTRACT 001 FROM WORK-BIT-EXPAND.
047355     IF WORK-BIT-EXPAND LESS THAN ZERO
047356         MOVE '0' TO BIT-SEVEN
047357         ADD 001 TO WORK-BIT-EXPAND
047358       ELSE MOVE '1' TO BIT-SEVEN.
047360*************************************************************
047400 SELECT-RECORDS SECTION.
047401     OPEN INPUT  SMFIN.
047410 READ-SMF.
047500*************************************************************
047600*  READ AN SMF RECORD                                       *
047700*************************************************************
047800     READ SMFIN AT END
047900         GO TO END-SELECT-RECORDS.
047910     MOVE DUMMY-RECORD TO SMFTYPE14.
048000     ADD 1 TO NBR-RECORDS-READ.
048100*************************************************************
048200*  IS IT A TYPE 14  X'0E' OR 15 X'0F'                       *
048300*************************************************************
048400     MOVE SMF14RTY TO WORK-CHAR.
048500     MOVE LOW-VALUES TO BINARY-WORK.
048600     MOVE WORK-CHAR TO BINARY-ONE.
048700     COMPUTE WORK-BIT-EXPAND = WORK-BINARY.
048800     IF WORK-BIT-EXPAND NOT = 14
048900         AND WORK-BIT-EXPAND NOT = 15
049000         GO TO READ-SMF.
049100     IF WORK-BIT-EXPAND = 14
049200         MOVE 'I' TO IO.
049300     IF WORK-BIT-EXPAND = 15
049400         MOVE 'O' TO IO.
049500*************************************************************
049600*  SELECT ONLY PERMANENT DATASETS                           *
049700*************************************************************
049800     MOVE SMF14RIN TO WORK-ARRAY-ONE.
049900     MOVE CHAR-ONE (1) TO WORK-CHAR.
050000     PERFORM EXPAND-BITS.
050100     MOVE BIT-TWO TO DASD-FLAG.
050200     MOVE BIT-SEVEN TO VIO-FLAG.
050300*************************************************************
050400*  IF INVALID DSN, IGNORE                                   *
050500*************************************************************
050600*    IF DSN1 NOT ALPHABETIC
050700*        GO TO READ-SMF.
050800*************************************************************
050900*  SEE IF IT'S A DATASET WE DON'T CARE ABOUT.               *
051000*************************************************************
051100*    IF PART1 = 'FORMAT' OR 'VSAMDS'
051200*        GO TO READ-SMF.
051300*************************************************************
051400*  CHECK PROCESS-ALL                                        *
051500*************************************************************
051600     IF PROCESS-ALL = 'Y'
051700       GO TO FORMAT-DATA.
051800     MOVE ZERO TO ED-SUB2.
051900     MOVE ZERO TO ID-SUB2.
052000     MOVE ZERO TO EJ-SUB2.
052100     MOVE ZERO TO IJ-SUB2.
052110     MOVE ZERO TO EU-SUB2.
052120     MOVE ZERO TO IU-SUB2.
052130     MOVE ZERO TO EV-SUB2.
052140     MOVE ZERO TO IV-SUB2.
052200*************************************************************
052300*  CHECK TO SEE IF DATA SET IS TO BE EXCLUDED               *
052400*************************************************************
052500 CHK-ED.
052600     ADD 1 TO ED-SUB2.
052700     IF ED-SUB2 > ED-SUB
052800         GO TO CHK-ID.
052900     MOVE 'N' TO CONT.
053000     MOVE SPACES TO WORK-VALUE.
053100     MOVE SMFDSN TO WORK-VALUE.
053200     MOVE ED-DSN (ED-SUB2) TO CHK-FLD.
053300     MOVE ED-LN (ED-SUB2) TO CHK-LN.
053400     MOVE ED-MASK (ED-SUB2) TO CHK-MASK.
053500     PERFORM CHECK-FIELD THRU CHK-FLD-EXIT.
053600     IF CONT = 'Y'
053700         GO TO READ-SMF.
053800     GO TO CHK-ED.
053900*************************************************************
054000*  CHECK TO SEE IF DATA SET IS TO BE INCLUDED               *
054100*************************************************************
054200 CHK-ID.
054300     ADD 1 TO ID-SUB2.
054400     IF ID-SUB2 > ID-SUB
054500       GO TO CHK-EJ.
054600     MOVE 'N' TO CONT.
054700     MOVE SPACES TO WORK-VALUE.
054800     MOVE SMFDSN TO WORK-VALUE.
054900     MOVE ID-DSN (ID-SUB2) TO CHK-FLD.
055000     MOVE ID-LN (ID-SUB2) TO CHK-LN.
055100     MOVE ID-MASK (ID-SUB2) TO CHK-MASK.
055200     PERFORM CHECK-FIELD THRU CHK-FLD-EXIT.
055300     IF CONT = 'Y'
055400       GO TO FORMAT-DATA.
055500     GO TO CHK-ID.
055600*************************************************************
055700*  CHECK TO SEE JOB IS TO BE EXCLUDED                       *
055800*************************************************************
055900 CHK-EJ.
056000     ADD 1 TO EJ-SUB2.
056100     IF EJ-SUB2 > EJ-SUB
056200       GO TO CHK-IJ.
056300     MOVE 'N' TO CONT.
056400     MOVE SPACES TO WORK-VALUE.
056500     MOVE SMF14JBN TO WORK-VALUE.
056600     MOVE EJ-JOB (EJ-SUB2) TO CHK-FLD.
056700     MOVE EJ-LN (EJ-SUB2) TO CHK-LN.
056800     MOVE EJ-MASK (EJ-SUB2) TO CHK-MASK.
056900     PERFORM CHECK-FIELD THRU CHK-FLD-EXIT.
057000     IF CONT = 'Y'
057100       GO TO READ-SMF.
057200     GO TO CHK-EJ.
057300*************************************************************
057400*  CHECK TO SEE JOB IS TO BE INCLUDED                       *
057500*************************************************************
057600 CHK-IJ.
057700     ADD 1 TO IJ-SUB2.
057800     IF IJ-SUB2 > IJ-SUB
057900       GO TO CHK-EU.
058000     MOVE 'N' TO CONT.
058100     MOVE SPACES TO WORK-VALUE.
058200     MOVE SMF14JBN TO WORK-VALUE.
058300     MOVE IJ-JOB (IJ-SUB2) TO CHK-FLD.
058400     MOVE IJ-LN (IJ-SUB2) TO CHK-LN.
058500     MOVE IJ-MASK (IJ-SUB2) TO CHK-MASK.
058600     PERFORM CHECK-FIELD THRU CHK-FLD-EXIT.
058700     IF CONT = 'Y'
058800       GO TO FORMAT-DATA.
058900     GO TO CHK-IJ.
058910*************************************************************
058920*  CHECK TO SEE USERID IS TO BE INCLUDED                    *
058930*************************************************************
058940 CHK-EU.
058950     ADD 1 TO EU-SUB2.
058960     IF EU-SUB2 > EU-SUB
058970       GO TO CHK-IU.
058980     MOVE 'N' TO CONT.
058990     MOVE SPACES TO WORK-VALUE.
058991     MOVE SMF14UID TO WORK-VALUE.
058992     MOVE EU-USR (EU-SUB2) TO CHK-FLD.
058993     MOVE EU-LN (EU-SUB2) TO CHK-LN.
058994     MOVE EU-MASK (EU-SUB2) TO CHK-MASK.
058995     PERFORM CHECK-FIELD THRU CHK-FLD-EXIT.
058996     IF CONT = 'Y'
058997       GO TO READ-SMF.
058998     GO TO CHK-EU.
058999*************************************************************
059000*  CHECK TO SEE USER ID IS TO BE INCLUDED                   *
059001*************************************************************
059002 CHK-IU.
059003     ADD 1 TO IU-SUB2.
059004     IF IU-SUB2 > IU-SUB
059005       GO TO CHK-EV.
059006     MOVE 'N' TO CONT.
059007     MOVE SPACES TO WORK-VALUE.
059008     MOVE SMF14UID TO WORK-VALUE.
059009     MOVE IU-USR (IU-SUB2) TO CHK-FLD.
059010     MOVE IU-LN (IU-SUB2) TO CHK-LN.
059011     MOVE IU-MASK (IU-SUB2) TO CHK-MASK.
059012     PERFORM CHECK-FIELD THRU CHK-FLD-EXIT.
059013     IF CONT = 'Y'
059014       GO TO FORMAT-DATA.
059015     GO TO CHK-IU.
059016*************************************************************
059017*  CHECK TO SEE IF VOLUME IS TO BE EXCLUDED                 *
059018*************************************************************
059019 CHK-EV.
059020     ADD 1 TO EV-SUB2.
059021     IF EV-SUB2 > EV-SUB
059022         GO TO CHK-IV.
059023     MOVE 'N' TO CONT.
059024     MOVE SPACES TO WORK-VALUE.
059025     MOVE SMFSRTEV TO WORK-VALUE.
059026     MOVE EV-VOL (EV-SUB2) TO CHK-FLD.
059027     MOVE EV-LN (EV-SUB2) TO CHK-LN.
059028     MOVE EV-MASK (EV-SUB2) TO CHK-MASK.
059029     PERFORM CHECK-FIELD THRU CHK-FLD-EXIT.
059030     IF CONT = 'Y'
059031         GO TO READ-SMF.
059032     GO TO CHK-EV.
059033*************************************************************
059034*  CHECK TO SEE IF VOLUME IS TO BE INCLUDED                 *
059035*************************************************************
059036 CHK-IV.
059037     ADD 1 TO IV-SUB2.
059038     IF IV-SUB2 > IV-SUB
059039       GO TO READ-SMF.
059040     MOVE 'N' TO CONT.
059041     MOVE SPACES TO WORK-VALUE.
059042     MOVE SMFSRTEV TO WORK-VALUE.
059043     MOVE IV-VOL (IV-SUB2) TO CHK-FLD.
059044     MOVE IV-LN (IV-SUB2) TO CHK-LN.
059045     MOVE IV-MASK (IV-SUB2) TO CHK-MASK.
059046     PERFORM CHECK-FIELD THRU CHK-FLD-EXIT.
059047     IF CONT = 'Y'
059048       GO TO FORMAT-DATA.
059049     GO TO CHK-IV.
059050 FORMAT-DATA.
059060     RELEASE SMFTYPE14.
059070     GO TO READ-SMF.
059080 END-SELECT-RECORDS.
059081     CLOSE SMFIN.
059082 END-INPUT.
059090     EXIT.
059091 PRINT-REPORT SECTION.
059092     OPEN OUTPUT DATA-OUT, REPORT-OUT.
059093     PERFORM PRINT-HEADINGS-ROUTINE THROUGH PRINT-EXIT.
059094 WRITE-LINE.
059095     RETURN SMF-REC-TYPE14 AT END
059096         GO TO FINISH-REPORT.
059100*************************************************************
059200*  GET DATA FROM RECORD                                     *
059300*************************************************************
059400     MOVE SPACES TO DETAIL-LINE.
059500     MOVE SMFDSN TO D-DATASET-NAME.
059600     MOVE SMFSRTEV TO D-VOL-SER.
059700     MOVE SMF14JBN TO D-JOB-NAME.
059710     MOVE SMF14UID TO D-USER-ID.
059800     DIVIDE SMF14TME BY 100 GIVING X.
059900     DIVIDE X BY 3600 GIVING D-HH
060000         REMAINDER Y.
060100     DIVIDE Y BY 60 GIVING D-MM
060200         REMAINDER D-SS.
060300     MOVE D-TIME TO D-END-TIME.
060400     MOVE SMF14DTE TO WORK-JUL.
060500     MOVE WORK-JUL TO WORK-JUL-1.
060600     CALL 'G207DCAN' USING G207-FUNC10, WORK-JUL D-END-DATE.
060700     MOVE SMFTIOE5 TO D-FILE-NAME.
060800     MOVE LOW-VALUES TO WORK-ARRAY-ONE.
060900     MOVE SMFUCBDV TO WORK-ARRAY-ONE.
061000     MOVE CHAR-ONE (1) TO WORK-CHAR.
061100     PERFORM EXPAND-BITS.
061200     MOVE ZERO TO X.
061300     IF BIT-ZERO = '1'
061400         ADD 8 TO X.
061500     IF BIT-ONE = '1'
061600         ADD 4 TO X.
061700     IF BIT-TWO = '1'
061800         ADD 2 TO X.
061900     IF BIT-THREE = '1'
062000         ADD 1 TO X.
062100     IF X < 10
062200         MOVE X TO W
062300         MOVE W TO D-ADDR-1.
062400     IF X = 10
062500         MOVE 'A' TO D-ADDR-1.
062600     IF X = 11
062700         MOVE 'B' TO D-ADDR-1.
062800     IF X = 12
062900         MOVE 'C' TO D-ADDR-1.
063000     IF X = 13
063100         MOVE 'D' TO D-ADDR-1.
063200     IF X = 14
063300         MOVE 'E' TO D-ADDR-1.
063400     IF X = 15
063500         MOVE 'F' TO D-ADDR-1.
063600     MOVE ZERO TO X.
063700     IF BIT-FOUR = '1'
063800         ADD 8 TO X.
063900     IF BIT-FIVE = '1'
064000         ADD 4 TO X.
064100     IF BIT-SIX = '1'
064200         ADD 2 TO X.
064300     IF BIT-SEVEN = '1'
064400         ADD 1 TO X.
064500     IF X < 10
064600         MOVE X TO W
064700         MOVE W TO D-ADDR-2.
064800     IF X = 10
064900         MOVE 'A' TO D-ADDR-2.
065000     IF X = 11
065100         MOVE 'B' TO D-ADDR-2.
065200     IF X = 12
065300         MOVE 'C' TO D-ADDR-2.
065400     IF X = 13
065500         MOVE 'D' TO D-ADDR-2.
065600     IF X = 14
065700         MOVE 'E' TO D-ADDR-2.
065800     IF X = 15
065900         MOVE 'F' TO D-ADDR-2.
066000     MOVE CHAR-ONE (2) TO WORK-CHAR.
066100     PERFORM EXPAND-BITS.
066200     MOVE ZERO TO X.
066300     IF BIT-ZERO = '1'
066400         ADD 8 TO X.
066500     IF BIT-ONE = '1'
066600         ADD 4 TO X.
066700     IF BIT-TWO = '1'
066800         ADD 2 TO X.
066900     IF BIT-THREE = '1'
067000         ADD 1 TO X.
067100     IF X < 10
067200         MOVE X TO W
067300         MOVE W TO D-ADDR-3.
067400     IF X = 10
067500         MOVE 'A' TO D-ADDR-3.
067600     IF X = 11
067700         MOVE 'B' TO D-ADDR-3.
067800     IF X = 12
067900         MOVE 'C' TO D-ADDR-3.
068000     IF X = 13
068100         MOVE 'D' TO D-ADDR-3.
068200     IF X = 14
068300         MOVE 'E' TO D-ADDR-3.
068400     IF X = 15
068500         MOVE 'F' TO D-ADDR-3.
068600     MOVE ZERO TO X.
068700     IF BIT-FOUR = '1'
068800         ADD 8 TO X.
068900     IF BIT-FIVE = '1'
069000         ADD 4 TO X.
069100     IF BIT-SIX = '1'
069200         ADD 2 TO X.
069300     IF BIT-SEVEN = '1'
069400         ADD 1 TO X.
069500     IF X < 10
069600         MOVE X TO W
069700         MOVE W TO D-ADDR-4.
069800     IF X = 10
069900         MOVE 'A' TO D-ADDR-4.
070000     IF X = 11
070100         MOVE 'B' TO D-ADDR-4.
070200     IF X = 12
070300         MOVE 'C' TO D-ADDR-4.
070400     IF X = 13
070500         MOVE 'D' TO D-ADDR-4.
070600     IF X = 14
070700         MOVE 'E' TO D-ADDR-4.
070800     IF X = 15
070900         MOVE 'F' TO D-ADDR-4.
071000     MOVE SMFEXCP TO R-BLK-CNT.
071100     IF DASD-FLAG NOT = '1'
071200         AND VIO-FLAG NOT = '1'
071300         MOVE SMFDCBBL TO R-BLK-CNT.
071400*    IF R-BLK-CNT = ZERO
071500*        GO TO READ-SMF.
071600     MOVE R-BLK-CNT TO D-BLK-CNT.
071700     MOVE D-JOB-NAME TO R-JOB-NAME.
071800     MOVE D-DATASET-NAME TO R-DATASET-NAME.
071900     MOVE D-END-DATE TO R-END-DATE.
072000     MOVE D-END-TIME TO R-END-TIME.
072100     MOVE D-UNIT-ADDR TO R-UNIT-ADDR.
072200     MOVE D-VOL-SER TO R-VOL-SER.
072300     MOVE D-FILE-NAME TO R-FILE-NAME.
072400     WRITE RECORD-OUT.
072500 PRINT-DATA.
072600     MOVE DETAIL-LINE TO P-LINE.
072700     PERFORM PRINT-1 THROUGH PRINT-EXIT.
072800     ADD 1 TO NBR-RECORDS-USED.
072900     GO TO WRITE-LINE.
080500 FINISH-REPORT.
080600     CLOSE DATA-OUT REPORT-OUT.
080700 END-REPORT.
081000     EXIT.
