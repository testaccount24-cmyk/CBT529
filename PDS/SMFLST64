000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. SMFLIST.
000300 AUTHOR. MATTHEW V. STITT.
000400 DATE-WRITTEN. 06/15/91.
000500 DATE-COMPILED.
000600 REMARKS. PRINT LISTING OF VSAM DATASET ACCESSES SMF RECORDS.
000700 ENVIRONMENT DIVISION.
000800 CONFIGURATION SECTION.
000900 SOURCE-COMPUTER. IBM-3081.
001000 OBJECT-COMPUTER. IBM-3081.
001100 SPECIAL-NAMES.
001200     C01 IS TO-CHAN-1.
001300 INPUT-OUTPUT SECTION.
001400 FILE-CONTROL.
001500     SELECT SMF-DATA ASSIGN TO UT-S-SYS004.
001600     SELECT REPORT-OUT ASSIGN TO UT-S-SYS019.
001700     SELECT DATA-OUT ASSIGN TO UT-S-SYS020.
001800     SELECT SMF-REC-TYPE64 ASSIGN TO UT-S-SORTWK01.
001810     SELECT PARMIN   ASSIGN TO UT-S-SYS017.
001850 DATA DIVISION.
001860 FILE SECTION.
001870 FD  PARMIN
001871     RECORDING MODE IS F
001880     LABEL RECORDS ARE STANDARD
001890     BLOCK CONTAINS 0 RECORDS.
001891 01  PARM-RECORD.
001892     03  PARM-TYPE           PIC X(02).
001893     03  PARM-IO-TYPE        PIC X(01).
001894     03  PARM-DSN            PIC X(44).
001895     03  PARM-JOBX       REDEFINES PARM-DSN.
001896         05  PARM-JOB        PIC X(08).
001897         05  FILLER          PIC X(36).
001898     03  PARM-ARRAY      REDEFINES PARM-DSN.
001899         05  PARM-CHAR   OCCURS 44 TIMES
001900                             PIC X.
001901     03  FILLER              PIC X(33).
002100 FD  SMF-DATA
002200     RECORDING MODE IS S
002300     BLOCK CONTAINS 32760 CHARACTERS
002400     RECORD CONTAINS 0 CHARACTERS
002500     LABEL RECORDS ARE STANDARD.
002600 01  SMF-FILE                    PIC X(32760).
002700 FD  DATA-OUT
002800     RECORDING MODE IS F
002810     BLOCK CONTAINS 0 RECORDS
002900     LABEL RECORDS ARE STANDARD.
003000 01  RECORD-OUT.
003100     02  R-JOB-NAME              PIC X(8).
003200     02  R-FILE-NAME             PIC X(8).
003300     02  R-DATASET-NAME          PIC X(44).
003400     02  R-UNIT-ADDR.
003500         03  R-ADDR-1            PIC X.
003600         03  R-ADDR-2            PIC X.
003700         03  R-ADDR-3            PIC X.
003800         03  R-ADDR-4            PIC X.
003900     02  R-VOL-SER               PIC X(6).
004000     02  R-END-DATE              PIC X(8).
004100     02  R-END-TIME              PIC X(8).
004200     02  R-BLK-CNT               PIC 9(15).
004300 FD  REPORT-OUT
004301     RECORDING MODE IS F
004310     LABEL RECORDS ARE OMITTED.
004320 01  PRINT-LINE.
004400     02  P-LINE                  PIC X(133).
004500 SD  SMF-REC-TYPE64.
004600 01  SORT-RECORD.
004700     02  SMF-TIME                PIC S9(8)  COMP.
004800     02  SMF-DATE                PIC 9(7)   COMP-3.
004900     02  SMF-SID                 PIC X(4).
005000     02  SMF-TYPE64.
005100         03  SMF64JBN            PIC X(8).
005200         03  SMF64RST            PIC S9(8)  COMP.
005300         03  SMF64RSD            PIC 9(7)   COMP-3.
005400         03  SMF64UIF            PIC X(8).
005500         03  SMF64RIN            PIC X.
005600         03  SMF64DTY            PIC X.
005700         03  SMF64CNM            PIC X(44).
005800         03  SMF64DNM            PIC X(44).
005900         03  SMF64NTR            PIC S9(4)  COMP.
006000         03  SMF64CHR            PIC S9(8)  COMP.
006100         03  SMF64ESL            PIC S9(4)  COMP.
006200         03  SMF64-STATS.
006300             04  SMF64SLN        PIC S9(8)  COMP.
006400             04  SMF64NIL        PIC S9(8)  COMP.
006500             04  SMF64NEX        PIC S9(8)  COMP.
006600             04  SMF64NLR        PIC S9(8)  COMP.
006700             04  SMF64NDE        PIC S9(8)  COMP.
006800             04  SMF64NIN        PIC S9(8)  COMP.
006900             04  SMF64NUP        PIC S9(8)  COMP.
007000             04  SMF64NRE        PIC S9(8)  COMP.
007100             04  SMF64NFS        PIC S9(8)  COMP.
007200             04  SMF64NCS        PIC S9(8)  COMP.
007300             04  SMF64NAS        PIC S9(8)  COMP.
007400             04  SMF64NEP        PIC S9(8)  COMP.
007500             04  SMF64DIL        PIC S9(8)  COMP.
007600             04  SMF64DEX        PIC S9(8)  COMP.
007700             04  SMF64DLR        PIC S9(8)  COMP.
007800             04  SMF64DDE        PIC S9(8)  COMP.
007900             04  SMF64DIN        PIC S9(8)  COMP.
008000             04  SMF64DUP        PIC S9(8)  COMP.
008100             04  SMF64DRE        PIC S9(8)  COMP.
008200             04  SMF64DFS        PIC S9(8)  COMP.
008300             04  SMF64DCS        PIC S9(8)  COMP.
008400             04  SMF64DAS        PIC S9(8)  COMP.
008500             04  SMF64DEP        PIC S9(8)  COMP.
008600             04  SMF64DBS        PIC S9(8)  COMP.
008700             04  SMF64DCI        PIC S9(8)  COMP.
008800             04  SMF64DLS        PIC S9(8)  COMP.
008900             04  SMF64DKL        PIC S9(4)  COMP.
009000             04  SMF64DDN        PIC X(8).
009100             04  SMF64STR        PIC X.
009101             04  SMF64BNO        PIC X.
009102             04  SMF64BSP        PIC S9(8)  COMP.
009103             04  SMF64BFD        PIC S9(4)  COMP.
009110             04  SMF64BFI        PIC S9(4)  COMP.
009200             04  SMF64CLN        PIC X(44).
009300             04  SMF64PLH        PIC S9(4)  COMP.
009310             04  SMF64MC1        PIC X.
009320             04  SMF64MC2        PIC X.
009330             04  SMF64MC3        PIC X.
009331             04  SMF64MC4        PIC X.
009332             04  SMF64SMB        PIC X.
009333             04  SMF64RSC        PIC X.
009350             04  SMF64SIO        PIC S9(8)  COMP.
009360             04  SMF64HIT        PIC S9(8)  COMP.
009370             04  SMF64WTS        PIC S9(8)  COMP.
009380             04  SMF64MIS        PIC S9(8)  COMP.
009390             04  SMF64IOS        PIC S9(8)  COMP.
009391             04  SMF64SDS        PIC S9(16) COMP.
009392             04  SMF64CDS        PIC S9(16) COMP.
009393             04  SMF64CSS        PIC S9(16) COMP.
009394             04  SMF64CCS        PIC S9(16) COMP.
009395             04  SMF64DTK        PIC X(36).
009396             04  SMF64TRK        PIC S9(8)  COMP.
009397             04  SMF64BMH        PIC S9(8)  COMP.
009398             04  SMF64CFH        PIC S9(8)  COMP.
009399             04  SMF64RIO        PIC S9(8)  COMP.
009400             04  SMF64TIM        PIC S9(8)  COMP.
009401             04  SMF64DT         PIC S9(8)  COMP.
009402             04  SMF64FG1        PIC S9(2)  COMP.
009410         03  SMF64-EXTENT.
009500             04  SMF64FCC        PIC S9(8)  COMP.
009600             04  SMF64TCC        PIC S9(8)  COMP.
009700             04  SMF64VSN        PIC X(6).
009800             04  SMF64CUU        PIC X(2).
009900             04  SMF64IND        PIC XX.
010000             04  SMF64UTY        PIC S9(8)  COMP.
010100             04  SMF64RV1        PIC X(4).
010200 WORKING-STORAGE SECTION.
010300 77  WS-START                    PIC X(36)  VALUE
010400     'SMFLIST  WORKING STORAGE STARTS HERE'.
010410 77  ED-SUB                      PIC 9(03) COMP VALUE ZERO.
010420 77  ID-SUB                      PIC 9(03) COMP VALUE ZERO.
010430 77  EJ-SUB                      PIC 9(03) COMP VALUE ZERO.
010440 77  IJ-SUB                      PIC 9(03) COMP VALUE ZERO.
010450 77  EU-SUB                      PIC 9(03) COMP VALUE ZERO.
010460 77  IU-SUB                      PIC 9(03) COMP VALUE ZERO.
010461 77  EV-SUB                      PIC 9(03) COMP VALUE ZERO.
010462 77  IV-SUB                      PIC 9(03) COMP VALUE ZERO.
010470 77  ED-SUB2                     PIC 9(03) COMP VALUE ZERO.
010480 77  ID-SUB2                     PIC 9(03) COMP VALUE ZERO.
010490 77  EJ-SUB2                     PIC 9(03) COMP VALUE ZERO.
010491 77  IJ-SUB2                     PIC 9(03) COMP VALUE ZERO.
010492 77  EU-SUB2                     PIC 9(03) COMP VALUE ZERO.
010493 77  IU-SUB2                     PIC 9(03) COMP VALUE ZERO.
010496 77  EV-SUB2                     PIC 9(03) COMP VALUE ZERO.
010497 77  IV-SUB2                     PIC 9(03) COMP VALUE ZERO.
010500 77  PROCESS-ALL                 PIC X(01) VALUE SPACE.
010509 77  CONT                        PIC X(01) VALUE SPACE.
010510 77  IO                          PIC X(01) VALUE SPACE.
010511 77  DASD-FLAG                   PIC X(1).
010512 77  VIO-FLAG                    PIC X(1).
010513 77  CHK-MASK                    PIC X(1).
010514 77  CHK-LN                      PIC 9(02).
010520 77  WORK-CHAR                   PIC X.
010600 77  AB-CODE                     PIC S9(3)  COMP    VALUE ZERO.
010700 77  WORK-BIT-EXPAND             PIC S9(5)  COMP-3  VALUE ZERO.
010800 77  WORK-JUL                    PIC 9(5).
010900 77  WORK-JUL-1                  PIC 9(5).
011000 77  WORK-JUL-2                  PIC 9(5).
011100 77  JUL-DIFF                    PIC S9(5)  COMP-3.
011200 77  G207-FUNC14                 PIC XX     VALUE '14'.
011210 77  G207-FUNC34                 PIC XX     VALUE '34'.
011610 77  PAGE-CNT                    PIC 9(7)   COMP-3  VALUE ZERO.
011620 77  LINE-COUNT                  PIC 9(7)   COMP-3  VALUE ZERO.
011700 77  FIRST-REPORT-SW             PIC 9              VALUE ZERO.
011710 77  NBR-RECORDS-READ            PIC 9(18) VALUE ZERO.
011720 77  NBR-RECORDS-USED            PIC 9(18) VALUE ZERO.
011800 77  SMF64-NU-EXT                PIC S9(8)  COMP.
011900 77  SMF64BLK                    PIC S9(8)  COMP.
012000 77  W                           PIC 9.
012100 77  X                           PIC S9(8)  COMP.
012200 77  Y                           PIC S9(8)  COMP.
012300 77  Z                           PIC S9(8)  COMP.
012400 01  SMF-RECORD.
012500*    02  REC-LEN                 PIC XX.
012600*    02  SEG-DESC                PIC XX.
012700     02  SYS-IND                 PIC X.
012800     02  REC-TYPE                PIC X.
012900     02  REC-TIME                PIC S9(8)  COMP.
013000     02  REC-DATE                PIC 9(7)   COMP-3.
013100     02  SYS-IDENT               PIC X(4).
013200     02  SMF-DATA-REC            PIC X(32760).
013300 01  D-TIME.
013400     02  D-HH                    PIC 99.
013500     02  FILLER                  PIC X      VALUE '.'.
013600     02  D-MM                    PIC 99.
013700     02  FILLER                  PIC X      VALUE '.'.
013800     02  D-SS                    PIC 99.
013900 01  TYPE64-DATA.
014000     02  TYPE64-BASIC            PIC X(122).
014100     02  TYPE64-STATS            PIC X(292).
014200     02  TYPE64-EXTENT           PIC X(26).
017700 01  HEAD-1.
017800     02  FILLER                  PIC X     VALUE SPACE.
017900     02  HEAD-PROGRAM            PIC X(8)  VALUE SPACE.
018000     02  FILLER                  PIC X(47) VALUE SPACE.
018100     02  FILLER                  PIC X(22) VALUE
018200         'ABC DISTRIBUTING, LLC'.
018300     02  FILLER                  PIC X(45) VALUE SPACE.
018400     02  FILLER                  PIC X(5)  VALUE 'PAGE '.
018500     02  HEAD-PAGE               PIC ZZZZ9 VALUE ZERO.
018600 01  HEAD-2.
018700     02  FILLER                  PIC X     VALUE SPACE.
018800     02  FILLER                  PIC X(5)  VALUE 'DATE '.
018900     02  HEAD-DATE               PIC X(10) VALUE SPACE.
019000     02  FILLER                  PIC X(11) VALUE SPACE.
019100     02  HEAD-SYS-DESC           PIC X(80) VALUE SPACE.
019200 01  HEAD-3.
019300     02  FILLER                  PIC X     VALUE SPACE.
019400     02  FILLER                  PIC X(51) VALUE SPACE.
019500     02  HEAD-SEQ                PIC X(30) VALUE SPACE.
019600 01  HEAD-4.
019700     02  FILLER                  PIC X(2)  VALUE SPACE.
019800     02  FILLER                  PIC X(3)  VALUE 'JOB'.
019900     02  FILLER                  PIC X(7)  VALUE SPACE.
019910     02  FILLER                  PIC X(4)  VALUE 'USER'.
019920     02  FILLER                  PIC X(6)  VALUE SPACE.
020000     02  FILLER                  PIC X(4)  VALUE 'FILE'.
020100     02  FILLER                  PIC X(6)  VALUE SPACE.
020200     02  FILLER                  PIC X(7)  VALUE 'DATASET'.
020300     02  FILLER                  PIC X(39) VALUE SPACE.
020400     02  FILLER                  PIC X(4)  VALUE 'UNIT'.
020500     02  FILLER                  PIC X(2)  VALUE SPACE.
020600     02  FILLER                  PIC X(6)  VALUE 'VOLUME'.
020700     02  FILLER                  PIC X(3)  VALUE SPACE.
020800     02  FILLER                  PIC X(3)  VALUE 'END'.
020900     02  FILLER                  PIC X(8)  VALUE SPACE.
021000     02  FILLER                  PIC X(3)  VALUE 'END'.
021100     02  FILLER                  PIC X(12) VALUE SPACE.
021200     02  FILLER                  PIC X(5)  VALUE 'BLOCK'.
021300 01  HEAD-5.
021400     02  FILLER                  PIC X(2)  VALUE SPACE.
021500     02  FILLER                  PIC X(4)  VALUE 'NAME'.
021600     02  FILLER                  PIC X(6)  VALUE SPACE.
021610     02  FILLER                  PIC X(4)  VALUE 'NAME'.
021620     02  FILLER                  PIC X(6)  VALUE SPACE.
021700     02  FILLER                  PIC X(4)  VALUE 'NAME'.
021800     02  FILLER                  PIC X(6)  VALUE SPACE.
021900     02  FILLER                  PIC X(4)  VALUE 'NAME'.
022000     02  FILLER                  PIC X(42) VALUE SPACE.
022100     02  FILLER                  PIC X(4)  VALUE 'ADDR'.
022200     02  FILLER                  PIC X(3)  VALUE SPACE.
022300     02  FILLER                  PIC X(4)  VALUE 'NAME'.
022400     02  FILLER                  PIC X(4)  VALUE SPACE.
022500     02  FILLER                  PIC X(4)  VALUE 'DATE'.
022600     02  FILLER                  PIC X(7)  VALUE SPACE.
022700     02  FILLER                  PIC X(4)  VALUE 'TIME'.
022800     02  FILLER                  PIC X(11) VALUE SPACE.
022900     02  FILLER                  PIC X(5)  VALUE 'COUNT'.
023000 01  DETAIL-LINE.
023100     02  FILLER                  PIC X(2).
023200     02  D-JOB-NAME              PIC X(8).
023300     02  FILLER                  PIC X(2).
023310     02  D-USER-NAME             PIC X(8).
023320     02  FILLER                  PIC X(2).
023400     02  D-FILE-NAME             PIC X(8).
023500     02  FILLER                  PIC X(2).
023600     02  D-DATASET-NAME          PIC X(44).
023700     02  FILLER                  PIC X(2).
023800     02  D-UNIT-ADDR.
023900         03  D-ADDR-1            PIC X.
024000         03  D-ADDR-2            PIC X.
024100         03  D-ADDR-3            PIC X.
024200         03  D-ADDR-4            PIC X.
024300     02  FILLER                  PIC X(2).
024400     02  D-VOL-SER               PIC X(6).
024500     02  FILLER                  PIC X(2).
024600     02  D-END-DATE              PIC X(10).
024700     02  FILLER                  PIC X(2).
024800     02  D-END-TIME              PIC X(8).
024900     02  FILLER                  PIC X(1).
025000     02  D-BLK-CNT               PIC Z(15).
025100 01  WORK-BINARY                 PIC 9(8)  COMP.
025200 01  BINARY-WORK             REDEFINES WORK-BINARY
025300                                 PIC XXXX.
025400 01  BINARY-WORK-1           REDEFINES WORK-BINARY.
025500     02  FILLER                  PIC XXX.
025600     02  BINARY-ONE              PIC X.
025700 01  BINARY-WORK-2           REDEFINES WORK-BINARY.
025800     02  FILLER                  PIC XX.
025900     02  BINARY-TWO              PIC XX.
026000 01  BINARY-WORK-3           REDEFINES WORK-BINARY.
026100     02  FILLER                  PIC X.
026200     02  BINARY-THREE            PIC XXX.
026300 01  BINARY-WORK-4           REDEFINES WORK-BINARY.
026400     02  BINARY-FOUR             PIC X(4).
026600 01  EXPAND-BYTE.
026700     02  BIT-ZERO                PIC X.
026800     02  BIT-ONE                 PIC X.
026900     02  BIT-TWO                 PIC X.
027000     02  BIT-THREE               PIC X.
027100     02  BIT-FOUR                PIC X.
027200     02  BIT-FIVE                PIC X.
027300     02  BIT-SIX                 PIC X.
027400     02  BIT-SEVEN               PIC X.
027500 01  WORK-ARRAY-ONE.
027600     02  ARRAY-ONE           OCCURS 32760 TIMES  INDEXED BY A1.
027700         03  CHAR-ONE            PIC X.
027800 01  WORK-ARRAY-TWO.
027900     02  ARRAY-TWO           OCCURS 32760 TIMES  INDEXED BY A2.
028000         03  CHAR-TWO            PIC X.
028010*------------------------------------------------------------*
028020*  EXCLUDE DATA SET NAME TABLE                               *
028030*------------------------------------------------------------*
028040 01  ED-TABLE.
028050     05  ED-ENTRY OCCURS 100 TIMES.
028060         10  ED-MASK               PIC X(1) VALUE SPACE.
028070         10  ED-IO                 PIC X(1) VALUE SPACE.
028080         10  ED-LN                 PIC 9(02) VALUE ZERO.
028090         10  ED-DSN                PIC X(44) VALUE LOW-VALUES.
028091*------------------------------------------------------------*
028092*  INCLUDE DATA SET NAME TABLE                               *
028093*------------------------------------------------------------*
028094 01  ID-TABLE.
028095     05  ID-ENTRY OCCURS 100 TIMES.
028096         10  ID-MASK               PIC X(1) VALUE SPACE.
028097         10  ID-IO                 PIC X(1) VALUE SPACE.
028098         10  ID-LN                 PIC 9(02) VALUE ZERO.
028099         10  ID-DSN                PIC X(44) VALUE LOW-VALUES.
028100*------------------------------------------------------------*
028101*  EXCLUDE JOB NAME TABLE                                    *
028102*------------------------------------------------------------*
028103 01  EJ-TABLE.
028104     05  EJ-ENTRY OCCURS 100 TIMES.
028105         10  EJ-MASK               PIC X(1) VALUE SPACE.
028106         10  EJ-IO                 PIC X(1) VALUE SPACE.
028107         10  EJ-LN                 PIC 9(02) VALUE ZERO.
028108         10  EJ-JOB                PIC X(08) VALUE LOW-VALUES.
028109*------------------------------------------------------------*
028110*  INCLUDE JOB NAME TABLE                                    *
028111*------------------------------------------------------------*
028112 01  IJ-TABLE.
028113     05  IJ-ENTRY OCCURS 100 TIMES.
028114         10  IJ-MASK               PIC X(1) VALUE SPACE.
028115         10  IJ-IO                 PIC X(1) VALUE SPACE.
028116         10  IJ-LN                 PIC 9(02) VALUE ZERO.
028117         10  IJ-JOB                PIC X(08) VALUE LOW-VALUES.
028118*------------------------------------------------------------*
028119*  EXCLUDE USERID TABLE                                      *
028120*------------------------------------------------------------*
028121 01  EU-TABLE.
028122     05  EU-ENTRY OCCURS 100 TIMES.
028123         10  EU-MASK               PIC X(1) VALUE SPACE.
028124         10  EU-IO                 PIC X(1) VALUE SPACE.
028125         10  EU-LN                 PIC 9(02) VALUE ZERO.
028126         10  EU-USR                PIC X(08) VALUE LOW-VALUES.
028127*------------------------------------------------------------*
028128*  INCLUDE USERID TABLE                                      *
028129*------------------------------------------------------------*
028130 01  IU-TABLE.
028131     05  IU-ENTRY OCCURS 100 TIMES.
028132         10  IU-MASK               PIC X(1) VALUE SPACE.
028133         10  IU-IO                 PIC X(1) VALUE SPACE.
028134         10  IU-LN                 PIC 9(02) VALUE ZERO.
028135         10  IU-USR                PIC X(08) VALUE LOW-VALUES.
028136*------------------------------------------------------------*
028137*  EXCLUDE VOLUME NAME TABLE                                 *
028138*------------------------------------------------------------*
028139 01  EV-TABLE.
028140     05  EV-ENTRY OCCURS 100 TIMES.
028141         10  EV-MASK               PIC X(1) VALUE SPACE.
028142         10  EV-IO                 PIC X(1) VALUE SPACE.
028143         10  EV-LN                 PIC 9(02) VALUE ZERO.
028144         10  EV-VOL                PIC X(06) VALUE LOW-VALUES.
028145*------------------------------------------------------------*
028146*  INCLUDE VOLUME NAME TABLE                                 *
028147*------------------------------------------------------------*
028148 01  IV-TABLE.
028149     05  IV-ENTRY OCCURS 100 TIMES.
028150         10  IV-MASK               PIC X(1) VALUE SPACE.
028151         10  IV-IO                 PIC X(1) VALUE SPACE.
028152         10  IV-LN                 PIC 9(02) VALUE ZERO.
028153         10  IV-VOL                PIC X(06) VALUE LOW-VALUES.
028154*------------------------------------------------------------*
028155*  DSN OR JOB REDEFINES AREAS                                *
028156*------------------------------------------------------------*
028157 01  WORK-VALUE.
028158     05  WK-CHAR               OCCURS 44 TIMES
028159                               PIC X(1).
028160 01  CHK-FLD.
028161     05  CHK-CHAR              OCCURS 44 TIMES
028162                               PIC X(1).
028163 PROCEDURE DIVISION.
028164     OPEN INPUT  PARMIN.
028189     MOVE  0  TO NBR-RECORDS-READ.
028192     MOVE 'Y' TO PROCESS-ALL.
028193 PROCESS-PARMS.
028194*************************************************************
028195*  READ A PARAMETER CARD (IF THERE ARE ANY).                *
028196*************************************************************
028197     READ PARMIN AT END
028198         GO TO CHK-PARM-COUNT.
028199     ADD 1 TO NBR-RECORDS-READ.
028200     MOVE 'N' TO PROCESS-ALL.
028201     IF PARM-TYPE = 'ED'
028202         GO TO LOAD-ED.
028203     IF PARM-TYPE = 'ID'
028204         GO TO LOAD-ID.
028205     IF PARM-TYPE = 'EJ'
028206         GO TO LOAD-EJ.
028207     IF PARM-TYPE = 'IJ'
028208         GO TO LOAD-IJ.
028209     IF PARM-TYPE = 'EU'
028210         GO TO LOAD-EU.
028211     IF PARM-TYPE = 'IU'
028212         GO TO LOAD-IU.
028213     IF PARM-TYPE = 'EV'
028214         GO TO LOAD-EV.
028215     IF PARM-TYPE = 'IV'
028216         GO TO LOAD-IV.
028217     GO TO CHECK-PARM-NV.
028218*-----------------------------------------------------------*
028223 LOAD-ED.
028224*************************************************************
028225*  LOAD A EXCLUDE DATASET RECORD                            *
028226*************************************************************
028227     ADD 1 TO ED-SUB.
028228     IF ED-SUB > 100
028229         DISPLAY 'MORE THAN 100 TYPE ED PARAMETER CARDS'
028230         DISPLAY 'PARAMETER CARD NOT PROCESSED         '
028231         DISPLAY PARM-RECORD
028232         MOVE 999 TO AB-CODE
028233         CALL 'ILBOABN0' USING AB-CODE.
028234     MOVE ZERO TO X.
028235     MOVE PARM-DSN TO ED-DSN (ED-SUB).
028236 FIND-ED-MASK-LOOP.
028237     ADD 1 TO X.
028238     IF X > 44
028239         GO TO END-ED-MASK-LOOP.
028240     IF PARM-CHAR (X) = SPACE
028241         GO TO END-ED-MASK-LOOP.
028242     IF PARM-CHAR (X) NOT = '*'
028243         GO TO FIND-ED-MASK-LOOP.
028244     MOVE 'Y' TO ED-MASK (ED-SUB).
028246     MOVE X TO ED-LN (ED-SUB).
028247     DISPLAY 'DSN MASK : ' ED-DSN (ED-SUB) ' TO BE BYPASSED'.
028248     GO TO PROCESS-PARMS.
028249 END-ED-MASK-LOOP.
028250     SUBTRACT 1 FROM X.
028251     MOVE X TO ED-LN (ED-SUB).
028252     DISPLAY 'DSN      : ' ED-DSN (ED-SUB) ' TO BE BYPASSED'.
028253     GO TO PROCESS-PARMS.
028254*-----------------------------------------------------------*
028263 LOAD-ID.
028264*************************************************************
028265*  LOAD A INCLUDE DATASET RECORD                            *
028266*************************************************************
028267     ADD 1 TO ID-SUB.
028268     IF ID-SUB > 100
028269         DISPLAY 'MORE THAN 100 TYPE ID PARAMETER CARDS'
028270         DISPLAY 'PARAMETER CARD NOT PROCESSED         '
028271         DISPLAY PARM-RECORD
028272         MOVE 999 TO AB-CODE
028273         CALL 'ILBOABN0' USING AB-CODE.
028274     MOVE ZERO TO X.
028275     MOVE PARM-DSN TO ID-DSN (ID-SUB).
028276 FIND-ID-MASK-LOOP.
028277     ADD 1 TO X.
028278     IF X > 44
028279         GO TO END-ID-MASK-LOOP.
028280     IF PARM-CHAR (X) = SPACE
028281         GO TO END-ID-MASK-LOOP.
028282     IF PARM-CHAR (X) NOT = '*'
028283         GO TO FIND-ID-MASK-LOOP.
028284     MOVE 'Y' TO ID-MASK (ID-SUB).
028286     MOVE X TO ID-LN (ID-SUB).
028287     DISPLAY 'DSN MASK : ' ID-DSN (ID-SUB) ' TO BE SELECTED'.
028288     GO TO PROCESS-PARMS.
028289 END-ID-MASK-LOOP.
028290     SUBTRACT 1 FROM X.
028291     MOVE X TO ID-LN (ID-SUB).
028292     DISPLAY 'DSN      : ' ID-DSN (ID-SUB) ' TO BE SELECTED'.
028293     GO TO PROCESS-PARMS.
028294*-----------------------------------------------------------*
028303 LOAD-EJ.
028304*************************************************************
028305*  LOAD A EXCLUDE JOB RECORD                                *
028306*************************************************************
028307     ADD 1 TO EJ-SUB.
028308     IF EJ-SUB > 100
028309         DISPLAY 'MORE THAN 100 TYPE EJ PARAMETER CARDS'
028310         DISPLAY 'PARAMETER CARD NOT PROCESSED         '
028311         DISPLAY PARM-RECORD
028312         MOVE 999 TO AB-CODE
028313         CALL 'ILBOABN0' USING AB-CODE.
028314     MOVE ZERO TO X.
028315     MOVE PARM-JOB TO EJ-JOB (EJ-SUB).
028316 FIND-EJ-MASK-LOOP.
028317     ADD 1 TO X.
028318     IF X > 8
028319         GO TO END-EJ-MASK-LOOP.
028320     IF PARM-CHAR (X) = SPACE
028321         GO TO END-EJ-MASK-LOOP.
028322     IF PARM-CHAR (X) NOT = '*'
028323         GO TO FIND-EJ-MASK-LOOP.
028324     MOVE 'Y' TO EJ-MASK (EJ-SUB).
028326     MOVE X TO EJ-LN (EJ-SUB).
028327     DISPLAY 'JOB MASK : ' EJ-JOB (EJ-SUB) ' TO BE SELECTED'.
028328     GO TO PROCESS-PARMS.
028329 END-EJ-MASK-LOOP.
028330     SUBTRACT 1 FROM X.
028331     MOVE X TO EJ-LN (EJ-SUB).
028332     DISPLAY 'JOB      : ' EJ-JOB (EJ-SUB) ' TO BE SELECTED'.
028333     GO TO PROCESS-PARMS.
028334*-----------------------------------------------------------*
028343 LOAD-IJ.
028344*************************************************************
028345*  LOAD A INCLUDE JOB RECORD                                *
028346*************************************************************
028347     ADD 1 TO IJ-SUB.
028348     IF IJ-SUB > 100
028349         DISPLAY 'MORE THAN 100 TYPE IJ PARAMETER CARDS'
028350         DISPLAY 'PARAMETER CARD NOT PROCESSED         '
028351         DISPLAY PARM-RECORD
028352         MOVE 999 TO AB-CODE
028353         CALL 'ILBOABN0' USING AB-CODE.
028354     MOVE ZERO TO X.
028355     MOVE PARM-JOB TO IJ-JOB (IJ-SUB).
028356 FIND-IJ-MASK-LOOP.
028357     ADD 1 TO X.
028358     IF X > 8
028359         GO TO END-IJ-MASK-LOOP.
028360     IF PARM-CHAR (X) = SPACE
028361         GO TO END-IJ-MASK-LOOP.
028362     IF PARM-CHAR (X) NOT = '*'
028363         GO TO FIND-IJ-MASK-LOOP.
028364     MOVE 'Y' TO IJ-MASK (IJ-SUB).
028366     MOVE X TO IJ-LN (IJ-SUB).
028367     DISPLAY 'JOB MASK : ' IJ-JOB (IJ-SUB) ' TO BE SELECTED'.
028368     GO TO PROCESS-PARMS.
028369 END-IJ-MASK-LOOP.
028370     SUBTRACT 1 FROM X.
028371     MOVE X TO IJ-LN (IJ-SUB).
028372     DISPLAY 'JOB      : ' IJ-JOB (IJ-SUB) ' TO BE SELECTED'.
028373     GO TO PROCESS-PARMS.
028374*-----------------------------------------------------------*
028383*************************************************************
028384*  LOAD A EXCLUDE USER ID RECORD                            *
028385*************************************************************
028386 LOAD-EU.
028387     ADD 1 TO EU-SUB.
028388     IF EU-SUB > 100
028389         DISPLAY 'MORE THAN 100 TYPE EU PARAMETER CARDS'
028390         DISPLAY 'PARAMETER CARD NOT PROCESSED         '
028391         DISPLAY PARM-RECORD
028392         MOVE 999 TO AB-CODE
028393         CALL 'ILBOABN0' USING AB-CODE.
028394     MOVE ZERO TO X.
028395     MOVE PARM-JOB TO EU-USR (EU-SUB).
028396 FIND-EU-MASK-LOOP.
028397     ADD 1 TO X.
028398     IF X > 8
028399         GO TO END-EU-MASK-LOOP.
028400     IF PARM-CHAR (X) NOT = '*'
028401         GO TO FIND-EU-MASK-LOOP.
028402     IF PARM-CHAR (X) = SPACE
028403         GO TO END-EU-MASK-LOOP.
028404     MOVE 'Y' TO EU-MASK (EU-SUB).
028406     MOVE X TO EU-LN (EU-SUB).
028407     DISPLAY 'USER MASK: ' EU-USR (EU-SUB) ' TO BE SELECTED'.
028408     GO TO PROCESS-PARMS.
028409 END-EU-MASK-LOOP.
028410     SUBTRACT 1 FROM X.
028411     MOVE X TO EU-LN (EU-SUB).
028412     DISPLAY 'USER ID  : ' EU-USR (EU-SUB) ' TO BE SELECTED'.
028413     GO TO PROCESS-PARMS.
028414*-----------------------------------------------------------*
028423 LOAD-IU.
028424*************************************************************
028425*  LOAD A INCLUDE USER ID RECORD                            *
028426*************************************************************
028427     ADD 1 TO IU-SUB.
028428     IF IU-SUB > 100
028429         DISPLAY 'MORE THAN 100 TYPE IU PARAMETER CARDS'
028430         DISPLAY 'PARAMETER CARD NOT PROCESSED         '
028431         DISPLAY PARM-RECORD
028432         MOVE 999 TO AB-CODE
028433         CALL 'ILBOABN0' USING AB-CODE.
028434     MOVE ZERO TO X.
028435     MOVE PARM-JOB TO IU-USR (IU-SUB).
028436 FIND-IU-MASK-LOOP.
028437     ADD 1 TO X.
028438     IF X > 8
028439         GO TO END-IU-MASK-LOOP.
028440     IF PARM-CHAR (X) =  SPACE
028441         GO TO END-IU-MASK-LOOP.
028442     IF PARM-CHAR (X) NOT = '*'
028443         GO TO FIND-IU-MASK-LOOP.
028444     MOVE 'Y' TO IU-MASK (IU-SUB).
028446     MOVE X TO IU-LN (IU-SUB).
028447     DISPLAY 'USER MASK: ' IU-USR (IU-SUB) ' TO BE SELECTED'.
028448     GO TO PROCESS-PARMS.
028449 END-IU-MASK-LOOP.
028450     SUBTRACT 1 FROM X.
028451     MOVE X TO IU-LN (IU-SUB).
028452     DISPLAY 'USER ID  : ' IU-USR (IU-SUB) ' TO BE SELECTED'.
028453     GO TO PROCESS-PARMS.
028454*-----------------------------------------------------------*
028463 LOAD-EV.
028464*************************************************************
028465*  LOAD A EXCLUDE VOLUME RECORD                             *
028466*************************************************************
028467     ADD 1 TO EV-SUB.
028468     IF EV-SUB > 100
028469         DISPLAY 'MORE THAN 100 TYPE EV PARAMETER CARDS'
028470         DISPLAY 'PARAMETER CARD NOT PROCESSED         '
028471         DISPLAY PARM-RECORD
028472         MOVE 999 TO AB-CODE
028473         CALL 'ILBOABN0' USING AB-CODE.
028474     MOVE ZERO TO X.
028475     MOVE PARM-DSN TO EV-VOL (EV-SUB).
028476 FIND-EV-MASK-LOOP.
028477     ADD 1 TO X.
028478     IF X > 06
028479         GO TO END-EV-MASK-LOOP.
028480     IF PARM-CHAR (X) = SPACE
028481         GO TO END-EV-MASK-LOOP.
028482     IF PARM-CHAR (X) NOT = '*'
028483         GO TO FIND-EV-MASK-LOOP.
028484     MOVE 'Y' TO EV-MASK (EV-SUB).
028486     MOVE X TO EV-LN (EV-SUB).
028487     DISPLAY 'VOL MASK : ' EV-VOL (EV-SUB) ' TO BE BYPASSED'.
028488     GO TO PROCESS-PARMS.
028489 END-EV-MASK-LOOP.
028490     SUBTRACT 1 FROM X.
028491     MOVE X TO EV-LN (EV-SUB).
028492     DISPLAY 'VOL      : ' EV-VOL (EV-SUB) ' TO BE BYPASSED'.
028493     GO TO PROCESS-PARMS.
028495*-----------------------------------------------------------*
028504 LOAD-IV.
028505*************************************************************
028506*  LOAD A INCLUDE VOLUME RECORD                             *
028507*************************************************************
028508     ADD 1 TO IV-SUB.
028509     IF IV-SUB > 100
028510         DISPLAY 'MORE THAN 100 TYPE IV PARAMETER CARDS'
028511         DISPLAY 'PARAMETER CARD NOT PROCESSED         '
028512         DISPLAY PARM-RECORD
028513         MOVE 999 TO AB-CODE
028514         CALL 'ILBOABN0' USING AB-CODE.
028515     MOVE ZERO TO X.
028516     MOVE PARM-DSN TO IV-VOL (IV-SUB).
028517 FIND-IV-MASK-LOOP.
028518     ADD 1 TO X.
028519     IF X > 06
028520         GO TO END-IV-MASK-LOOP.
028521     IF PARM-CHAR (X) = SPACE
028522         GO TO END-IV-MASK-LOOP.
028523     IF PARM-CHAR (X) NOT = '*'
028524         GO TO FIND-IV-MASK-LOOP.
028525     MOVE 'Y' TO IV-MASK (IV-SUB).
028527     MOVE X TO IV-LN (IV-SUB).
028528     DISPLAY 'VOL MASK : ' IV-VOL (IV-SUB) ' TO BE SELECTED'.
028529     GO TO PROCESS-PARMS.
028530 END-IV-MASK-LOOP.
028531     SUBTRACT 1 FROM X.
028532     MOVE X TO IV-LN (IV-SUB).
028533     DISPLAY 'VOL      : ' IV-VOL (IV-SUB) ' TO BE SELECTED'.
028534     GO TO PROCESS-PARMS.
028535 CHECK-PARM-NV.
028536     DISPLAY 'INVALID PARAMETER TYPE, MUST BE E/ID E/IJ E/IU E/IV'
028537     DISPLAY 'CARD PROCESSED: ' PARM-RECORD.
028538     MOVE 888 TO AB-CODE.
028539     CALL 'ILBOABN0' USING AB-CODE.
028540 CHK-PARM-COUNT.
028544     CLOSE PARMIN.
028545     IF RETURN-CODE > 0
028546       GOBACK.
028547     SORT SMF-REC-TYPE64 ASCENDING SMF-DATE, SMF-TIME
028548         INPUT PROCEDURE IS SELECT-RECORDS
028549         OUTPUT PROCEDURE IS PRINT-REPORT.
028550 FINISH-JOB.
028560     MOVE ZERO TO RETURN-CODE.
028600     GOBACK.
028700*****************************************************************
028800*  THIS ROUTINE WILL FORMAT A BINARY BYTE (EIGHT BITS) INTO     *
028900*  EIGHT BYTES WITH THE BIT ON AND OFF REPRESENTATION IN EACH   *
029000*  BYTE.  THE BINARY BYTE IS PUT INTO FIELD WORK-CHAR AND THE   *
029100*  EIGHT BYTES ARE RETURNED IN FIELD EXPAND-BYTE.  THE FOUR     *
029200*  BYTE FIELDS WORK-BINARY ARE USED HEAVILY IN THIS ROUTINE     *
029300*****************************************************************
029400 EXPAND-BITS.
029500     MOVE LOW-VALUES TO BINARY-WORK.
029600     MOVE WORK-CHAR TO BINARY-ONE.
029700     COMPUTE WORK-BIT-EXPAND = WORK-BINARY.
029800     MOVE ALL ZERO TO EXPAND-BYTE.
029900     SUBTRACT 128 FROM WORK-BIT-EXPAND.
030000     IF WORK-BIT-EXPAND LESS THAN ZERO
030100         MOVE '0' TO BIT-ZERO
030200         ADD 128 TO WORK-BIT-EXPAND
030300       ELSE MOVE '1' TO BIT-ZERO.
030400     SUBTRACT 064 FROM WORK-BIT-EXPAND.
030500     IF WORK-BIT-EXPAND LESS THAN ZERO
030600         MOVE '0' TO BIT-ONE
030700         ADD 064 TO WORK-BIT-EXPAND
030800       ELSE MOVE '1' TO BIT-ONE.
030900     SUBTRACT 032 FROM WORK-BIT-EXPAND.
031000     IF WORK-BIT-EXPAND LESS THAN ZERO
031100         MOVE '0' TO BIT-TWO
031200         ADD 032 TO WORK-BIT-EXPAND
031300       ELSE MOVE '1' TO BIT-TWO.
031400     SUBTRACT 016 FROM WORK-BIT-EXPAND.
031500     IF WORK-BIT-EXPAND LESS THAN ZERO
031600         MOVE '0' TO BIT-THREE
031700         ADD 016 TO WORK-BIT-EXPAND
031800       ELSE MOVE '1' TO BIT-THREE.
031900     SUBTRACT 008 FROM WORK-BIT-EXPAND.
032000     IF WORK-BIT-EXPAND LESS THAN ZERO
032100         MOVE '0' TO BIT-FOUR
032200         ADD 008 TO WORK-BIT-EXPAND
032300       ELSE MOVE '1' TO BIT-FOUR.
032400     SUBTRACT 004 FROM WORK-BIT-EXPAND.
032500     IF WORK-BIT-EXPAND LESS THAN ZERO
032600         MOVE '0' TO BIT-FIVE
032700         ADD 004 TO WORK-BIT-EXPAND
032800       ELSE MOVE '1' TO BIT-FIVE.
032900     SUBTRACT 002 FROM WORK-BIT-EXPAND.
033000     IF WORK-BIT-EXPAND LESS THAN ZERO
033100         MOVE '0' TO BIT-SIX
033200         ADD 002 TO WORK-BIT-EXPAND
033300       ELSE MOVE '1' TO BIT-SIX.
033400     SUBTRACT 001 FROM WORK-BIT-EXPAND.
033500     IF WORK-BIT-EXPAND LESS THAN ZERO
033600         MOVE '0' TO BIT-SEVEN
033700         ADD 001 TO WORK-BIT-EXPAND
033800       ELSE MOVE '1' TO BIT-SEVEN.
033900 TRANSFER-DATA.
034000     MOVE ZERO TO X.
034100     SET A2 TO X.
034200     MOVE LOW-VALUES TO WORK-ARRAY-TWO.
034300 TRANSFER-LOOP.
034400     SET A2 UP BY 1.
034500     IF A2 > Y
034600         GO TO END-TRANSFER-LOOP.
034700     SET A1 UP BY 1.
034800     MOVE CHAR-ONE (A1) TO CHAR-TWO (A2).
034900     GO TO TRANSFER-LOOP.
035000 END-TRANSFER-LOOP.
035100     EXIT.
035120*************************************************************
035130*  CHK FOR EXCLUDE OR INCLUDE                               *
035140*************************************************************
035150 CHECK-FIELD.
035160     MOVE 'N' TO CONT.
035170     IF CHK-MASK NOT = 'Y'
035180         AND CHK-FLD = WORK-VALUE
035190         MOVE 'Y' TO CONT
035200         GO TO CHK-FLD-EXIT.
035210     MOVE ZERO TO X.
035220 CHK-CHAR-LOOP.
035230     ADD 1 TO X.
035240     IF X > CHK-LN
035250         GO TO CHK-FLD-EXIT.
035260     IF X > 44
035270         GO TO CHK-FLD-EXIT.
035280     IF WK-CHAR (X) NOT = CHK-CHAR (X)
035290       AND CHK-CHAR(X) NOT = '*'
035291         GO TO CHK-FLD-EXIT.
035292     IF CHK-CHAR (X) = '*'
035293         MOVE 'Y' TO CONT
035294         GO TO CHK-FLD-EXIT.
035295     GO TO CHK-CHAR-LOOP.
035296 CHK-FLD-EXIT.
035297     EXIT.
035298 PRINT-HEADINGS-ROUTINE.
035299     ADD 1 TO PAGE-CNT
035300     MOVE PAGE-CNT TO HEAD-PAGE.
035400     MOVE HEAD-1 TO PRINT-LINE.
035500     WRITE PRINT-LINE AFTER ADVANCING TO-CHAN-1.
035600     MOVE HEAD-2 TO PRINT-LINE.
035700     WRITE PRINT-LINE AFTER ADVANCING 1 LINES.
035800     MOVE HEAD-3 TO PRINT-LINE.
035900     WRITE PRINT-LINE AFTER ADVANCING 1 LINES.
036000     MOVE HEAD-4 TO PRINT-LINE.
036100     WRITE PRINT-LINE AFTER ADVANCING 2 LINES.
036200     MOVE HEAD-5 TO PRINT-LINE.
036300     WRITE PRINT-LINE AFTER ADVANCING 1 LINES.
036400     MOVE SPACE TO PRINT-LINE.
036500     MOVE ZERO TO LINE-COUNT.
036600 PRINT-1.
036610     WRITE PRINT-LINE AFTER ADVANCING 1 LINES.
036620     ADD 1 TO LINE-COUNT.
036630     IF LINE-COUNT > 56
036640         PERFORM PRINT-HEADINGS-ROUTINE.
036650 PRINT-EXIT.
036660     EXIT.
036700 SELECT-RECORDS SECTION.
036800     OPEN INPUT SMF-DATA.
036900 READ-FILE.
037000     MOVE LOW-VALUES TO SMF-RECORD.
037100     READ SMF-DATA INTO SMF-RECORD AT END
037200         GO TO END-SELECT.
037300     MOVE REC-TYPE TO WORK-CHAR.
037400     MOVE LOW-VALUES TO BINARY-WORK.
037500     MOVE WORK-CHAR TO BINARY-ONE.
037600     COMPUTE WORK-BIT-EXPAND = WORK-BINARY.
037700     IF WORK-BIT-EXPAND NOT = 64
037800         GO TO READ-FILE.
037900     MOVE SMF-DATA-REC TO SMF-TYPE64.
038000     MOVE SMF-TYPE64 TO TYPE64-BASIC.
038001     MOVE LOW-VALUES TO WORK-ARRAY-ONE.
038002     MOVE SMF-RECORD TO WORK-ARRAY-ONE.
038003     SET A1 TO SMF64ESL.
038004     SET A1 UP BY 140.
038005     SET A1 DOWN BY 4.
038006     MOVE 292 TO Y.
038007     PERFORM TRANSFER-DATA THROUGH END-TRANSFER-LOOP.
038008     MOVE WORK-ARRAY-TWO TO TYPE64-STATS.
038009     MOVE TYPE64-BASIC TO SMF-TYPE64.
038010     MOVE TYPE64-STATS TO SMF64-STATS.
038011*************************************************************
038020*  CHECK PROCESS-ALL                                        *
038030*************************************************************
038040     IF PROCESS-ALL = 'Y'
038050       GO TO FORMAT-DATA.
038060     MOVE ZERO TO ED-SUB2.
038070     MOVE ZERO TO ID-SUB2.
038080     MOVE ZERO TO EJ-SUB2.
038090     MOVE ZERO TO IJ-SUB2.
038091     MOVE ZERO TO EU-SUB2.
038092     MOVE ZERO TO IU-SUB2.
038095*************************************************************
038096*  CHECK TO SEE IF DATA SET IS TO BE EXCLUDED               *
038097*************************************************************
038098 CHK-ED.
038099     ADD 1 TO ED-SUB2.
038100     IF ED-SUB2 > ED-SUB
038101         GO TO CHK-ID.
038105     MOVE SMF64CLN TO WORK-VALUE.
038106     MOVE ED-DSN (ED-SUB2) TO CHK-FLD.
038107     MOVE ED-LN (ED-SUB2) TO CHK-LN.
038108     MOVE ED-MASK (ED-SUB2) TO CHK-MASK.
038109     PERFORM CHECK-FIELD THRU CHK-FLD-EXIT.
038110     IF CONT = 'Y'
038111         GO TO READ-FILE.
038112     GO TO CHK-ED.
038113*************************************************************
038114*  CHECK TO SEE IF DATA SET IS TO BE INCLUDED               *
038115*************************************************************
038116 CHK-ID.
038117     ADD 1 TO ID-SUB2.
038118     IF ID-SUB2 > ID-SUB
038119       GO TO CHK-EJ.
038123     MOVE SMF64CLN TO WORK-VALUE.
038124     MOVE ID-DSN (ID-SUB2) TO CHK-FLD.
038125     MOVE ID-LN (ID-SUB2) TO CHK-LN.
038126     MOVE ID-MASK (ID-SUB2) TO CHK-MASK.
038132     PERFORM CHECK-FIELD THRU CHK-FLD-EXIT.
038133     IF CONT = 'Y'
038135       GO TO FORMAT-DATA.
038136     GO TO CHK-ID.
038137*************************************************************
038138*  CHECK TO SEE JOB IS TO BE EXCLUDED                       *
038139*************************************************************
038140 CHK-EJ.
038141     ADD 1 TO EJ-SUB2.
038142     IF EJ-SUB2 > EJ-SUB
038143       GO TO CHK-IJ.
038147     MOVE SMF64JBN TO WORK-VALUE.
038148     MOVE EJ-JOB (EJ-SUB2) TO CHK-FLD.
038149     MOVE EJ-LN (EJ-SUB2) TO CHK-LN.
038150     MOVE EJ-MASK (EJ-SUB2) TO CHK-MASK.
038151     PERFORM CHECK-FIELD THRU CHK-FLD-EXIT.
038152     IF CONT = 'Y'
038153       GO TO READ-FILE.
038154     GO TO CHK-EJ.
038155*************************************************************
038156*  CHECK TO SEE JOB IS TO BE INCLUDED                       *
038157*************************************************************
038158 CHK-IJ.
038159     ADD 1 TO IJ-SUB2.
038160     IF IJ-SUB2 > IJ-SUB
038161       GO TO CHK-EU.
038165     MOVE SMF64JBN TO WORK-VALUE.
038166     MOVE IJ-JOB (IJ-SUB2) TO CHK-FLD.
038167     MOVE IJ-LN (IJ-SUB2) TO CHK-LN.
038168     MOVE IJ-MASK (IJ-SUB2) TO CHK-MASK.
038169     PERFORM CHECK-FIELD THRU CHK-FLD-EXIT.
038170     IF CONT = 'Y'
038171       GO TO FORMAT-DATA.
038172     GO TO CHK-IJ.
038173*************************************************************
038174*  CHECK TO SEE USERID IS TO BE INCLUDED                    *
038175*************************************************************
038176 CHK-EU.
038177     ADD 1 TO EU-SUB2.
038178     IF EU-SUB2 > EU-SUB
038179       GO TO CHK-IU.
038183     MOVE SMF64UIF TO WORK-VALUE.
038184     MOVE EU-USR (EU-SUB2) TO CHK-FLD.
038185     MOVE EU-LN (EU-SUB2) TO CHK-LN.
038186     MOVE EU-MASK (EU-SUB2) TO CHK-MASK.
038187     PERFORM CHECK-FIELD THRU CHK-FLD-EXIT.
038188     IF CONT = 'Y'
038189       GO TO READ-FILE.
038190     GO TO CHK-EU.
038191*************************************************************
038192*  CHECK TO SEE USER ID IS TO BE INCLUDED                   *
038193*************************************************************
038194 CHK-IU.
038195     ADD 1 TO IU-SUB2.
038196     IF IU-SUB2 > IU-SUB
038197       GO TO FORMAT-DATA.
038201     MOVE SMF64UIF TO WORK-VALUE.
038202     MOVE IU-USR (IU-SUB2) TO CHK-FLD.
038203     MOVE IU-LN (IU-SUB2) TO CHK-LN.
038204     MOVE IU-MASK (IU-SUB2) TO CHK-MASK.
038205     PERFORM CHECK-FIELD THRU CHK-FLD-EXIT.
038206     IF CONT = 'Y'
038207       GO TO FORMAT-DATA.
038210     GO TO CHK-IU.
038231 FORMAT-DATA.
038900     SET A1 TO 140.
039000     SET A1 DOWN BY 4.
039100     MOVE 26 TO Y.
039200     COMPUTE SMF64-NU-EXT = SMF64ESL / 26.
039300 EXCP-LOOP.
039400     PERFORM TRANSFER-DATA THROUGH END-TRANSFER-LOOP.
039500     MOVE WORK-ARRAY-TWO TO TYPE64-EXTENT.
039600     MOVE REC-DATE TO SMF-DATE.
039700     MOVE REC-TIME TO SMF-TIME.
039800     MOVE TYPE64-BASIC TO SMF-TYPE64.
039900     MOVE TYPE64-STATS TO SMF64-STATS.
040000     MOVE TYPE64-EXTENT TO SMF64-EXTENT.
040002     IF PROCESS-ALL = 'Y'
040003       GO TO RELEASE-RECORD.
040004     IF CONT = 'Y'
040005       GO TO RELEASE-RECORD.
040011     MOVE ZERO TO EV-SUB2.
040012     MOVE ZERO TO IV-SUB2.
040013*************************************************************
040020*  CHECK TO SEE IF VOLUME IS TO BE EXCLUDED                 *
040030*************************************************************
040040 CHK-EV.
040050     ADD 1 TO EV-SUB2.
040060     IF EV-SUB2 > EV-SUB
040070         GO TO CHK-IV.
040092     MOVE SMF64VSN TO WORK-VALUE.
040093     MOVE EV-VOL (EV-SUB2) TO CHK-FLD.
040094     MOVE EV-LN (EV-SUB2) TO CHK-LN.
040095     MOVE EV-MASK (EV-SUB2) TO CHK-MASK.
040096     PERFORM CHECK-FIELD THRU CHK-FLD-EXIT.
040097     IF CONT = 'Y'
040098         GO TO READ-FILE.
040099     GO TO CHK-EV.
040100*************************************************************
040101*  CHECK TO SEE IF VOLUME IS TO BE INCLUDED                 *
040102*************************************************************
040103 CHK-IV.
040104     ADD 1 TO IV-SUB2.
040105     IF IV-SUB2 > IV-SUB
040106       GO TO READ-FILE.
040110     MOVE SMF64VSN TO WORK-VALUE.
040111     MOVE IV-VOL (IV-SUB2) TO CHK-FLD.
040112     MOVE IV-LN (IV-SUB2) TO CHK-LN.
040113     MOVE IV-MASK (IV-SUB2) TO CHK-MASK.
040114     PERFORM CHECK-FIELD THRU CHK-FLD-EXIT.
040115     IF CONT = 'Y'
040116       GO TO RELEASE-RECORD.
040117     GO TO CHK-IV.
040118 RELEASE-RECORD.
040120     RELEASE SORT-RECORD.
040200     SUBTRACT 1 FROM SMF64-NU-EXT.
040300     IF SMF64-NU-EXT < 1
040400         GO TO READ-FILE.
040500     GO TO EXCP-LOOP.
040600 END-SELECT.
040700     CLOSE SMF-DATA.
040800 END-SELECT-EXIT.
040900     EXIT.
041000 PRINT-REPORT SECTION.
041300     OPEN OUTPUT DATA-OUT.
041310     OPEN OUTPUT REPORT-OUT.
041400     MOVE 'SMFLIST ' TO HEAD-PROGRAM.
041500     MOVE 'SYSTEM MEASUREMENT FACILITY -- VSAM FILES ACCESSED'
041600         TO HEAD-SYS-DESC.
041700     MOVE 'DATE AND TIME SEQUENCE' TO HEAD-SEQ.
041800     CALL 'G207DCAN' USING G207-FUNC14, HEAD-DATE.
042000     PERFORM PRINT-HEADINGS-ROUTINE THROUGH PRINT-EXIT.
042100 GET-SMF-DATA.
042400     RETURN SMF-REC-TYPE64 AT END
042500         GO TO FINISH-REPORT.
042600     MOVE SPACE TO DETAIL-LINE.
042700     MOVE SMF64JBN TO D-JOB-NAME.
042710     MOVE SMF64UIF TO D-USER-NAME.
042800     DIVIDE SMF-TIME BY 100 GIVING X.
042900     DIVIDE X BY 3600 GIVING D-HH
043000         REMAINDER Y.
043100     DIVIDE Y BY 60 GIVING D-MM
043200         REMAINDER D-SS.
043300     MOVE D-TIME TO D-END-TIME.
043400     MOVE SMF-DATE TO WORK-JUL.
043500     MOVE WORK-JUL TO WORK-JUL-1.
043600     CALL 'G207DCAN' USING G207-FUNC34, WORK-JUL D-END-DATE.
043700     MOVE SMF64DDN TO D-FILE-NAME.
043800     MOVE SMF64CLN TO D-DATASET-NAME.
043900     MOVE SMF64VSN TO D-VOL-SER.
044000     MOVE LOW-VALUES TO WORK-ARRAY-ONE.
044100     MOVE SMF64CUU TO WORK-ARRAY-ONE.
044200     MOVE CHAR-ONE (1) TO WORK-CHAR.
044300     PERFORM EXPAND-BITS.
044400     MOVE ZERO TO X.
044500     IF BIT-ZERO = '1'
044600         ADD 8 TO X.
044700     IF BIT-ONE = '1'
044800         ADD 4 TO X.
044900     IF BIT-TWO = '1'
045000         ADD 2 TO X.
045100     IF BIT-THREE = '1'
045200         ADD 1 TO X.
045300     IF X < 10
045400         MOVE X TO W
045500         MOVE W TO D-ADDR-1.
045600     IF X = 10
045700         MOVE 'A' TO D-ADDR-1.
045800     IF X = 11
045900         MOVE 'B' TO D-ADDR-1.
046000     IF X = 12
046100         MOVE 'C' TO D-ADDR-1.
046200     IF X = 13
046300         MOVE 'D' TO D-ADDR-1.
046400     IF X = 14
046500         MOVE 'E' TO D-ADDR-1.
046600     IF X = 15
046700         MOVE 'F' TO D-ADDR-1.
046800     MOVE ZERO TO X.
046900     IF BIT-FOUR = '1'
047000         ADD 8 TO X.
047100     IF BIT-FIVE = '1'
047200         ADD 4 TO X.
047300     IF BIT-SIX = '1'
047400         ADD 2 TO X.
047500     IF BIT-SEVEN = '1'
047600         ADD 1 TO X.
047700     IF X < 10
047800         MOVE X TO W
047900         MOVE W TO D-ADDR-2.
048000     IF X = 10
048100         MOVE 'A' TO D-ADDR-2.
048200     IF X = 11
048300         MOVE 'B' TO D-ADDR-2.
048400     IF X = 12
048500         MOVE 'C' TO D-ADDR-2.
048600     IF X = 13
048700         MOVE 'D' TO D-ADDR-2.
048800     IF X = 14
048900         MOVE 'E' TO D-ADDR-2.
049000     IF X = 15
049100         MOVE 'F' TO D-ADDR-2.
049200     MOVE CHAR-ONE (2) TO WORK-CHAR.
049300     PERFORM EXPAND-BITS.
049400     MOVE ZERO TO X.
049500     IF BIT-ZERO = '1'
049600         ADD 8 TO X.
049700     IF BIT-ONE = '1'
049800         ADD 4 TO X.
049900     IF BIT-TWO = '1'
050000         ADD 2 TO X.
050100     IF BIT-THREE = '1'
050200         ADD 1 TO X.
050300     IF X < 10
050400         MOVE X TO W
050500         MOVE W TO D-ADDR-3.
050600     IF X = 10
050700         MOVE 'A' TO D-ADDR-3.
050800     IF X = 11
050900         MOVE 'B' TO D-ADDR-3.
051000     IF X = 12
051100         MOVE 'C' TO D-ADDR-3.
051200     IF X = 13
051300         MOVE 'D' TO D-ADDR-3.
051400     IF X = 14
051500         MOVE 'E' TO D-ADDR-3.
051600     IF X = 15
051700         MOVE 'F' TO D-ADDR-3.
051800     MOVE ZERO TO X.
051900     IF BIT-FOUR = '1'
052000         ADD 8 TO X.
052100     IF BIT-FIVE = '1'
052200         ADD 4 TO X.
052300     IF BIT-SIX = '1'
052400         ADD 2 TO X.
052500     IF BIT-SEVEN = '1'
052600         ADD 1 TO X.
052700     IF X < 10
052800         MOVE X TO W
052900         MOVE W TO D-ADDR-4.
053000     IF X = 10
053100         MOVE 'A' TO D-ADDR-4.
053200     IF X = 11
053300         MOVE 'B' TO D-ADDR-4.
053400     IF X = 12
053500         MOVE 'C' TO D-ADDR-4.
053600     IF X = 13
053700         MOVE 'D' TO D-ADDR-4.
053800     IF X = 14
053900         MOVE 'E' TO D-ADDR-4.
054000     IF X = 15
054100         MOVE 'F' TO D-ADDR-4.
054200     COMPUTE SMF64BLK = SMF64DLR + SMF64DDE + SMF64DIN
054300                                 + SMF64DUP + SMF64DRE.
054400     MOVE SMF64BLK TO R-BLK-CNT.
054500*    IF R-BLK-CNT = ZERO
054600*        GO TO GET-SMF-DATA.
054700     MOVE R-BLK-CNT TO D-BLK-CNT.
054800     MOVE D-JOB-NAME TO R-JOB-NAME.
054900     MOVE D-FILE-NAME TO R-FILE-NAME.
055000     MOVE D-DATASET-NAME TO R-DATASET-NAME.
055100     MOVE D-UNIT-ADDR TO R-UNIT-ADDR.
055200     MOVE D-END-DATE TO R-END-DATE.
055300     MOVE D-END-TIME TO R-END-TIME.
055400     MOVE D-VOL-SER TO R-VOL-SER.
055500     WRITE RECORD-OUT.
055600     MOVE DETAIL-LINE TO P-LINE.
055700     PERFORM PRINT-1 THROUGH PRINT-EXIT.
055800     GO TO GET-SMF-DATA.
055900 FINISH-REPORT.
056000     CLOSE REPORT-OUT DATA-OUT.
056100 PRINT-REPORT-EXIT.
056200     EXIT.
