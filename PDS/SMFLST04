001306 IDENTIFICATION DIVISION.
001307 PROGRAM-ID. SMFLIST.
001308 AUTHOR. MATTHEW V. STITT.
001309 DATE-WRITTEN. 06/15/91.
001310 DATE-COMPILED.
001311 REMARKS. PRINT LISTING OF PROGRAM EXECUTIONS FROM SMF RECORDS.
001312 ENVIRONMENT DIVISION.
001313 CONFIGURATION SECTION.
001314 SOURCE-COMPUTER. IBM-3081.
001315 OBJECT-COMPUTER. IBM-3081.
001316 SPECIAL-NAMES.
001317     C01 IS TO-CHAN-1.
001320 INPUT-OUTPUT SECTION.
001400 FILE-CONTROL.
001500     SELECT SMF-DATA ASSIGN TO UT-S-SYS004.
001600     SELECT REPORT-OUT ASSIGN TO UT-S-SYS019.
001700     SELECT SMF-REC-TYPE4 ASSIGN TO UT-S-SORTWK01.
001800 DATA DIVISION.
001900 FILE SECTION.
002000 FD  SMF-DATA
002020     LABEL RECORDS ARE STANDARD
002030     RECORD CONTAINS 0 CHARACTERS
002040     BLOCK CONTAINS 32760 CHARACTERS
002050     RECORDING MODE IS S.
002060 01  SMF-RECORD-A            PIC X(32752).
002600 FD  REPORT-OUT
002610     RECORDING MODE IS F
002620     BLOCK CONTAINS 0 RECORDS
002630     LABEL RECORDS ARE STANDARD.
002640 01  PRINT-LINE.
002700     02  P-LINE                  PIC X(133).
002800 SD  SMF-REC-TYPE4.
002900 01  SORT-RECORD.
003000     02  SMF-TIME                PIC S9(8)  COMP.
003100     02  SMF-DATE                PIC 9(7)   COMP-3.
003200     02  SMF-TYPE4.
003300         03  SMF4JBN             PIC X(8).
003400         03  SMF4RST             PIC S9(8)  COMP.
003500         03  SMF4RSD             PIC 9(7)   COMP-3.
003600         03  SMF4UIF             PIC X(8).
003700         03  SMF4STN             PIC X.
003800         03  SMF4SIT             PIC S9(8)  COMP.
003900         03  SMF4STID            PIC 9(7)   COMP-3.
004000         03  SMF4NCI             PIC S9(8)  COMP.
004100         03  SMF4SCC             PIC S9(4)  COMP.
004200         03  SMF4PRTY            PIC X.
004300         03  SMF4PGMN            PIC X(8).
004400         03  SMF4STMN            PIC X(8).
004500         03  SMF4RSV5            PIC XX.
004600         03  SMF4SYST            PIC S9(4)  COMP.
004700         03  SMF4HOST            PIC S9(4)  COMP.
004800         03  SMF4RV1             PIC XX.
004900         03  SMF4RSHO            PIC S9(8)  COMP.
005000         03  SMF4SPK             PIC X.
005100         03  SMF4STI             PIC X.
005200         03  SMF4RV2             PIC XX.
005300         03  SMF4AST             PIC S9(8)  COMP.
005400         03  SMF4PPST            PIC S9(8)  COMP.
005500         03  SMF4SRBT            PIC S9(8)  COMP.
005600         03  SMF4RIN.
005700             04  SMF4RIN1        PIC X.
005800             04  SMF4RIN2        PIC X.
005900         03  SMF4RLCT            PIC S9(4)  COMP.
006000         03  SMF4LENN            PIC S9(4)  COMP.
006100     02  SMF4-RELOCATE.
006200         03  SMF4PGIN            PIC S9(8)  COMP.
006300         03  SMF4PGOT            PIC S9(8)  COMP.
006400         03  SMF4NSW             PIC S9(8)  COMP.
006500         03  SMF4PSI             PIC S9(8)  COMP.
006600         03  SMF4PSO             PIC S9(8)  COMP.
006700         03  SMF4VPI             PIC S9(8)  COMP.
006800         03  SMF4VPO             PIC S9(8)  COMP.
006900         03  SMF4SST             PIC S9(8)  COMP.
007000         03  SMF4ACT             PIC S9(8)  COMP.
007100         03  SMF4PGNO            PIC S9(4)  COMP.
007200         03  SMF4TRAN            PIC S9(8)  COMP.
007300         03  SMF4RECL            PIC S9(8)  COMP.
007400         03  SMF4RCLM            PIC S9(8)  COMP.
007500         03  SMF4CPGN            PIC S9(8)  COMP.
007600         03  SMF4CRCL            PIC S9(8)  COMP.
007700         03  SMF4PGST            PIC S9(8)  COMP.
007800         03  SMF4PSEC            PIC S9(8)  COMP.
007900         03  SMF4LPAI            PIC S9(8)  COMP.
008000         03  SMF4LPAR            PIC S9(8)  COMP.
008100         03  SMF4CPUS            PIC S9(8)  COMP.
008200         03  SMF4IOCS            PIC S9(8)  COMP.
008300         03  SMF4MSOS            PIC S9(8)  COMP.
008400         03  SMF4SRBS            PIC S9(8)  COMP.
008500 WORKING-STORAGE SECTION.
008600 77  WS-START                    PIC X(36)  VALUE
008700     'SMFLIST  WORKING STORAGE STARTS HERE'.
008800 77  WORK-CHAR                   PIC X.
008900 77  WORK-BIT-EXPAND             PIC S9(5)  COMP-3  VALUE ZERO.
009000 77  WORK-JUL                    PIC 9(5).
009001 77  WORK-JUL1                   PIC S9(7).
009002 77  WORK-JUL2                   PIC S9(7).
009010 77  PAGE-CNT                    PIC 9(5)   VALUE ZERO.
009020 77  LINE-COUNT                  PIC 9(5)   VALUE ZERO.
009030 77  DAY-DIFF                    PIC S9(7)  COMP-3  VALUE ZERO.
009100 77  G207-FUNC10                 PIC XX     VALUE '10'.
009110 77  G207-FUNC14                 PIC XX     VALUE '14'.
009111 77  G207-FUNC16                 PIC XX     VALUE '16'.
009120 77  G207-FUNC26                 PIC XX     VALUE '26'.
009200 77  X                           PIC S9(15)  COMP.
009300 77  Y                           PIC S9(15)  COMP.
009400 77  Z                           PIC S9(15)  COMP.
009500 77  SMF4-RELOC-DISP             PIC S9(8)  COMP.
009600 01  SMF-RECORD.
009700*    02  REC-LEN                 PIC XX.
009800*    02  SEG-DESC                PIC XX.
009900     02  SYS-IND                 PIC X.
010000     02  REC-TYPE                PIC X.
010100     02  REC-TIME                PIC S9(8)  COMP.
010200     02  REC-DATE                PIC 9(7)   COMP-3.
010300     02  SYS-IDENT               PIC X(4).
010400     02  SMF-DATA-REC            PIC X(32760).
010500 01  SMF4-DEVICE.
010600     02  SMF4DEVC                PIC X.
010700     02  SMF4UTYP                PIC X.
010800     02  SMF4CUAD                PIC S9(4)  COMP.
010900     02  SMF4EXCP                PIC S9(8)  COMP.
011000 01  SMF4-ACCOUNT.
011100     02  SMF4LNTH                PIC X.
011200     02  SMF4SETM                PIC XXX.
011300     02  SMF4NAF                 PIC X.
011400 01  D-TIME.
011500     02  D-HH                    PIC ZZ99.
011600     02  FILLER                  PIC X      VALUE '.'.
011700     02  D-MM                    PIC 99.
011800     02  FILLER                  PIC X      VALUE '.'.
011900     02  D-SS                    PIC 99.
011910 01  HEAD-1.
011920     02  FILLER                  PIC X     VALUE SPACE.
011930     02  HEAD-PROGRAM            PIC X(8)  VALUE SPACE.
011940     02  FILLER                  PIC X(47) VALUE SPACE.
011950     02  FILLER                  PIC X(22) VALUE
011960         'HUNTSVILLE HOSPITAL   '.
011970     02  FILLER                  PIC X(45) VALUE SPACE.
011980     02  FILLER                  PIC X(5)  VALUE 'PAGE '.
011990     02  HEAD-PAGE               PIC ZZZZ9 VALUE ZERO.
011991 01  HEAD-2.
011992     02  FILLER                  PIC X     VALUE SPACE.
011993     02  FILLER                  PIC X(5)  VALUE 'DATE '.
011994     02  HEAD-DATE               PIC X(10) VALUE SPACE.
011995     02  FILLER                  PIC X(11) VALUE SPACE.
011996     02  HEAD-SYS-DESC           PIC X(80) VALUE SPACE.
011997 01  HEAD-3.
011998     02  FILLER                  PIC X     VALUE SPACE.
011999     02  FILLER                  PIC X(51) VALUE SPACE.
012000     02  HEAD-SEQ                PIC X(30) VALUE SPACE.
012100 01  HEAD-4.
012200     02  FILLER                  PIC X(5)  VALUE SPACE.
012300     02  FILLER                  PIC X(3)  VALUE 'JOB'.
012400     02  FILLER                  PIC X(10) VALUE SPACE.
012500     02  FILLER                  PIC X(4)  VALUE 'STEP'.
012600     02  FILLER                  PIC X(9)  VALUE SPACE.
012700     02  FILLER                  PIC X(7)  VALUE 'PROGRAM'.
012800     02  FILLER                  PIC X(6)  VALUE SPACE.
012900     02  FILLER                  PIC X(5)  VALUE 'START'.
013000     02  FILLER                  PIC X(8)  VALUE SPACE.
013100     02  FILLER                  PIC X(5)  VALUE 'START'.
013200     02  FILLER                  PIC X(8)  VALUE SPACE.
013300     02  FILLER                  PIC X(3)  VALUE 'END'.
013400     02  FILLER                  PIC X(10) VALUE SPACE.
013500     02  FILLER                  PIC X(3)  VALUE 'END'.
013600     02  FILLER                  PIC X(10) VALUE SPACE.
013700     02  FILLER                  PIC X(8)  VALUE 'DURATION'.
013800 01  HEAD-5.
013900     02  FILLER                  PIC X(5)  VALUE SPACE.
014000     02  FILLER                  PIC X(4)  VALUE 'NAME'.
014100     02  FILLER                  PIC X(9)  VALUE SPACE.
014200     02  FILLER                  PIC X(4)  VALUE 'NAME'.
014300     02  FILLER                  PIC X(11) VALUE SPACE.
014400     02  FILLER                  PIC X(4)  VALUE 'NAME'.
014500     02  FILLER                  PIC X(7)  VALUE SPACE.
014600     02  FILLER                  PIC X(4)  VALUE 'DATE'.
014700     02  FILLER                  PIC X(9)  VALUE SPACE.
014800     02  FILLER                  PIC X(4)  VALUE 'TIME'.
014900     02  FILLER                  PIC X(9)  VALUE SPACE.
015000     02  FILLER                  PIC X(4)  VALUE 'DATE'.
015100     02  FILLER                  PIC X(9)  VALUE SPACE.
015200     02  FILLER                  PIC X(4)  VALUE 'TIME'.
015300     02  FILLER                  PIC X(11) VALUE SPACE.
015400     02  FILLER                  PIC X(4)  VALUE 'TIME'.
015500 01  DETAIL-LINE.
015600     02  FILLER                  PIC X(5).
015700     02  D-JOB-NAME              PIC X(8).
015800     02  FILLER                  PIC X(5).
015900     02  D-STEP-NAME             PIC X(8).
016000     02  FILLER                  PIC X(5).
016100     02  D-PROG-NAME             PIC X(8).
016200     02  FILLER                  PIC X(3).
016300     02  D-START-DATE            PIC X(10).
016400     02  FILLER                  PIC X(1).
016500     02  D-START-TIME            PIC X(10).
016600     02  FILLER                  PIC X(5).
016700     02  D-END-DATE              PIC X(10).
016800     02  FILLER                  PIC X(1).
016900     02  D-END-TIME              PIC X(10).
017000     02  FILLER                  PIC X(5).
017100     02  D-DUR-TIME              PIC X(10).
017200 01  WORK-BINARY                 PIC 9(8)  COMP.
017300 01  BINARY-WORK             REDEFINES WORK-BINARY
017400                                 PIC XXXX.
017500 01  BINARY-WORK-1           REDEFINES WORK-BINARY.
017600     02  FILLER                  PIC XXX.
017700     02  BINARY-ONE              PIC X.
017800 01  BINARY-WORK-2           REDEFINES WORK-BINARY.
017900     02  FILLER                  PIC XX.
018000     02  BINARY-TWO              PIC XX.
018100 01  BINARY-WORK-3           REDEFINES WORK-BINARY.
018200     02  FILLER                  PIC X.
018300     02  BINARY-THREE            PIC XXX.
018400 01  BINARY-WORK-4           REDEFINES WORK-BINARY.
018500     02  BINARY-FOUR             PIC X(4).
018600 SKIP1
018700 01  EXPAND-BYTE.
018800     02  BIT-ZERO                PIC X.
018900     02  BIT-ONE                 PIC X.
019000     02  BIT-TWO                 PIC X.
019100     02  BIT-THREE               PIC X.
019200     02  BIT-FOUR                PIC X.
019300     02  BIT-FIVE                PIC X.
019400     02  BIT-SIX                 PIC X.
019500     02  BIT-SEVEN               PIC X.
019600 01  WORK-ARRAY-ONE.
019700     02  ARRAY-ONE           OCCURS 32760 TIMES  INDEXED BY A1.
019800         03  CHAR-ONE            PIC X.
019900 01  WORK-ARRAY-TWO.
020000     02  ARRAY-TWO           OCCURS 32760 TIMES  INDEXED BY A2.
020100         03  CHAR-TWO            PIC X.
020200 PROCEDURE DIVISION.
020300     SORT SMF-REC-TYPE4 ASCENDING SMF4STID, SMF4SIT
020400         INPUT PROCEDURE IS SELECT-RECORDS
020500         OUTPUT PROCEDURE IS PRINT-REPORT.
020600 FINISH-JOB.
020700     GOBACK.
020800*****************************************************************
020900*  THIS ROUTINE WILL FORMAT A BINARY BYTE (EIGHT BITS) INTO     *
021000*  EIGHT BYTES WITH THE BIT ON AND OFF REPRESENTATION IN EACH   *
021100*  BYTE.  THE BINARY BYTE IS PUT INTO FIELD WORK-CHAR AND THE   *
021200*  EIGHT BYTES ARE RETURNED IN FIELD EXPAND-BYTE.  THE FOUR     *
021300*  BYTE FIELDS WORK-BINARY ARE USED HEAVILY IN THIS ROUTINE     *
021400*****************************************************************
021500 EXPAND-BITS.
021600     MOVE LOW-VALUES TO BINARY-WORK.
021700     MOVE WORK-CHAR TO BINARY-ONE.
021800     COMPUTE WORK-BIT-EXPAND = WORK-BINARY.
021900     MOVE ALL ZERO TO EXPAND-BYTE.
022000     SUBTRACT 128 FROM WORK-BIT-EXPAND.
022100     IF WORK-BIT-EXPAND LESS THAN ZERO
022200         MOVE '0' TO BIT-ZERO
022300         ADD 128 TO WORK-BIT-EXPAND
022400       ELSE MOVE '1' TO BIT-ZERO.
022500     SUBTRACT 064 FROM WORK-BIT-EXPAND.
022600     IF WORK-BIT-EXPAND LESS THAN ZERO
022700         MOVE '0' TO BIT-ONE
022800         ADD 064 TO WORK-BIT-EXPAND
022900       ELSE MOVE '1' TO BIT-ONE.
023000     SUBTRACT 032 FROM WORK-BIT-EXPAND.
023100     IF WORK-BIT-EXPAND LESS THAN ZERO
023200         MOVE '0' TO BIT-TWO
023300         ADD 032 TO WORK-BIT-EXPAND
023400       ELSE MOVE '1' TO BIT-TWO.
023500     SUBTRACT 016 FROM WORK-BIT-EXPAND.
023600     IF WORK-BIT-EXPAND LESS THAN ZERO
023700         MOVE '0' TO BIT-THREE
023800         ADD 016 TO WORK-BIT-EXPAND
023900       ELSE MOVE '1' TO BIT-THREE.
024000     SUBTRACT 008 FROM WORK-BIT-EXPAND.
024100     IF WORK-BIT-EXPAND LESS THAN ZERO
024200         MOVE '0' TO BIT-FOUR
024300         ADD 008 TO WORK-BIT-EXPAND
024400       ELSE MOVE '1' TO BIT-FOUR.
024500     SUBTRACT 004 FROM WORK-BIT-EXPAND.
024600     IF WORK-BIT-EXPAND LESS THAN ZERO
024700         MOVE '0' TO BIT-FIVE
024800         ADD 004 TO WORK-BIT-EXPAND
024900       ELSE MOVE '1' TO BIT-FIVE.
025000     SUBTRACT 002 FROM WORK-BIT-EXPAND.
025100     IF WORK-BIT-EXPAND LESS THAN ZERO
025200         MOVE '0' TO BIT-SIX
025300         ADD 002 TO WORK-BIT-EXPAND
025400       ELSE MOVE '1' TO BIT-SIX.
025500     SUBTRACT 001 FROM WORK-BIT-EXPAND.
025600     IF WORK-BIT-EXPAND LESS THAN ZERO
025700         MOVE '0' TO BIT-SEVEN
025800         ADD 001 TO WORK-BIT-EXPAND
025900       ELSE MOVE '1' TO BIT-SEVEN.
026000 SELECT-RECORDS SECTION.
026100     OPEN INPUT SMF-DATA.
026200 READ-FILE.
026300     MOVE LOW-VALUES TO SMF-RECORD.
026400     READ SMF-DATA AT END
026500         GO TO END-SELECT.
026510     MOVE SMF-RECORD-A TO SMF-RECORD.
026600     MOVE REC-TYPE TO WORK-CHAR.
026700     MOVE LOW-VALUES TO BINARY-WORK.
026800     MOVE WORK-CHAR TO BINARY-ONE.
026900     COMPUTE WORK-BIT-EXPAND = WORK-BINARY.
027000     IF WORK-BIT-EXPAND NOT = 4
027100         GO TO READ-FILE.
027200     MOVE SMF-DATA-REC TO SMF-TYPE4.
027300     MOVE LOW-VALUES TO WORK-ARRAY-ONE.
027400     MOVE SMF-DATA-REC TO WORK-ARRAY-ONE.
027500     SET A1 TO SMF4RLCT.
027600     MOVE ZERO TO X.
027700     SET A2 TO X.
027800     MOVE LOW-VALUES TO WORK-ARRAY-TWO.
027900 RELOC-LOOP.
028000     SET A1 UP BY 1.
028100     SET A2 UP BY 1.
028200     IF A2 > 94
028300         GO TO END-RELOC-LOOP.
028400     MOVE CHAR-ONE (A1) TO CHAR-TWO (A2).
028500     GO TO RELOC-LOOP.
028600 END-RELOC-LOOP.
028700     MOVE WORK-ARRAY-TWO TO SMF4-RELOCATE.
028800     MOVE REC-DATE TO SMF-DATE.
028900     MOVE REC-TIME TO SMF-TIME.
029000     RELEASE SORT-RECORD.
029100     GO TO READ-FILE.
029200 END-SELECT.
029300     CLOSE SMF-DATA.
029400 END-SELECT-EXIT.
029500     EXIT.
029600 PRINT-REPORT SECTION.
029700     OPEN OUTPUT REPORT-OUT.
029800     MOVE 'SMFLIST ' TO HEAD-PROGRAM.
029900     MOVE 'SYSTEM MEASUREMENT FACILITY -- PROGRAMS RUN'
030000         TO HEAD-SYS-DESC.
030100     MOVE 'AS GIVEN ON DATA FILE' TO HEAD-SEQ.
030200     CALL 'G207DCAN' USING G207-FUNC14, HEAD-DATE.
030300     PERFORM PRINT-HEADINGS-ROUTINE THROUGH PRINT-EXIT.
030400     GO TO GET-SMF-DATA.
030500 PRINT-HEADINGS-ROUTINE.
030510     ADD 1 TO PAGE-CNT
030520     MOVE PAGE-CNT TO HEAD-PAGE.
030530     MOVE HEAD-1 TO PRINT-LINE.
030540     WRITE PRINT-LINE AFTER ADVANCING TO-CHAN-1.
030550     MOVE HEAD-2 TO PRINT-LINE.
030560     WRITE PRINT-LINE AFTER ADVANCING 1 LINES.
030570     MOVE HEAD-3 TO PRINT-LINE.
030580     WRITE PRINT-LINE AFTER ADVANCING 1 LINES.
030590     MOVE HEAD-4 TO PRINT-LINE.
030591     WRITE PRINT-LINE AFTER ADVANCING 2 LINES.
030592     MOVE HEAD-5 TO PRINT-LINE.
030593     WRITE PRINT-LINE AFTER ADVANCING 1 LINES.
030594     MOVE SPACE TO PRINT-LINE.
030595     MOVE ZERO TO LINE-COUNT.
030596 PRINT-1.
030597     WRITE PRINT-LINE AFTER ADVANCING 1 LINES.
030598     ADD 1 TO LINE-COUNT.
030599     IF LINE-COUNT > 56
030600         PERFORM PRINT-HEADINGS-ROUTINE.
030601 PRINT-EXIT.
030602     EXIT.
030610 GET-SMF-DATA.
030700     RETURN SMF-REC-TYPE4 AT END
030800         GO TO FINISH-REPORT.
030900     MOVE SPACE TO DETAIL-LINE.
031000     MOVE SMF4JBN TO D-JOB-NAME.
031100     MOVE SMF4STMN TO D-STEP-NAME.
031200     MOVE SMF4PGMN TO D-PROG-NAME.
031300     DIVIDE SMF-TIME BY 100 GIVING X.
031400     DIVIDE X BY 3600 GIVING D-HH
031500         REMAINDER Y.
031600     DIVIDE Y BY 60 GIVING D-MM
031700         REMAINDER D-SS.
031800     MOVE D-TIME TO D-END-TIME.
031900*    MOVE SMF-DATE TO WORK-JUL.
031910     MOVE SMF-DATE TO WORK-JUL1.
032000*    CALL 'G207DCAN' USING G207-FUNC10, WORK-JUL D-END-DATE.
032010     CALL 'G207DCAN' USING G207-FUNC16, WORK-JUL1 D-END-DATE.
032100     DIVIDE SMF4SIT BY 100 GIVING X.
032200     DIVIDE X BY 3600 GIVING D-HH
032300         REMAINDER Y.
032400     DIVIDE Y BY 60 GIVING D-MM
032500         REMAINDER D-SS.
032600     MOVE D-TIME TO D-START-TIME.
032700*    MOVE SMF4STID TO WORK-JUL.
032710     MOVE SMF4STID TO WORK-JUL2.
032800*    CALL 'G207DCAN' USING G207-FUNC10, WORK-JUL D-START-DATE.
032810     CALL 'G207DCAN' USING G207-FUNC16, WORK-JUL2 D-START-DATE.
032811     MOVE ZERO TO DAY-DIFF.
032820     CALL 'G207DCAN' USING DAY-DIFF, WORK-JUL2 WORK-JUL1.
032900*    IF SMF4SIT > SMF-TIME
033000*        ADD 8640000 TO SMF-TIME.
033101     MULTIPLY DAY-DIFF BY 8640000 GIVING X.
033103     ADD SMF-TIME TO X.
033110     SUBTRACT SMF4SIT FROM X GIVING Z.
033120*    SUBTRACT SMF-TIME FROM SMF4SIT GIVING Z.
033200     DIVIDE Z BY 100 GIVING X.
033300     DIVIDE X BY 3600 GIVING D-HH
033400         REMAINDER Y.
033500     DIVIDE Y BY 60 GIVING D-MM
033600         REMAINDER D-SS.
033700     MOVE D-TIME TO D-DUR-TIME.
033800     MOVE DETAIL-LINE TO P-LINE.
033900     PERFORM PRINT-1 THROUGH PRINT-EXIT.
034000     GO TO GET-SMF-DATA.
034100 FINISH-REPORT.
034200     CLOSE REPORT-OUT.
034300 PRINT-REPORT-EXIT.
034400     EXIT.
