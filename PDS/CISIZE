       IDENTIFICATION DIVISION.
       PROGRAM-ID. CISIZE.
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
           C01 IS TO-CHAN-1.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT PRINT-FILE ASSIGN TO UT-S-PRINTER.
       DATA DIVISION.
       FILE SECTION.
       FD  PRINT-FILE
           RECORDING MODE IS F
           LABEL RECORDS ARE STANDARD.
       01  PRINT-LINE              PIC X(133).
       WORKING-STORAGE SECTION.
       77  LINE-CNT                PIC 9(3) VALUE ZERO.
       77  CI-SIZE                 PIC S9(5) COMP-3.
       77  REC-LEN                 PIC S9(5) COMP-3.
       77  FSPC-PCT                PIC S9(3) COMP-3.
       77  WORK-CI-SIZE            PIC S9(5) COMP-3.
       77  WORK-NO-RECS            PIC S9(5) COMP-3.
       77  WORK-RECS-TOTAL         PIC S9(5) COMP-3.
       77  WORK-FSPC-CI            PIC S9(5) COMP-3.
       77  WORK-FSPC-RECS          PIC S9(5) COMP-3.
       77  WORK-FSPC-REM           PIC S9(5) COMP-3.
       77  WORK-FSPC-PCT           PIC S99V9(3) COMP-3.
       77  A                       PIC S9(4) COMP.
       77  END-PAGE-SWITCH         PIC X.
       77  PAGE-CTR                PIC S9(5) COMP-3.
       01  WORK-DATE.
           02  WORK-YY             PIC X(2) VALUE SPACE.
           02  WORK-MM             PIC X(2) VALUE SPACE.
           02  WORK-DD             PIC X(2) VALUE SPACE.
       01  HEAD-LINE-1.
           02  FILLER              PIC X(5) VALUE SPACE.
           02  FILLER              PIC X(6) VALUE 'CISIZE'.
           02  FILLER              PIC X(4) VALUE SPACE.
           02  FILLER              PIC X(26) VALUE
               'OPTIMUM CISIZE CALCULATION'.
           02  FILLER              PIC X(27) VALUE SPACE.
           02  FILLER              PIC X(13) VALUE 'FREESPACE OF '.
           02  REP-FSPC-PCT        PIC ZZ9.
           02  FILLER              PIC X(1) VALUE '%'.
           02  FILLER              PIC X(7) VALUE '  DATE '.
           02  REP-DATE.
               03  REP-MONTH       PIC X(2).
               03  FILLER          PIC X(1) VALUE '/'.
               03  REP-DAY         PIC X(2).
               03  FILLER          PIC X(1) VALUE '/'.
               03  REP-YEAR        PIC X(2).
           02  FILLER              PIC X(7) VALUE '  PAGE '.
           02  REP-PAGE            PIC ZZZ9.
       01  DETAIL-LINE.
           02  FILLER              PIC X(5).
           02  P-REC-LEN           PIC X(8).
           02  P-REC-BAR           PIC X(1).
           02  PRINT-ARRAY.
               03  PRINT-FILL  OCCURS 16 TIMES.
                   04  PRINT-DET   PIC X(7).
       01  DETAIL-RECORDS.
           02  FILLER              PIC X.
           02  DET-NO-RECS         PIC ZZZ9.
           02  DET-ASK             PIC X.
           02  DET-BAR             PIC X.
       01  PRINT-LEN.
           02  FILLER              PIC X(4).
           02  PRINT-REC-LEN       PIC ZZZ9.
           02  FILLER              PIC X.
       PROCEDURE DIVISION.
           OPEN OUTPUT PRINT-FILE.
           ACCEPT WORK-DATE FROM DATE.
           MOVE WORK-YY TO REP-YEAR.
           MOVE WORK-MM TO REP-MONTH.
           MOVE WORK-DD TO REP-DAY.
           MOVE ZERO TO FSPC-PCT, PAGE-CTR.
           PERFORM WRITE-HEADINGS THRU WRITE-HEADINGS-EXIT.
           GO TO START-HERE.
       WRITE-HEADINGS.
           MOVE SPACE TO END-PAGE-SWITCH.
           ADD 1 TO PAGE-CTR.
           MOVE ZERO TO LINE-CNT.
           MOVE PAGE-CTR TO REP-PAGE.
           MOVE FSPC-PCT TO REP-FSPC-PCT.
           MOVE HEAD-LINE-1 TO PRINT-LINE.
           WRITE PRINT-LINE AFTER ADVANCING TO-CHAN-1.
           MOVE SPACE TO DETAIL-LINE.
           MOVE 'I' TO P-REC-BAR.
           MOVE ZERO TO A, CI-SIZE.
       WRITE-HEADINGS-LOOP.
           ADD 1 TO A.
           IF A > 16
               GO TO END-HEADINGS-LOOP.
           ADD 512 TO CI-SIZE.
           MOVE SPACE TO DETAIL-RECORDS.
           MOVE CI-SIZE TO DET-NO-RECS.
           MOVE 'I' TO DET-BAR.
           MOVE DETAIL-RECORDS TO PRINT-DET (A).
           GO TO WRITE-HEADINGS-LOOP.
       END-HEADINGS-LOOP.
           WRITE PRINT-LINE FROM DETAIL-LINE AFTER ADVANCING 2 LINES.
           PERFORM WRITE-SEPARATOR-LINE THRU WRITE-SEPARATOR-EXIT.
       WRITE-HEADINGS-EXIT.
           EXIT.
       WRITE-SEPARATOR-LINE.
           MOVE SPACE TO DETAIL-LINE.
           MOVE '--------' TO P-REC-LEN.
           MOVE 'I' TO P-REC-BAR.
           MOVE ZERO TO A.
       SET-UP-SEPARATOR-LOOP.
           ADD 1 TO A.
           IF A > 16
               GO TO END-SET-UP-LOOP.
           MOVE '-------' TO DETAIL-RECORDS.
           MOVE 'I' TO DET-BAR.
           MOVE DETAIL-RECORDS TO PRINT-DET (A).
           GO TO SET-UP-SEPARATOR-LOOP.
       END-SET-UP-LOOP.
           ADD 1 TO LINE-CNT.
           WRITE PRINT-LINE FROM DETAIL-LINE AFTER ADVANCING 1 LINES.
           IF LINE-CNT > 25
               MOVE '1' TO END-PAGE-SWITCH.
       WRITE-SEPARATOR-EXIT.
           EXIT.
       START-HERE.
           MOVE ZERO TO REC-LEN.
       PROCESS-LOOP-1.
           ADD 8 TO REC-LEN.
           IF REC-LEN > 2048
               GO TO END-PROCESS-LOOP-1.
           MOVE REC-LEN TO PRINT-REC-LEN.
           MOVE PRINT-LEN TO P-REC-LEN.
           MOVE 'I' TO P-REC-BAR.
           MOVE ZERO TO A, CI-SIZE.
       PROCESS-LOOP-2.
           MOVE SPACE TO DETAIL-RECORDS.
           ADD 512 TO CI-SIZE.
           IF CI-SIZE > 8192
               GO TO END-PROCESS-LOOP-2.
           ADD 1 TO A.
           IF A > 16
               GO TO END-PROCESS-LOOP-2.
           COMPUTE WORK-FSPC-PCT = FSPC-PCT / 100.
           COMPUTE WORK-CI-SIZE = (CI-SIZE - 10)
                                - (CI-SIZE * WORK-FSPC-PCT).
           COMPUTE WORK-NO-RECS = WORK-CI-SIZE / REC-LEN.
           COMPUTE WORK-RECS-TOTAL = (CI-SIZE - 10) / REC-LEN.
           COMPUTE WORK-FSPC-CI = CI-SIZE - 10 - WORK-CI-SIZE.
           COMPUTE WORK-FSPC-RECS = WORK-FSPC-CI / REC-LEN.
           COMPUTE WORK-FSPC-REM = (CI-SIZE - 10)
                                 - (WORK-NO-RECS * REC-LEN)
                                 - (WORK-FSPC-RECS * REC-LEN).
           IF WORK-FSPC-CI NOT = ZERO
               AND WORK-FSPC-CI > REC-LEN
               AND WORK-FSPC-REM < (REC-LEN * .30)
               MOVE '*' TO DET-ASK.
           IF WORK-FSPC-CI = ZERO
               AND WORK-FSPC-REM < (REC-LEN * .30)
               MOVE '*' TO DET-ASK.
           MOVE WORK-NO-RECS TO DET-NO-RECS.
           MOVE 'I' TO DET-BAR.
           MOVE DETAIL-RECORDS TO PRINT-DET (A).
           GO TO PROCESS-LOOP-2.
       END-PROCESS-LOOP-2.
           WRITE PRINT-LINE FROM DETAIL-LINE AFTER ADVANCING 1 LINES.
           IF LINE-CNT > 25
               MOVE '1' TO END-PAGE-SWITCH.
           PERFORM WRITE-SEPARATOR-LINE THRU WRITE-SEPARATOR-EXIT.
           IF END-PAGE-SWITCH = '1'
               PERFORM WRITE-HEADINGS THRU WRITE-HEADINGS-EXIT.
           GO TO PROCESS-LOOP-1.
       END-PROCESS-LOOP-1.
           ADD 5 TO FSPC-PCT.
           IF FSPC-PCT > 30
               GO TO FINISH-JOB.
           MOVE ZERO TO PAGE-CTR.
           PERFORM WRITE-HEADINGS THRU WRITE-HEADINGS-EXIT.
           GO TO START-HERE.
       FINISH-JOB.
           CLOSE PRINT-FILE.
           STOP RUN.
