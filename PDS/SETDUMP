       IDENTIFICATION DIVISION.
       PROGRAM-ID. MIDPPGM.
       ENVIRONMENT DIVISION.
       DATA DIVISION.
       WORKING-STORAGE SECTION.
       01  START-OF-STORAGE            PIC X(26) VALUE
           'MIDP WORKING STORAGE START'.
       01  DATETIME                    PIC S9(15) COMP VALUE +0.
       01  CICSRESP                    PIC S9(9) COMP VALUE +0.
       01  CICSRESP2                   PIC S9(4) COMP VALUE +0.
       01  SYSDMPCMD                   PIC  X(8)      VALUE SPACES.
       01  TRNDMPCODE                  PIC  X(4)      VALUE SPACES.
       01  BLANK-ENTRY                 PIC  XXXX      VALUE SPACES.
       01  FORMATTED-LENGTH            PIC S9(4) COMP VALUE +0110.
       01  NOLIMIT                     PIC S9(8) COMP VALUE +0999.
       01  ZERODUMPS                   PIC S9(8) COMP VALUE +0000.
       01  ACTION-CODE                 PIC S9(8) COMP VALUE +0000.
       01  SHUT-OPTION                 PIC S9(8) COMP VALUE +0000.
       01  TRNDMP-OPTION               PIC S9(8) COMP VALUE +0000.
       01  LOCAL-OPTION                PIC S9(8) COMP VALUE +0000.
       01  SYSDMP-OPTION               PIC S9(8) COMP VALUE +0000.
       01  SYSDMP-DUMPSCOPE            PIC S9(8) COMP VALUE +0000.
       01  SYSDMP-DAEOPTION            PIC S9(8) COMP VALUE +0000.
       01  MSGS-LAYOUT.
           05 MESSAGE-OK-SYS.
              07 FILLER                PIC X(52)   VALUE
               'MIDP HAS SUCCESSFULLY PROCESSED THE SYSDUMP CMD   : '.
              07 FILLER                PIC X(10)   VALUE SPACES.
           05 MESSAGE-OK-TRN.
              07 FILLER                PIC X(52)   VALUE
               'MIDP HAS SUCCESSFULLY PROCESSED TRANSACTION CODE  : '.
              07 FILLER                PIC X(10)   VALUE SPACES.
           05 MESSAGE-ERROR.
              07 FILLER                PIC X(52)   VALUE
               'MIDP HAS ENCOUNTERED AN ERROR PROCESSING CODE/CMD : '.
              07 MESSAGE-TYPE          PIC X(10)   VALUE SPACES.
       01  FORMATTED-MESSAGE.
           03  FILLER                  PIC X(10)   VALUE '********* '.
           03  FORMAT-DATE             PIC X(08)   VALUE SPACES.
           03  FILLER                  PIC X       VALUE SPACES.
           03  FORMAT-TIME             PIC X(08)   VALUE SPACES.
           03  FILLER                  PIC X       VALUE SPACES.
           03  FORMATTED-SCREEN-DATA.
               05  FORMATTED-TYPE      PIC X(62)   VALUE SPACES.
               05  FORMATTED-TEXT      PIC X(08)   VALUE SPACES.
               05  FILLER              PIC XX      VALUE SPACES.
               05  FORMATTED-RESP2     PIC 9999    VALUE 0.
           03  FILLER                  PIC X(06)   VALUE SPACES.
       01  SYSDUMP-CMD-TABLE.
            03 SCT-AVAILABLE-ENTRYS.
               05 FILLER          PIC X(8) VALUE 'FC0002  '.
               05 FILLER          PIC X(8) VALUE 'AP0001  '.
               05 FILLER          PIC X(8) VALUE 'SR0001  '.
               05 FILLER          PIC X(8) VALUE 'FFFFFFFF'.
               05 FILLER          PIC X(8) VALUE 'FFFFFFFF'.
               05 FILLER          PIC X(8) VALUE 'FFFFFFFF'.
               05 FILLER          PIC X(8) VALUE 'FFFFFFFF'.
               05 FILLER          PIC X(8) VALUE 'FFFFFFFF'.
               05 FILLER          PIC X(8) VALUE 'FFFFFFFF'.
               05 FILLER          PIC X(8) VALUE 'FFFFFFFF'.
            03 FILLER REDEFINES SCT-AVAILABLE-ENTRYS
               OCCURS 10 TIMES INDEXED BY XSCT.
               05 SCT-ENTRY           PIC X(08).
       COPY DFHAID.
       01  TRANDUMP-CODE-TABLE.
            03 TCT-AVAILABLE-ENTRYS.
               05 FILLER          PIC X(4) VALUE 'AZI6'.
               05 FILLER          PIC X(4) VALUE 'ATNI'.
               05 FILLER          PIC X(4) VALUE 'AZTS'.
               05 FILLER          PIC X(4) VALUE 'FFFF'.
               05 FILLER          PIC X(4) VALUE 'FFFF'.
               05 FILLER          PIC X(4) VALUE 'FFFF'.
               05 FILLER          PIC X(4) VALUE 'FFFF'.
               05 FILLER          PIC X(4) VALUE 'FFFF'.
               05 FILLER          PIC X(4) VALUE 'FFFF'.
               05 FILLER          PIC X(4) VALUE 'FFFF'.
            03 FILLER REDEFINES TCT-AVAILABLE-ENTRYS
               OCCURS 10 TIMES INDEXED BY XTCT.
               05 TCT-ENTRY           PIC X(04).
       01  END-OF-STORAGE              PIC X(26) VALUE
           'MIDP WORKING STORAGE END  '.
       LINKAGE SECTION.
       01  DFHCOMMAREA                 PIC X(2).
       PROCEDURE DIVISION.
       T000-START.
           EXEC CICS ASKTIME
                     ABSTIME(DATETIME)
                     END-EXEC.
           EXEC CICS FORMATTIME
                     ABSTIME(DATETIME)
                     DATESEP('/') MMDDYY(FORMAT-DATE)
                     TIMESEP      TIME(FORMAT-TIME)
                     END-EXEC.
       T005-SET-VALUES.
           MOVE DFHVALUE(ADD)        TO ACTION-CODE.
           MOVE DFHVALUE(NOSHUTDOWN) TO SHUT-OPTION.
           MOVE DFHVALUE(LOCAL)      TO LOCAL-OPTION.
           MOVE DFHVALUE(NOSYSDUMP)  TO SYSDMP-OPTION.
           MOVE DFHVALUE(NOTRANDUMP) TO TRNDMP-OPTION.
           MOVE DFHVALUE(LOCAL)      TO SYSDMP-DUMPSCOPE.
           MOVE DFHVALUE(DAE)        TO SYSDMP-DAEOPTION.
           SET XSCT TO 1.
       T010-ISSUE-COMMANDS.
           IF XSCT GREATER THAN 10 OR
              SCT-ENTRY (XSCT) EQUAL TO 'FFFFFFFF'
              SET XTCT TO 1
              GO TO T050-ISSUE-DUMP-CODES.
           MOVE SCT-ENTRY (XSCT) TO SYSDMPCMD
                                    FORMATTED-TEXT.
           EXEC CICS SET SYSDUMPCODE(SYSDMPCMD)
                         MAXIMUM(NOLIMIT)
                         ACTION(ACTION-CODE)
                         SHUTOPTION(SHUT-OPTION)
                         SYSDUMPING(SYSDMP-OPTION)
                         DUMPSCOPE(SYSDMP-DUMPSCOPE)
                         DAEOPTION(SYSDMP-DAEOPTION)
                         RESP(CICSRESP)
                         RESP2(CICSRESP2)
                         END-EXEC.
           IF CICSRESP EQUAL TO DFHRESP(NORMAL)
              MOVE MESSAGE-OK-SYS TO FORMATTED-TYPE
              MOVE 0          TO FORMATTED-RESP2
           ELSE
              PERFORM T080-ERROR-RTN THRU
                      T080-ERROR-RTN-EXIT
              MOVE CICSRESP2     TO FORMATTED-RESP2
              MOVE MESSAGE-ERROR TO FORMATTED-TYPE.
           IF EIBTRMID EQUAL TO SPACES OR
              EIBTRMID EQUAL TO LOW-VALUES
              GO TO T015-WRITE-QUEUE.
       T010-SEND-MESSAGE.
           EXEC CICS SEND ERASE
                          FROM(BLANK-ENTRY)
                          LENGTH(1)
                          END-EXEC.
       T015-WRITE-QUEUE.
           EXEC CICS WRITEQ TD
                     FROM(FORMATTED-MESSAGE)
                     LENGTH(FORMATTED-LENGTH)
                     QUEUE('CSSL')
                     END-EXEC.
           SET XSCT UP BY 1.
           GO TO T010-ISSUE-COMMANDS.
       T050-ISSUE-DUMP-CODES.
           IF XTCT GREATER THAN 10 OR
              TCT-ENTRY (XTCT) EQUAL TO 'FFFF'
              GO TO T110-END-OF-JOB.
           MOVE TCT-ENTRY (XTCT) TO TRNDMPCODE
                                    FORMATTED-TEXT.
           EXEC CICS SET TRANDUMPCODE(TRNDMPCODE)
                         MAXIMUM(ZERODUMPS)
                         DUMPSCOPE(LOCAL-OPTION)
                         ACTION(ACTION-CODE)
                         SHUTOPTION(SHUT-OPTION)
                         TRANDUMPING(TRNDMP-OPTION)
                         RESP(CICSRESP)
                         RESP2(CICSRESP2)
                         END-EXEC.
           IF CICSRESP EQUAL TO DFHRESP(NORMAL)
              MOVE MESSAGE-OK-TRN TO FORMATTED-TYPE
              MOVE 0          TO FORMATTED-RESP2
           ELSE
              PERFORM T080-ERROR-RTN THRU
                      T080-ERROR-RTN-EXIT
              MOVE CICSRESP2     TO FORMATTED-RESP2
              MOVE MESSAGE-ERROR TO FORMATTED-TYPE.
           IF EIBTRMID EQUAL TO SPACES OR
              EIBTRMID EQUAL TO LOW-VALUES
              GO TO T065-WRITE-QUEUE.
       T055-SEND-MESSAGE.
           EXEC CICS SEND ERASE
                          FROM(BLANK-ENTRY)
                          LENGTH(1)
                          END-EXEC.
       T065-WRITE-QUEUE.
           EXEC CICS WRITEQ TD
                     FROM(FORMATTED-MESSAGE)
                     LENGTH(FORMATTED-LENGTH)
                     QUEUE('CSSL')
                     END-EXEC.
           SET XTCT UP BY 1.
           GO TO T050-ISSUE-DUMP-CODES.
       T080-ERROR-RTN.
           IF CICSRESP = DFHRESP(DUPREC)
              MOVE 'DUPREC  : ' TO MESSAGE-TYPE
              GO TO T080-ERROR-RTN-EXIT.
           IF CICSRESP = DFHRESP(INVREQ)
              MOVE 'INVREQ  : ' TO MESSAGE-TYPE
              GO TO T080-ERROR-RTN-EXIT.
           IF CICSRESP = DFHRESP(IOERR)
              MOVE 'IOERR   : ' TO MESSAGE-TYPE
              GO TO T080-ERROR-RTN-EXIT.
           IF CICSRESP = DFHRESP(NOTAUTH)
              MOVE 'NOTAUTH : ' TO MESSAGE-TYPE
              GO TO T080-ERROR-RTN-EXIT.
           IF CICSRESP = DFHRESP(NOTFND)
              MOVE 'NOTFND  : ' TO MESSAGE-TYPE
              GO TO T080-ERROR-RTN-EXIT.
           IF CICSRESP = DFHRESP(NOSPACE)
              MOVE 'NOSPACE : ' TO MESSAGE-TYPE
              GO TO T080-ERROR-RTN-EXIT.
       T080-ERROR-RTN-EXIT.
           EXIT.
       T110-END-OF-JOB.
           EXEC CICS RETURN
               END-EXEC.
           GOBACK.
