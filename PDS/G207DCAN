***********************************************************************
*        G207DCAN -- DATE CONVERSION SUBPROGRAM                       *
*        STANDARD PARAMETER FORMAT                                    *
* (COBOL) CALL 'G207DCAN' USING FUNC-CODE, INPUT-DATE, OUTPUT-DATE    *
*                                                                     *
* FUNCTION CODES AVAILABLE FOR DATE CONVERSIONS                       *
*                                                                     *
*  01 - RETRIEVE CURRENT DATE IN CALENDAR FORMAT     - MM/DD/YY       *
*  02 - RETRIEVE CURRENT DATE IN COMPRESSED FORMAT   - MMDDYY         *
*  03 - RETRIEVE CURRENT DATE IN JULIAN FORMAT       - YYDDD          *
*  04 - RETRIEVE CURRENT DATE IN ALPHANUMERIC FORMAT - MAY 31, 1947   *
*  05 - CONVERT DATE FORMAT FROM CALENDAR TO COMPRESSED               *
*  06 - CONVERT DATE FORMAT FROM CALENDAR TO JULIAN                   *
*  07 - CONVERT DATE FORMAT FROM CALENDAR TO ALPHANUMERIC             *
*  08 - CONVERT DATE FORMAT FROM COMPRESSED TO CALENDAR               *
*  09 - CONVERT DATE FORMAT FROM COMPRESSED TO JULIAN                 *
*  10 - CONVERT DATE FORMAT FROM JULIAN TO CALENDAR                   *
*  11 - CONVERT DATE FROM JULIAN TO COMPRESSED                        *
*  12 - CONVERT DATE FROM JULIAN TO ALPHANUMERIC                      *
*  13 - CONVERT DATE FROM JULIAN TO DAY OF WEEK                       *
*  14 - RETRIEVE DATE IN CENTURY CALENDAR            - MM/DD/YYYY     *
*  15 - RETRIEVE DATE IN CENTURY JULIAN              - YYYYDDD        *
*  16 - CONVERT CENTURY DATE FROM JULIAN TO CALENDAR                  *
*  17 - CONVERT CENTURY DATE FROM CALENDAR TO JULIAN                  *
*  18 - CONVERT CENTURY DATE FROM JULIAN TO ALPHANUMERIC              *
*  19 - CONVERT CENTURY DATE FROM CALENDAR TO ALPHANUMERIC            *
*  20 - CONVERT CENTURY DATE FROM JULIAN TO DAY OF WEEK               *
*  21 - CONVERT DATE FROM CALENDAR TO CENTURY CALENDAR                *
*  22 - CONVERT DATE FROM JULIAN TO CENTURY JULIAN                    *
*  23 - CONVERT DATE FROM CALENDAR TO CENTURY JULIAN                  *
*  24 - CONVERT DATE FROM COMPRESSED TO CENTURY JULIAN                *
*  25 - CONVERT DATE FROM CENTURY JULIAN TO JULIAN                    *
*  26 - CONVERT DATE FROM CENTURY JULIAN TO CALENDAR                  *
*  27 - CONVERT DATE FROM CENTURY JULIAN TO COMPRESSED                *
*  28 - CONVERT CALENDAR TO DAY OF WEEK                               *
*  29 - CONVERT COMPRESSED TO DAY OF WEEK                             *
*  30 - CONVERT CENTURY CALENDAR TO DAY OF WEEK                       *
*  31 - CONVERT CENTURY CALENDAR TO CALENDAR                          *
*  32 - CONVERT CENTURY CALENDAR TO COMPRESSED                        *
*  33 - CONVERT CENTURY CALENDAR TO JULIAN                            *
*  34 - CONVERT DATE FROM JULIAN TO CENTURY CALENDAR                  *
*  35 - CONVERT DATE FROM TOD CLOCK TO CENTURY CALENDAR               *
*  36 - CONVERT DATE FROM TOD CLOCK TO CENTURY JULIAN                 *
*  37 - CONVERT DATE FROM TOD CLOCK TO ALPHANUMERIC                   *
*  38 - CONVERT DATE FROM TOD CLOCK TO DAY OF WEEK                    *
*                                                                     *
*        FIELD FORMATS                                                *
*  FUNCTION   INPUT PARAMETER        LENGTH  OUTPUT PARAMETER LENGTH  *
*  01         MM/DD/YY     (OUTPUT)    08    NOT USED                 *
*  02         MMDDYY       (OUTPUT)    06    NOT USED                 *
*  03         YYDDD        (OUTPUT)    05    NOT USED                 *
*  04         MAY 16, 1962 (OUTPUT)    19    NOT USED                 *
*  05         MM/DD/YY                 08    MMDDYY             06    *
*  06         MM/DD/YY                 08    YYDDD              05    *
*  07         MM/DD/YY                 19    MAY 16, 1962       19    *
*  08         MMDDYY                   06    MM/DD/YY           08    *
*  09         MMDDYY                   06    YYDDD              05    *
*  10         YYDDD                    05    MM/DD/YY           08    *
*  11         YYDDD                    05    MMDDYY             06    *
*  12         YYDDD                    05    MAY 16, 1962       19    *
*  13         YYDDD                    05    4WEDNESDAY         10    *
*  14         MM/DD/YYYY   (OUTPUT)    10    NOT USED                 *
*  15         YYYYDDD      (OUTPUT)    07    NOT USED                 *
*  16         YYYYDDD                  07    MM/DD/YYYY         10    *
*  17         MM/DD/YYYY               10    YYYYDDD            07    *
*  18         YYYYDDD                  07    MAY 16, 1962       19    *
*  19         MM/DD/YYYY               10    MAY 16, 1962       19    *
*  20         YYYYDDD                  07    4WEDNESDAY         10    *
*  21         MM/DD/YY                 08    MM/DD/YYYY         10    *
*  22         YYDDD                    05    YYYYDDD            07    *
*  23         MM/DD/YY                 08    YYYYDDD            07    *
*  24         MMDDYY                   06    YYYYDDD            07    *
*  25         YYYYDDD                  07    YYDDD              05    *
*  26         YYYYDDD                  07    MM/DD/YY           08    *
*  27         YYYYDDD                  07    MMDDYY             06    *
*  28         MM/DD/YY                 08    4WEDNESDAY         10    *
*  29         MMDDYY                   06    4WEDNESDAY         10    *
*  30         MM/DD/YYYY               10    4WEDNESDAY         10    *
*  31         MM/DD/YYYY               10    MM/DD/YY           08    *
*  32         MM/DD/YYYY               10    MMDDYY             06    *
*  33         MM/DD/YYYY               10    YYDDD              05    *
*  34         YYDDD                    05    MM/DD/YYYY         10    *
*  35         XXXXXXXX                 08    MM/DD/YYYY         10    *
*  36         XXXXXXXX                 08    YYYYDDD            07    *
*  37         XXXXXXXX                 10    MAY 16, 1962       19    *
*  38         XXXXXXXX                 07    4WEDNESDAY         10    *
*                                                                     *
*  IF THE FIRST PARAMETER IS PASSED AS A PACKED DECIMAL 7 DIGIT FIELD *
*  RATHER THAN A TWO CHARACTER FUNCTION CODE THE CALCULATION ROUTINES *
*  WILL BE ENTERED AND SUMS AND DIFFERENCES OF JULIAN DATES ARE       *
*  CALCULATED.  IF PARAMETER 1 IS ZERO PARAMETER 2 WILL BE SUBTRACTED *
*  FROM PARAMETER 3 AND THE RESULT WILL BE PLACED IN PARAMETER 1 AS A *
*  SIGNED NUMBER OF DAYS DIFFERENCE.  IF PARAMETER 1 IS NOT ZERO THEN *
*  IT WILL BE ADDED TO PARAMETER 2 AND THE RESULT PLACED IN PARAMETER *
*  3 AS A JULIAN DATE. PARAMETER 2 AND PARAMETER 3 ARE ASSUMED TO BE  *
*  7 CHARACTERS IN LENGTH SUITABLE FOR JULIAN DATES IN CENTURY FORMAT *
*                                                                     *
*  DATE FORMATS:                                                      *
*  1.  MM/DD/YY            CALENDAR                                   *
*  2.  MMDDYY              COMPRESSED                                 *
*  3.  YYDDD               JULIAN                                     *
*  4.  MONTH DAY, YEAR     ALPHANUMERIC                               *
*  5.  DAY OF WEEK         1WEEKDAY                                   *
*  6.  MM/DD/YYYY          EXTENDED CALENDAR                          *
*  7.  YYYYDDD             EXTENDED JULIAN                            *
*  8.  XXXXXXXX            TOD CLOCK FORMAT                           *
*                                                                     *
***********************************************************************
G207DCAN AMODE 31
G207DCAN RMODE ANY
G207DCAN CSECT
STRTHRE  STM  14,12,12(13)
         USING STRTHRE,15
         LA    9,SJULIAN
         ST    13,4(9)
         ST    9,8(13)
         BAL   13,PGMBEGIN
SJULIAN  DS    18F
         DC    CL8'G207DCAN'
PGMBEGIN L     3,0(1)
         TM    0(3),X'F0'
         BNO   CALCRTN
         CLC   0(2,3),=C'01'
         BL    NOFUN
         CLC   0(2,3),=C'38'
         BH    NOFUN
         L     4,4(1)
         L     5,8(1)
         MVC   DATEIN,0(4)
         MVC   DATEOUT,DATEIN
         PACK  DBLWRD,0(2,3)
         CVB   6,DBLWRD
         SLA   6,2
BRNCHTAB B     *(6)
F01      B     CV70
F02      B     CV70
F03      B     CV70
F04      B     CV70
F05      B     CV12
F06      B     CV16
F07      B     CV16
F08      B     CV21
F09      B     CV21
F10      B     CV37
F11      B     CV37
F12      B     CV37
F13      B     CV37
F14      B     CV70
F15      B     CV70
F16      B     CV76
F17      B     CV67
F18      B     CV76
F19      B     CV64
F20      B     CV75
F21      B     CV16
F22      B     CV37
F23      B     CV16
F24      B     CV21
F25      B     CV73
F26      B     CV76
F27      B     CV76
F28      B     CV16
F29      B     CV21
F30      B     CV67
F31      B     CV61
F32      B     CV61
F33      B     CV67
F34      B     CV37
F35      B     CV80
F36      B     CV80
F37      B     CV80
F38      B     CV80
NOFUN    DC    H'0'
**************************************************************
*                                                            *
*  RETRIEVE CURRENT DATE IN CENTURY JULIAN FORMAT            *
*  USING CURRENT CPU TOD CLOCK SETTING AS STARTING POINT     *
*                                                            *
*  FUNCTIONS: 01 02 03 04 14 15                              *
*                                                            *
**************************************************************
CV70     EQU   *
         LR    8,15
         STCK  TODCLCK
         LR    15,8
         MVC   TSVTS,TODCLCK
         L     8,16
         L     8,X'130'(,8)
         AL    8,TSVTS
         ST    8,TSVTS
         B     CV70A
**************************************************************
*                                                            *
*  CONVERT CPU TOD CLOCK SETTING TO CENTURY JULIAN FORMAT    *
*                                                            *
*  FUNCTIONS: 35 36 37 38                                    *
*                                                            *
**************************************************************
CV80     EQU   *
         MVC   TSVTS,DATEIN
CV70A    EQU   *
         ZAP   TSJULIAN,=P'1900'
         CLC   TSVTS,X4YEAR
         BL    CK1YEAR
         LA    7,X4YEAR
         BAL   8,SUBTRACT
         AP    TSJULIAN,=P'4'
CK4MORE  CLC   TSVTS,X4YEARS           ..4 MORE YEARS?
         BL    CKLEAPSW                ....NO,  GO CHECK FOR LEAP YEAR
         AP    TSJULIAN,=P'4'
         LA    7,X4YEARS
         BAL   8,SUBTRACT              SUBTRACT
         B     CK4MORE
CKLEAPSW CLC   TSVTS,X366DAYS          DATE OTHER THAN LEAP YEAR?
         BL    CALCDAYS
         AP    TSJULIAN,=P'1'          ..YES, ADD LEAP YEAR
         LA    7,X366DAYS
         BAL   8,SUBTRACT              SUBTRACT
CK1YEAR  CLC   TSVTS,X365DAYS         ANY MORE NON-LEAP YEARS LEFT?
         BL    CALCDAYS
         AP    TSJULIAN,=P'1'         ..YES, ADD NORMAL YEAR
         LA    7,X365DAYS
         BAL   8,SUBTRACT              SUBTRACT
         B     CK1YEAR
CALCDAYS SRP   TSJULIAN,3,0            CHANGE 0YYYY TO YYYYJJJ
         AP    TSJULIAN,=P'1'          SET TO FIRST DAY IN YEAR
CK1DAY   CLC   TSVTS,X1DAY             ONE MORE DAY REMAINING?
         BL    ENDDATE                 ..NO, GOT THE DATE
         AP    TSJULIAN,=P'1'
         LA    7,X1DAY
         BAL   8,SUBTRACT              SUBTRACT
         B     CK1DAY
ENDDATE  UNPK  JDATE(7),TSJULIAN
         OI    JDATE+L'JDATE-1,X'F0'
         MVC   DATEOUT(7),JDATE
         MVC   DATEIN(7),DATEOUT
         CLC   0(2,3),=C'03'
         BL    CV76
         BE    CV73
         CLC   0(2,3),=C'04'
         BE    CV76
         CLC   0(2,3),=C'14'
         BE    CV76
         CLC   0(2,3),=C'35'
         BE    CV76
         CLC   0(2,3),=C'37'
         BE    CV76
         CLC   0(2,3),=C'38'
         BE    CV75
         LA    11,6
         B     OUT
SUBTRACT L     9,TSVTS+4
         SL    9,4(7)                  SET CONDITION CODE
         ST    9,TSVTS+4
         L     9,TSVTS
         BNM   NOBORROW                BRANCH ON NO BORROW
         BCTR  9,0                     SUBTRACT BORROWED BIT
NOBORROW SL    9,0(7)
         ST    9,TSVTS
         BR    8
**************************************************************
*                                                            *
*  CONVERT DATE FROM CALENDAR TO CENTURY CALENDAR            *
*                                                            *
*  FUNCTIONS: 06 07 21 23 28                                 *
*             09 24 29                                       *
*                                                            *
**************************************************************
CV16     EQU   *
         MVC   DATEOUT(6),DATEIN
         MVC   DATEOUT+8(2),DATEIN+6
         CLI   DATEIN+6,C'5'
         BL    CV16A
         MVC   DATEOUT+6(2),=C'19'
         B     CV16B
CV16A    MVC   DATEOUT+6(2),=C'20'
CV16B    MVC   DATEIN(10),DATEOUT
         CLC   0(2,3),=C'06'
         BE    CV67
         CLC   0(2,3),=C'07'
         BE    CV64
         CLC   0(2,3),=C'09'
         BE    CV67
         CLC   0(2,3),=C'23'
         BE    CV67
         CLC   0(2,3),=C'24'
         BE    CV67
         CLC   0(2,3),=C'28'
         BE    CV67
         CLC   0(2,3),=C'29'
         BE    CV67
         LA    11,9
         B     OUT
**************************************************************
*                                                            *
*  CONVERT DATE FROM JULIAN TO CENTURY JULIAN                *
*                                                            *
*  FUNCTIONS: 10 11 12 13 22 34                              *
*                                                            *
**************************************************************
CV37     EQU   *
         MVC   DATEOUT+2(5),DATEIN
         CLI   DATEIN,C'5'
         BL    CV37A
         MVC   DATEOUT(2),=C'19'
         B     CV37B
CV37A    MVC   DATEOUT(2),=C'20'
CV37B    MVC   DATEIN(7),DATEOUT
         CLC   0(2,3),=C'34'
         BE    CV76
         CLC   0(2,3),=C'13'
         BL    CV76
         BE    CV75
         LA    11,6
         B     OUT
**************************************************************
*                                                            *
*  CONVERT DATE FROM CENTURY JULIAN TO JULIAN                *
*                                                            *
*  FUNCTIONS: 25                                             *
*             03 33                                          *
*             06                                             *
*             09                                             *
*                                                            *
**************************************************************
CV73     EQU   *
         MVC   DATEOUT(5),DATEIN+2
         MVC   DATEIN(5),DATEOUT
         LA    11,4
         B     OUT
**************************************************************
*                                                            *
*  CONVERT CENTURY DATE FROM JULIAN TO CALENDAR              *
*                                                            *
*  FUNCTIONS: 16 18 26 27                                    *
*             01 02 04 10 11 12 14 34 35 37                  *
*                                                            *
**************************************************************
CV76     EQU   *
         MVC   JDATE,DATEIN
         STM   0,5,ZQSAV
         PACK  DBLWRD,JDATE+0(4)
         CVB   5,DBLWRD
         PACK  DBLWRD,JDATE+4(3)
         CVB   1,DBLWRD
         LA    2,ZQTAB
         LA    3,12
         NI    ZQFND01+3,0
         C     5,=F'1900'
         BE    ZQLOP01
         C     5,=F'1800'
         BE    ZQLOP01
         C     5,=F'1700'
         BE    ZQLOP01
         N     5,=F'3'
         BC    4,ZQLOP01
         CH    1,=H'60'
         BL    ZQLOP01
         BH    *+8
         OI    ZQFND01+3,1
         BCT   1,ZQLOP01
ZQLOP01  SLA   3,1
         CH    1,0(3,2)
         BH    ZQFND01
         SRA   3,1
         BCT   3,ZQLOP01
         MVI   DBLWRD,X'F0'
         MVC   DBLWRD+1(7),DBLWRD
         B     ZQOUT
ZQFND01  LA    1,0(1)
         SH    1,0(3,2)
         SRA   3,1
         MH    3,=H'1000'
         AR    3,1
         CVD   3,DBLWRD
         UNPK  DBLWRD(5),DBLWRD+5(3)
         OI    DBLWRD+4,X'F0'
         MVC   DBLWRD+2(2),DBLWRD+3
         MVC   DBLWRD+4(4),JDATE
ZQOUT    MVC   DATEOUT(2),DBLWRD
         MVC   DATEOUT+3(2),DBLWRD+2
         MVC   DATEOUT+6(4),DBLWRD+4
         MVI   DATEOUT+2,C'/'
         MVI   DATEOUT+5,C'/'
         MVC   DATEIN(10),DATEOUT
         LM    0,5,ZQSAV
         CLC   0(2,3),=C'04'
         BL    CV61
         BE    CV64
         CLC   0(2,3),=C'12'
         BL    CV61
         BE    CV64
         CLC   0(2,3),=C'18'
         BE    CV64
         CLC   0(2,3),=C'26'
         BE    CV61
         CLC   0(2,3),=C'27'
         BE    CV61
         CLC   0(2,3),=C'37'
         BE    CV64
         LA    11,9
         B     OUT
ZQSAV    DS    6F
**************************************************************
*                                                            *
*  CONVERT CENTURY DATE FROM CALENDAR TO JULIAN              *
*                                                            *
*  FUNCTIONS: 17 30 33                                       *
*             06 23 28                                       *
*             09 24 29                                       *
*                                                            *
**************************************************************
CV67     EQU   *
         LA    2,DATEIN
         PACK  DBLWRD,0(2,2)
         CVB   6,DBLWRD
         SLA   6,1
         LA    6,ZQTAB(6)
         PACK  DBLWRD,3(2,2)
         CVB   7,DBLWRD
         AH    7,0(6)
         PACK  DBLWRD,6(4,2)
         CVB   6,DBLWRD
         C     6,=F'1900'
         BE    NOLEAP1
         C     6,=F'1800'
         BE    NOLEAP1
         C     6,=F'1700'
         BE    NOLEAP1
         N     6,=F'3'
         BC    4,NOLEAP1
         CLC   0(2,2),=C'03'
         BL    NOLEAP1
         LA    7,1(7)
NOLEAP1  CVD   7,DBLWRD
         UNPK  DATEOUT+4(3),DBLWRD+6(2)
         OI    DATEOUT+6,X'F0'
         MVC   DATEOUT(4),6(2)
         MVC   DATEIN(7),DATEOUT
         CLC   0(2,3),=C'06'
         BE    CV73
         CLC   0(2,3),=C'09'
         BE    CV73
         CLC   0(2,3),=C'28'
         BE    CV75
         CLC   0(2,3),=C'29'
         BE    CV75
         CLC   0(2,3),=C'30'
         BE    CV75
         CLC   0(2,3),=C'33'
         BE    CV73
         LA    11,6
         B     OUT
**************************************************************
*                                                            *
*  CONVERT CENTURY CALENDAR TO CALENDAR                      *
*                                                            *
*  FUNCTIONS: 31 32                                          *
*             01 02 26 27                                    *
*             04 10 11                                       *
*                                                            *
**************************************************************
CV61     EQU   *
         MVC   DATEOUT+6(2),DATEOUT+8
         MVC   DATEIN(8),DATEOUT
         CLC   0(2,3),=C'02'
         BE    CV12
         CLC   0(2,3),=C'32'
         BE    CV12
         CLC   0(2,3),=C'11'
         BE    CV12
         CLC   0(2,3),=C'27'
         BE    CV12
         LA    11,7
         B     OUT
**************************************************************
*                                                            *
*  CONVERT CALENDAR TO COMPRESSED CALENDAR                   *
*                                                            *
*  FUNCTIONS: 05                                             *
*             02 32                                          *
*             27                                             *
*             11                                             *
*                                                            *
**************************************************************
CV12     EQU   *
         MVC   DATEOUT(2),DATEIN
         MVC   DATEOUT+2(2),DATEIN+3
         MVC   DATEOUT+4(2),DATEIN+6
         LA    11,5
         B     OUT
**************************************************************
*                                                            *
*  CONVERT CENTURY DATE FROM CALENDAR TO ALPHANUMERIC        *
*                                                            *
*  FUNCTIONS: 19                                             *
*             07 18                                          *
*             04 12 37                                       *
*                                                            *
**************************************************************
CV64     EQU   *
         LA    6,DATEOUT
         LA    7,MOTABLE
         MVI   0(6),X'40'
         MVC   1(18,6),0(6)
TEST     CLC   0(2,7),DATEIN
         BE    MVNAM
         LA    7,11(0,7)
         B     TEST
MVNAM    MVC   0(9,6),2(7)
TESTA    LA    6,1(0,6)
         CLI   0(6),X'40'
         BNE   TESTA
         MVC   1(2,6),DATEIN+3
         MVI   3(6),C','
         CLI   DATEIN+8,X'40'
         BH    CV64C
         CLI   DATEIN+6,C'5'
         BL    CV64A
         MVC   4(2,6),=C'19'
         B     CV64B
CV64A    MVC   4(2,6),=C'20'
CV64B    MVC   6(2,6),DATEIN+6
         B     CV64D
CV64C    MVC   4(4,6),DATEIN+6
CV64D    LA    11,18
         B     OUT
**************************************************************
*                                                            *
*  CONVERT COMPRESSED CALENDAR TO CALENDAR                   *
*                                                            *
*  FUNCTIONS: 08 09 24 29                                    *
*                                                            *
**************************************************************
CV21     EQU   *
         MVC   DATEOUT(8),=C'  /  /  '
         MVC   DATEOUT(2),DATEIN
         MVC   DATEOUT+3(2),DATEIN+2
         MVC   DATEOUT+6(2),DATEIN+4
         MVC   DATEIN,DATEOUT
         CLC   0(2,3),=C'09'
         BE    CV16
         CLC   0(2,3),=C'24'
         BE    CV16
         CLC   0(2,3),=C'29'
         BE    CV16
         LA    11,7
         B     OUT
**************************************************************
*                                                            *
*  CONVERT CENTURY DATE FROM JULIAN TO DAY OF WEEK           *
*                                                            *
*  FUNCTIONS: 20                                             *
*             13 30 38                                       *
*             28                                             *
*             29                                             *
*                                                            *
**************************************************************
CV75     EQU   *
         ZAP   RESULT,ZEROS
         LA    10,BASEDATE
         LA    11,DATEIN
         LA    12,RESULT
         OI    RETURN+1,X'F0'
         STM   14,12,12(13)
         BAL   7,ENT2
         NI    SW,0
         LM    14,12,12(13)
         NI    RETURN+1,X'0F'
         DP    RESULT,SEVEN
         ZAP   DBLWRD,RESULT+3(1)
         CVB   2,DBLWRD
         C     2,=F'0'
         BNL   *+8
         LA    2,7(2)
         MH    2,=H'10'
         LA    2,DAYTABLE(2)
         MVC   DATEOUT(10),0(2)
         LA    11,9
         B     OUT
OUT      EQU   *
         CLC   0(2,3),=C'05'
         BL    MOVEEX
         CLC   0(2,3),=C'14'
         BE    MOVEEX
         CLC   0(2,3),=C'15'
         BE    MOVEEX
         OI    MOVEDAT+2,X'10'
MOVEEX   EX    11,MOVEDAT
         NI    MOVEDAT+2,X'EF'
         L     13,4(13)
         LM    14,12,12(13)
         SR    15,15
         BR    14
MOVEDAT  MVC   0(0,4),DATEOUT
*********************************************************************
*                                                                   *
*     TABLES, STORAGE, AND CONSTANTS                                *
*                                                                   *
*********************************************************************
MOTABLE  DC    C'01JANUARY  '
         DC    C'02FEBRUARY '
         DC    C'03MARCH    '
         DC    C'04APRIL    '
         DC    C'05MAY      '
         DC    C'06JUNE     '
         DC    C'07JULY     '
         DC    C'08AUGUST   '
         DC    C'09SEPTEMBER'
         DC    C'10OCTOBER  '
         DC    C'11NOVEMBER '
         DC    C'12DECEMBER '
DAYTABLE DC    C'1SUNDAY   '
         DC    C'2MONDAY   '
         DC    C'3TUESDAY  '
         DC    C'4WEDNESDAY'
         DC    C'5THURSDAY '
         DC    C'6FRIDAY   '
         DC    C'7SATURDAY '
ZQTAB    DC    F'0'
         DC    H'31'
         DC    H'59'
         DC    H'90'
         DC    H'120'
         DC    H'151'
         DC    H'181'
         DC    H'212'
         DC    H'243'
         DC    H'273'
         DC    H'304'
ZQE      DC    H'334'
TODCLCK  DS    D
TSVTS    DS    D
DBLWRD   DC    D'0'
X4YEARS  DC    X'072CE4E26E000000' 1,461 DAYS, NO. OF DAYS INC LEAP YR
X4YEAR   DC    X'072BA304F8000000' 1,460 DAYS, NO. OF DAYS
X366DAYS DC    X'01CC2A9EB4000000' LEAP YEAR
X365DAYS DC    X'01CAE8C13E000000' NORMAL YEAR
X1DAY    DC    X'000141DD76000000' ONE DAY
X1HOUR   DC    X'00000D693A400000' ONE HOUR
X1MINUTE DC    X'0000003938700000' ONE MINUTE
X1SECOND DC    X'00000000F4240000' ONE SECOND
X1MILSEC DC    X'00000000003E8000' ONE MILLISECOND
X1MICSEC DC    X'0000000000001000' ONE MICROSECOND
ZEROS    DC    PL4'0'
TSJULIAN DS    PL4
RESULT   DC    PL4'0'
TSLEAP   DS    CL1
SEVEN    DC    PL1'7'
BASEDATE DC    CL7'1900063'
JDATE    DS    CL7
DATEIN   DS    CL10
DATEOUT  DS    CL19
SW       DC    X'0'
CALC1    DS    F
CALC2    DS    CL7'0'
CALC3    DS    CL7'0'
         DS    0D
**************************************************************
*                                                            *
*  DATE CALCULATION ROUTINES.  IF THE FIRST ARGUMENT IS ZERO *
*  THE DIFFERENCE BETWEEN TWO DATES WILL BE CACLULATED.  IF  *
*  THE FIRST ARGUMENT IS NOT ZERO THEN IT WILL BE ADDED WITH *
*  THE SECOND ARGUMENT AND THE RESULT PLACED INTO THE THIRD  *
*  ARGUMENT.                                                 *
*                                                            *
**************************************************************
CALCRTN  L     10,4(1)
         L     11,8(1)
         L     12,0(1)
ENT2     MVC   CALC1,0(12)
         MVC   CALC2,0(10)
         MVC   CALC3,0(11)
         TM    2(12),X'0C'
         BNO   CV3CB
         MVC   CALC1,ZEROS
         MVI   CALC2,X'F0'
         MVC   CALC2+1(6),CALC2
         MVC   CALC3,CALC2
         MVC   CALC1+1(3),0(12)
         MVC   CALC2+2(5),0(10)
         MVC   CALC3+2(5),0(11)
         CLI   CALC2+2,C'5'
         BL    CV2CA
         MVC   CALC2(2),=C'19'
         B     CV2CB
CV2CA    MVC   CALC2(2),=C'20'
CV2CB    CLI   CALC3+2,C'5'
         BL    CV3CA
         MVC   CALC3(2),=C'19'
         B     CV3CB
CV3CA    MVC   CALC3(2),=C'20'
CV3CB    CP    CALC1,ZEROS
         BNE   FINDDATE
         CLC   CALC2,CALC3
         BE    RETURN
         XR    8,8
         XR    9,9
         PACK  DBLWRD,CALC2(4)
         CVB   2,DBLWRD
         PACK  DBLWRD,CALC3(4)
         CVB   3,DBLWRD
COMPYR   CR    2,3
         BE    SAMEYR
         BL    CKLEAP
         OI    SW,1
         XR    2,3
         XR    3,2
         XR    2,3
CKLEAP   LR    5,2
         C     2,=F'1900'
         BE    NOLEAP2
         C     2,=F'1800'
         BE    NOLEAP2
         C     2,=F'1700'
         BE    NOLEAP2
         N     5,=F'3'
         BC    4,NOLEAP2
         LA    8,1(8)
         B     LEAP
NOLEAP2  LA    9,1(9)
LEAP     LA    2,1(2)
         B     COMPYR
SAMEYR   MH    8,=H'366'
         MH    9,=H'365'
         AR    9,8
         PACK  DBLWRD,CALC2+4(3)
         CVB   2,DBLWRD
         PACK  DBLWRD,CALC3+4(3)
         CVB   3,DBLWRD
         TM    SW,1
         BNO   NORVRS
         XR    2,3
         XR    3,2
         XR    2,3
NORVRS   AR    9,3
         SR    9,2
         TM    SW,1
         BNO   NOTNEG
         LCR   9,9
NOTNEG   CVD   9,DBLWRD
         ZAP   CALC1,DBLWRD+4(4)
RETCALC  TM    2(12),X'0C'
         BNO   RET2
         MVC   0(3,12),CALC1+1
         MVC   0(5,10),CALC2+2
         MVC   0(5,11),CALC3+2
         B     RETURN
RET2     MVC   0(4,12),CALC1
         MVC   0(7,10),CALC2
         MVC   0(7,11),CALC3
RETURN   NOPR  7
         L     13,4(13)
         NI    SW,0
         LM    14,12,12(13)
         SR    15,15
         BR    14
FINDDATE ZAP   DBLWRD,CALC1
         CVB   2,DBLWRD
         PACK  DBLWRD,CALC2(4)
         CVB   3,DBLWRD
         PACK  DBLWRD,CALC2+4(3)
         CVB   4,DBLWRD
         AR    4,2
         LTR   2,2
         BZ    SAMDAY
         BP    POSIT
         BCT   3,NEGAT
POSIT    LCR   3,3
         B     DIFFYR
NEGAT    LTR   4,4
         BC    12,DIFFYR
         LA    3,1(3)
         B     END
DIFFYR   LPR   4,4
YR       C     4,=F'365'
         BH    INCR
         LTR   3,3
         BM    END
         OI    SW,1
INCR     LR    6,3
         LPR   6,6
         C     6,=F'1900'
         BE    DOWNYR
         C     6,=F'1800'
         BE    DOWNYR
         C     6,=F'1700'
         BE    DOWNYR
         N     6,=F'3'
         BZ    LEAP2
DOWNYR   S     4,=F'365'
         B     NOTLEAP2
LEAP2    C     4,=F'366'
         BE    END
         S     4,=F'366'
NOTLEAP2 C     4,=F'0'
         BE    COUNTYR
         TM    SW,1
         BO    END
COUNTYR  BCT   3,YR
END      LPR   3,3
         LPR   4,4
         CVD   3,DBLWRD
         UNPK  CALC3(5),DBLWRD+5(3)
         MVC   CALC3(4),CALC3+1
         CVD   4,DBLWRD
         UNPK  CALC3+4(3),DBLWRD+6(2)
         OI    CALC3+3,X'F0'
         OI    CALC3+6,X'F0'
         B     RETCALC
SAMDAY   MVC   CALC3,CALC2
         B     RETCALC
         END
